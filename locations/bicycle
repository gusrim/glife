# bicycle
$location_type = 'event_outdoors'
if $ARGS[0] = 'sarai':
	$metka = $ARGS[0]
	$loc = $CURLOC
	cla
	*clr
	minut += 1
	gs 'gadukino_event', 'sound'
	gs 'stat'
	!gs'time'

	'<center><img <<$set_imgh>> src="images/locations/gadukino/village/findvel.jpg"></center>'
	'You notice an old bike. But it looks like being in a good condition, you can probably ride it without any trouble.'
	if sunWeather = 0:'You notice an old bike. You would love to take a ride with it but you decide not to while it''s raining.'

	if hour >= 6 and hour <= 20 and sunWeather = 1 and month >= 4 and month <= 10:
		if findvelday ! daystart:
			act 'Ride the bike (1:00)':
				cls
				findvelday = daystart
				minut += 60
				run_exp += RAND(0,2)
				stren_exp += rand(0,2)
				fat -= 5
				pcs_mood += 5
				pcs_sweat += 2
				findvelrand = RAND(0,100)
				if $clothingworntype = 'nude':
					pcs_horny += 20
					pcs_willpwr += 10
					gs 'stat'
					'<center><img <<$set_imgh>> src="images/locations/gadukino/village/bike_nude.jpg"></center>'
					'You ride nude, avoiding crowded places, exulting from this newfound courage. It''s hard to say what excites you more, fear or desire to be noticed.'
					act 'Finish the trip':gt $loc, $metka
					exit
				end
				if CloSkirtShortness > 0 and findvelrand > 70:
					if $pantyworntype ! 'none':
						pcs_willpwr+=5
						gs'stat'
						'<center><img <<$set_imgh>> src="images/locations/gadukino/village/bike_tanga_1.jpg"></center>'
						'During your ride you lift your skirt several times, showing a glimpse of your panties to the passers-by.'

						gs 'arousal', 'voyeur', 5, 'exhibitionism'
						gs 'stat'

					else
						pcs_willpwr += 10
						gs'stat'
						'<center><img <<$set_imgh>> src="images/locations/gadukino/village/bike_tanga_2.jpg"></center>'
						'During your ride you lift your skirt several times, showing your bare ass to the passers-by.'

						gs 'arousal', 'voyeur', 10, 'exhibitionism'
						minut -= 5
						gs 'stat'
					end
					act 'Finish the ride':gt $loc, $metka

					gs 'arousal', 'end'
					gs 'stat'

					exit
				end
				if CloSkirtShortness > 0 and findvelrand <= 70:
					pcs_willpwr += 5
					gs'stat'
					'<center><img <<$set_imgh>> src="images/locations/gadukino/village/bike_2.jpg"></center>'
					'You take out the old bike for a ride. You ride by the endless fields enjoying the freedom you''re experiencing.'
					act 'Finish the ride':gt $loc, $metka
					exit
				end
				if CloSkirtShortness = 0:
					pcs_willpwr += 5
					gs'stat'
					'<center><img <<$set_imgh>> src="images/locations/gadukino/village/bike.jpg"></center>'
					'You ride your bike, enjoying the surrounding country-side.'
					act 'Finish the trip':gt $loc, $metka
					exit
				end
			end
		end
	end
	if goforest > 0 and sunWeather = 1 and month >= 4 and month <= 10:
		act 'Bike to the forest (0:25)':
			cls
			minut += 25
			pcs_mood += 5
			findvel = 2
			gs'stat'

			gs 'bicycle', 'clothes_findvel'
			'You jump onto the bike and ride to the forest.'
			gs 'bicycle', 'ridego', 'gadforest','1'
		end
	end
	if goswamp = 1 and sunWeather = 1 and month >= 4 and month <= 10:
		act 'Bike to the swamp (1:15)':
			cls
			minut += 75
			run_exp += RAND(0,1)
			stren_exp += rand(0,1)
			fat -= 1
			pcs_mood += 5
			pcs_sweat += 1
			findvel = 3
			gs'stat'
			!gs'time'

			gs 'bicycle', 'clothes_findvel'
			'You get on the bike and ride it to the swamp.'

			gs 'bicycle', 'ridego', 'swamp_yard'
		end
	end
	act 'Put the bike away': gt'Gadsarai'
end

if $ARGS[0] = 'forest':
	$metka = $ARGS[0]
	$loc = $CURLOC
	cla
	*clr
	minut += 1
	gs 'gadukino_event', 'sound'
	gs 'stat'
	!gs'time'

	'<center><img <<$set_imgh>> src="images/locations/gadukino/village/findvel.jpg"></center>'

	act 'Bike to the village (0:25)':
		dirtyclothes = 0 & swamp_clothes = 0
		if $clothingworntype ! 'nude' or pcs_exhibition > 50:
			cls
			pcs_mood += 10
			findvel = 1
			minut += 25
			exhibition_exp += 4
			gs'stat'

			gs 'bicycle', 'clothes_findvel'
			'You ride your bike out of the forest and return to the village.'
			gs 'bicycle', 'ridego', 'Gaddvor'
		else
			cls
			'<center><img <<$set_imgh>> src="images/locations/gadukino/village/findvel_nude.jpg"></center>'
			'You take off your clothes and jump on your bike and ride through the forest. You would never be able to do this in the village.'
			act 'Further':gt'gadforest','1'
		end
	end
	if goswamp = 1:
		act 'Bike to the swamp (0:50)':
			cls
			minut += 50
			pcs_mood += 5
			findvel = 3
			gs'stat'
			!gs'time'

			gs 'bicycle', 'clothes_findvel'
			'You ride your bike to the edge of the swamp.'

			gs 'bicycle', 'ridego', 'swamp_yard'
		end
	end
	act 'Put the bike aside': gt'gadforest','1'
end

if $ARGS[0] = 'swamp':
	$metka = $ARGS[0]
	$loc = $CURLOC
	cla
	*clr
	minut += 1
	gs 'gadukino_event', 'sound'
	gs 'stat'
	!gs'time'

	'<center><img <<$set_imgh>> src="images/locations/gadukino/village/findvel.jpg"></center>'

	act 'Bike to the village (1:15)':
		dirtyclothes = 0 & swamp_clothes = 0
		if $clothingworntype ! 'nude' or pcs_exhibition > 50:
			cls
			minut += 75
			run_exp += RAND(0,1)
			stren_exp += rand(0,1)
			fat -= 1
			pcs_sweat += 1
			pcs_mood += 10
			findvel = 1
			hanters = 0
			exhibition_exp += 4
			gs'stat'
			!gs'time'

			gs 'bicycle', 'clothes_findvel'
			'You ride your bike out of the swamp and return to the village.'

			gs 'bicycle', 'ridego', 'Gaddvor'
		else
			cls
			'<center><img <<$set_imgh>> src="images/locations/gadukino/village/findvel_nude.jpg"></center>'
			'You take off your clothes and jump on your bike and ride through the swamp. You would never be able to do this in the village.'
			act 'Further':gt'swamp_yard'
		end
	end
	act 'Bike to the forest (0:50)':
		cls
		minut += 50
		pcs_mood += 5
		hanters = 0
		findvel = 2
		gs'stat'
		!gs'time'

		gs 'bicycle', 'clothes_findvel'
		'You jump onto your bike and ride to the forest.'
		gs 'bicycle', 'ridego', 'gadforest','1'
	end
	act 'Put the bike aside': gt'swamp_yard'
end

if $ARGS[0] = 'clothes_findvel':
	if $clothingworntype = 'nude':
		'<center><img <<$set_imgh>> src="images/locations/gadukino/village/bike_nude.jpg"></center>'
	elseif CloSkirtShortness > 0:
		'<center><img <<$set_imgh>> src="images/locations/gadukino/village/bike_2.jpg"></center>'
	else
		'<center><img <<$set_imgh>> src="images/locations/gadukino/village/bike.jpg"></center>'
	end
end

if $ARGS[0] = 'ridego':
	!make this args, add a warning, and allow 30% alive by illume
	$metka = $ARGS[2]
	$loc = $ARGS[1]
	if hour >= 6 and hour < 22:
		act 'Further':gt $loc, $metka
	else
		'<B><font color=red>Are You sure? It''s still quite dark outside.</font></B>'
		act '<font color=red>Yes, Let''s go!</font>':
			if rand(1,10)>3:
				over = 12 & gt'gameover'
			else
				gt $loc, $metka
			end
		end
		act 'Put the bike away': gt $loc, $metka
	end
end


--- bicycle ---------------------------------

