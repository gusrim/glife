# stat_display

gs 'outdoors', 'main'

!!Sex stat update
stat['mast'] += mastr & mastr = 0
stat['hj'] += hj & hj = 0
stat['bj'] += bj & bj = 0
stat['vaginal'] += sex & sex = 0
stat['anal'] += anal & anal = 0
stat['cuni'] += kuni & kuni = 0
stat['anal_strap_give'] += pegging & pegging = 0

!!!!!!!!!!!!!!!!!!!!!
!!!Pain calculation!!!
!!!!!!!!!!!!!!!!!!!!!

pain['body1'] = pain['head'] + pain['hair'] + pain['ears'] + pain['eyebrows'] + pain['eyes'] + pain['cheeks'] + pain['nose'] +  pain['mouth'] + pain['lips'] + pain['tongue']
pain['body2'] = pain['throat'] + pain['neck'] + pain['back'] + pain['asscheeks'] + pain['asshole'] + pain['hips'] + pain['thighs'] + pain['legL'] + pain['legR'] + pain['feet']
pain['body3'] = pain['toes'] + pain['shoulders'] + pain['armL'] + pain['armR'] + pain['hands'] + pain['fingers'] + pain['chest'] + pain['breasts'] + pain['nipples'] + pain['ribs']
pain['body4'] = pain['tummy'] + pain['pubic'] + pain['vaginal'] + pain['labia'] + pain['clitoris'] + pain['urethra'] + pain['cervix']

pain['total'] = pain['body1'] + pain['body2'] + pain['body3'] + pain['body4'] + (vgape * 8) + (agape * 10) + (spanked * 24)
if pain['total'] > 100: pain['total'] = 100

pain['relief'] = 0

if alko > 6:
	pain['relief'] = 50
elseif alko > 3:
	pain['relief'] = 20
end

if StrongNarkota > 0:
	pain['relief'] = 90
elseif nark > 0:
	pain['relief'] = 60
elseif pain['killer'] > 12:
	pain['relief'] = 50
end

if pain['relief'] ! 0:
	if (pain['total'] / pain['relief']) > 0:
		pain['total'] = pain['total'] / pain['relief']
	elseif pain['total'] > 0:
		pain['total'] = 0
	end
end

if pain['total'] > 80:
	if (pcs_health * 5) >= healthmax: pcs_health = pcs_health * 20 / 100
	if (pcs_mood * 5) >= 100: pcs_mood = pcs_mood * 20 / 100
elseif pain['total'] > 60:
	if (pcs_health * 5 / 2) >= healthmax: pcs_health = pcs_health * 40 / 100
	if (pcs_mood * 5 / 2) >= 100: pcs_mood = pcs_mood * 40 / 100
elseif pain['total'] > 40:
	if (pcs_health * 5 / 3) >= healthmax: pcs_health = pcs_health * 60 / 100
	if (pcs_mood * 5 / 3) >= 100: pcs_mood = pcs_mood * 60 / 100
elseif pain['total'] > 20:
	if (pcs_health * 5 / 4) >= healthmax: pcs_health = pcs_health * 80 / 100
	if (pcs_mood * 5 / 4) >= 100: pcs_mood = pcs_mood * 80 / 100
elseif pain['total'] > 0:
	if (pcs_health * 10 / 9) >= healthmax: pcs_health = pcs_health * 90 / 100
	if (pcs_mood * 10 / 9) >= 100: pcs_mood = pcs_mood * 90 / 100
end

!!!!!!!!!!!!!!!!!!!!!
!!!Beginning Description!!!
!!!!!!!!!!!!!!!!!!!!!

if pcs_sleep > 100: pcs_sleep = 100

if Enable_statfsize>0:$stat_msg = '<font size=<<Enable_statfsize>>><<$stat_msg>>'

$stat_android = '<b><<$mid(100+hour,2,2)>>:<<$mid(100+minut,2,2)>></b>'

$stat_msg += '<<$weather>><BR>'
$stat_msg += '<BR><<$stat_android>> <<$week[week]>> <<day>> <<$month>> <<year>>'
$stat_android += '<<$week[week]>> <<day>>/<<month>>, <<money>> ₽, <a href="exec:clr & pl $stat_android">Status</a>'
! $stat_android for short desc in android mode, $stat_msg for status bar.

$stat_msg += '<BR>'
$pockets = 'pockets'
if bag > 0: $pockets = 'purse'
$stat_msg += '<BR>You have <b><<money>> ₽</b> in your <<$pockets>>.'
if bankAccount = 1:
	if karta >= 0:
		$stat_msg += '<BR>You have <b><<karta>> ₽</b> in your bank account.'
	else
		$stat_msg += '<BR><font color="red">Your bank account is overdrawn by <b><<karta>> ₽</b>!!</font>'
	end
end
if stolmoney > 0: $stat_msg += '<BR>You have <b><<stolmoney>> ₽</b> in your desk drawer.'
$stat_msg += '<BR>'

if ETOmenu = 1:
	$stat_msg += '<BR><b><<$pcs_health>></b>'
	$stat_msg += '<BR><b><<$pcs_willpwr>></b>'
	$stat_msg += '<BR><b><<$pcs_energy>></b>'
	$stat_msg += '<BR><b><<$water>></b>'
	$stat_msg += '<BR><b><<$pcs_sleep>></b>'
elseif Enable_scalepic = 2:
$st_temp='rgb(<<(16777216 + fcolor) mod 256>>,<<(16777216 + fcolor)/256 mod 256>>,<<(16777216 + fcolor)/65536>>)'
$stat_msg += '<p><font color="<<$st_temp>>"><TABLE width="300" cellpadding="1" cellspacing="0"><TR><TD width="85%">'+FUNC('indiktab', '2', pcs_horny, pcs_horny)+'</TD><TD> Arousal</TD></TR><TR><TD width="85%">'+FUNC('indiktab', '2', pain['total'], pain['total'])+'</TD><TD> Pain</TD></TR><TR><TD width="85%">'+FUNC('indiktab', '1', pcs_health * 100 / healthmax, pcs_health)+'</TD><TD> Health</TD></TR>'
if MagikDostup = 0: $stat_msg += '<TR><TD width="85%">'+FUNC('indiktab', '1', pcs_manna * 100 / mannamax, pcs_manna)+'</TD><TD> Manna</TD></TR>'
$stat_msg += '<TR><TD width="85%">'+FUNC('indiktab', '1', pcs_willpwr * 100 / willpowermax, pcs_willpwr)+'</TD><TD> Willpower</TD></TR><TR><TD width="85%">'+FUNC('indiktab', '1', pcs_stam * 100 / stammax, pcs_stam)+'</TD><TD> Stamina</TD></TR><TR><TD width="85%">'+FUNC('indiktab', '1', pcs_mood, pcs_mood)+'</TD><TD> Mood</TD></TR><TR><TD width="85%">'+FUNC('indiktab', '1', pcs_energy, pcs_energy)+'</TD><TD> Hunger</TD></TR><TR><TD width="85%">'+FUNC('indiktab', '1', pcs_sleep, pcs_sleep)+'</TD><TD> Sleep</TD></TR></TABLE></font></p>'
else
	gs 'indik', '2', pcs_horny, pcs_horny
	$stat_msg += '<BR><<$ind>> Arousal'

	gs 'indik', '2', pain['total'], pain['total']
	$stat_msg += '<BR><<$ind>> Pain'

	gs 'indik', '1', pcs_health * 100 / healthmax, pcs_health
	$stat_msg += '<BR><<$ind>> Health'

	if MagikDostup = 0:
		gs 'indik', '1', pcs_manna * 100 / mannamax, pcs_manna
		$stat_msg += '<BR><<$ind>> Manna'
	end

	gs 'indik', '1', pcs_willpwr * 100 / willpowermax, pcs_willpwr
	$stat_msg += '<BR><<$ind>> Willpower'

	gs 'indik', '1', pcs_stam * 100 / stammax, pcs_stam
	$stat_msg += '<BR><<$ind>> Stamina'

	gs 'indik', '1', pcs_mood, pcs_mood
	$stat_msg += '<BR><<$ind>> Mood'

	gs 'indik', '1', pcs_energy, pcs_energy
	$stat_msg += '<BR><<$ind>> Hunger'

	gs 'indik', '1', pcs_sleep, pcs_sleep
	$stat_msg += '<BR><<$ind>> Sleep'
end
$stat_msg += '<BR>'

if Enable_showattr = 0:
	$stat_msg += '<BR> <a href="exec:Enable_showattr = 1 & gs ''$menu_obnovit''">Attributes</a><BR>'
end
if Enable_showattr = 1:
	$stat_msg += '<BR> <a href="exec:Enable_showattr = 0 & gs ''$menu_obnovit''">Attributes:</a>'
	if Enable_scalepic = 2:
		if pcs_dom > 10:
			$dom_stat_msg = 'dominant'
		elseif sub > 10:
			$dom_stat_msg = 'submissive'
		else
			$dom_stat_msg = 'neutral'
		end

		$stat_msg += '<font color="<<$st_temp>>"><TABLE cellpadding="1" cellspacing="0" width="300"><TR><TD width="85%">'+FUNC('indiktab', '3', 100+pcs_dom-sub, 100+pcs_dom-sub,$dom_stat_msg)+'</TD><TD> Dominance</TD></TR>  <TR><TD width="85%">'+FUNC('indiktab', '1', pcs_stren, pcs_stren)+'</TD><TD> Strength</TD></TR><TR><TD width="85%">'+FUNC('indiktab', '1', pcs_agil, pcs_agil)+'</TD><TD> Agility</TD></TR><TR><TD width="85%">'+FUNC('indiktab', '1', pcs_vital, pcs_vital)+'</TD><TD> Endurance</TD></TR><TR><TD width="85%">'+FUNC('indiktab', '1', pcs_intel, pcs_intel)+'</TD><TD> Intelligence</TD></TR><TR><TD width="85%">'+FUNC('indiktab', '1', pcs_sprt, pcs_sprt)+'</TD><TD> Spirit</TD></TR><TR><TD width="85%">'+FUNC('indiktab', '1', pcs_react, pcs_react)+'</TD><TD nowrap> Mental Agility</TD></TR><TR><TD width="85%">'+FUNC('indiktab', '1', pcs_chrsm, pcs_chrsm)+'</TD><TD> Charisma</TD></TR><TR><TD width="85%">'+FUNC('indiktab', '1', pcs_prcptn, pcs_prcptn)+'</TD><TD> Perception</TD></TR>'
		if MagikDostup = 0: $stat_msg += '<TR><TD width="85%">'+FUNC('indiktab', '1', pcs_magik, pcs_magik)+'</TD><TD> Magic</TD></TR>'
		$stat_msg += '<TR><TD width="85%">'+FUNC('indiktab', '1', pcs_apprnc / 2, pcs_apprnc)+'</TD><TD> Attractiveness</TD></TR>'
		if SchoolAtestat = 0 and StoryLine > 0:$stat_msg += '<TR><TD width="85%">'+FUNC('indiktab', '1', pcs_grades, pcs_grades)+'</TD><TD nowrap> School Grades</TD></TR>'
		if pcs_exhibition > 0:$stat_msg += '<TR><TD width="85%">'+FUNC('indiktab', '1', pcs_exhibition, pcs_exhibition)+'</TD><TD> Exhibitionism</TD></TR></TABLE></font>'
		killvar '$dom_stat_msg'
	else
		gs 'indik','3', 50+(pcs_dom-sub)/2, 50+(pcs_dom-sub)/2
		$stat_msg += '<BR><<$ind>> Dominance'
		if pcs_dom > 10:
			$stat_msg += ' (dominant)'
		elseif sub > 10:
			$stat_msg += ' (submissive)'
		else
			$stat_msg += ' (natural)'
		end
!		$stat_msg += '<BR>'
		gs 'indik', '1', pcs_stren, pcs_stren
		$stat_msg += '<BR><<$ind>> Strength'
		gs 'indik', '1', pcs_agil, pcs_agil
		$stat_msg += '<BR><<$ind>> Agility'
		gs 'indik', '1', pcs_vital, pcs_vital
		$stat_msg += '<BR><<$ind>> Endurance'
		gs 'indik', '1', pcs_intel, pcs_intel
		$stat_msg += '<BR><<$ind>> Intelligence'
		gs 'indik', '1', pcs_sprt, pcs_sprt
		$stat_msg += '<BR><<$ind>> Spirit'
		gs 'indik', '1', pcs_react, pcs_react
		$stat_msg += '<BR><<$ind>> Mental Agility'
		gs 'indik', '1', pcs_chrsm, pcs_chrsm
		$stat_msg += '<BR><<$ind>> Charisma'
		gs 'indik', '1', pcs_prcptn, pcs_prcptn
		$stat_msg += '<BR><<$ind>> Perception'
		if MagikDostup = 0:
			gs 'indik', '1', pcs_magik, pcs_magik
			$stat_msg += '<BR><<$ind>> Magic'
		end
		gs 'indik', '1', pcs_apprnc / 2, pcs_apprnc
		$stat_msg += '<BR><<$ind>> Attractiveness'
	
		if SchoolAtestat = 0 and StoryLine > 0:
			gs 'indik', '1', pcs_grades, pcs_grades
			$stat_msg += '<BR><<$ind>> School Grades'
		end
		if pcs_exhibition > 0:
			gs 'indik', '1', pcs_exhibition, pcs_exhibition
			$stat_msg += '<BR><<$ind>> exhibitionism'
		end
		$stat_msg += '<BR>' 
	end
end
killvar '$st_temp'
!!----------------Mental skills--------------------------
if Enable_show_mental_skill = 0:
	$stat_msg += ' <a href="exec:Enable_show_mental_skill = 1 & gs ''$menu_obnovit''">Mental Skills</a>'
end
if Enable_show_mental_skill = 1:
	st_count=0
	$show_table=''
	if pcs_splcstng > 0: gs 'show_table', '<<pcs_splcstng>> Spell Casting', '2'
	if pcs_humint > 0: gs 'show_table', '<<pcs_humint>> People skills', '2'
	if pcs_persuas > 0: gs 'show_table', '<<pcs_persuas>> Persuasion', '2'
	if pcs_observ > 0: gs 'show_table', '<<pcs_observ>> Observation', '2'
	if st_count > 0 :$stat_msg += ' <a href="exec:Enable_show_mental_skill = 0 & gs ''$menu_obnovit''">Mental Skills:</a> <TABLE BORDER=1><<$show_table>></TR></TABLE>'
end

!!-----------------Sport skills--------------------------
if Enable_show_sport_skill = 0:
	$stat_msg += '<BR> <a href="exec:Enable_show_sport_skill = 1 & gs ''$menu_obnovit''">Sport Skills</a>'
end
if Enable_show_sport_skill = 1:
	st_count=0
	$show_table=''
	if pcs_jab > 0: gs 'show_table', '<<pcs_jab>> Jabs', '2'
	if pcs_punch > 0: gs 'show_table', '<<pcs_punch>> Power Strikes', '2'
	if pcs_kick > 0: gs 'show_table', '<<pcs_kick>> Kicks', '2'
	if pcs_def > 0: gs 'show_table', '<<pcs_def>> Defence', '2'
	if pcs_run > 0: gs 'show_table', '<<pcs_run>> Running', '2'
	if pcs_vball > 0: gs 'show_table', '<<pcs_vball>> Volleyball', '2'
	if pcs_ftbll > 0: gs 'show_table', '<<pcs_ftbll>> Football', '2'
	if pcs_wrstlng > 0: gs 'show_table', '<<pcs_wrstlng>> Wrestling', '2'
	if pcs_shoot > 0: gs 'show_table', '<<pcs_shoot>> Marksmanship', '2'
	if pcs_chess > 0: gs 'show_table', '<<pcs_chess>> Chess', '2'
	if pcs_icesktng > 0: gs 'show_table', '<<pcs_icesktng>> Ice Skating', '2'
	if pcs_ttgmng > 0: gs 'show_table', '<<pcs_ttgmng>> Gaming', '2'
	if st_count > 0 :$stat_msg += '<BR> <a href="exec:Enable_show_sport_skill = 0 & gs ''$menu_obnovit''">Sport Skills:</a> <TABLE BORDER=1><<$show_table>></TR></TABLE>'
end

!!-----------------Beauty/Modelling skills---------------
if Enable_show_beauty_skill = 0:
	$stat_msg += '<BR> <a href="exec:Enable_show_beauty_skill = 1 & gs ''$menu_obnovit''">Beauty/Modelling Skills</a>'
end
if Enable_show_beauty_skill = 1:
	st_count=0
	$show_table=''
	if pcs_makupskl > 0: gs 'show_table', '<<pcs_makupskl>> Makeup Skill', '2'
	if pcs_danc > 0: gs 'show_table', '<<pcs_danc>> Modern Dancing', '2'
	if pcs_dancero > 0: gs 'show_table', '<<pcs_dancero>> Erotic Dancing', '2'
	if pcs_dancpol > 0: gs 'show_table', '<<pcs_dancpol>> Pole Dancing', '2'
	if pcs_mdlng > 0: gs 'show_table', '<<pcs_mdlng>> Modelling', '2'
	if st_count > 0 :$stat_msg += '<BR> <a href="exec:Enable_show_beauty_skill = 0 & gs ''$menu_obnovit''">Beauty/Modelling Skills:</a> <TABLE BORDER=1><<$show_table>></TR></TABLE>'
end

!!---------------------Artistic skills-------------------
if Enable_show_art_skill = 0:
	$stat_msg += '<BR> <a href="exec:Enable_show_art_skill = 1 & gs ''$menu_obnovit''">Artistic Skills</a>'
end
if Enable_show_art_skill = 1:
	st_count=0
	$show_table=''
	if pcs_vokal > 0: gs 'show_table', '<<pcs_vokal>> Singing', '2'
	if pcs_instrmusic > 0: gs 'show_table', '<<pcs_instrmusic>> Instrumental Music', '2'
	if pcs_photoskl > 0: gs 'show_table', '<<pcs_photoskl>> Photography', '2'
	if pcs_artskls > 0: gs 'show_table', '<<pcs_artskls>> Artistic Skills', '2'
	if st_count > 0 :$stat_msg += '<BR> <a href="exec:Enable_show_art_skill = 0 & gs ''$menu_obnovit''">Artistic Skills:</a> <TABLE BORDER=1><<$show_table>></TR></TABLE>'
end

!!----------------------Job skills------------------------
if Enable_show_job_skill = 0:
	$stat_msg += '<BR> <a href="exec:Enable_show_job_skill = 1 & gs ''$menu_obnovit''">Job Skills</a>'
end
if Enable_show_job_skill = 1:
	st_count=0
	$show_table=''
	if pcs_compskl > 0: gs 'show_table', '<<pcs_compskl>> Computer Skill', '2'
	if pcs_comphckng > 0: gs 'show_table', '<<pcs_comphckng>> Hacking', '2'
	if pcs_hndiwrk > 0: gs 'show_table', '<<pcs_hndiwrk>> Handy-work', '2'
	if pcs_sewng > 0: gs 'show_table', '<<pcs_sewng>> Tailoring', '2'
	if pcs_servng > 0: gs 'show_table', '<<pcs_servng>> Serving', '2'
	if pcs_medcn > 0: gs 'show_table', '<<pcs_medcn>> Medicine', '2'
	if workPTU > 1: gs 'show_table','<<teachlevel>> Teacher credibility','2'
	if st_count > 0 :$stat_msg += '<BR> <a href="exec:Enable_show_job_skill = 0 & gs ''$menu_obnovit''">Job Skills:</a> <TABLE BORDER=1><<$show_table>></TR></TABLE>'
end

if Enable_showrelation = 0:
	$stat_msg += '<BR> <a href="exec:Enable_showrelation = 1 & gs ''$menu_obnovit''">Relations</a>'
end
if Enable_showrelation = 1:
	st_count=0
	$show_table=''
	if npc_QW['A192'] > 0:gs'show_table','<<npc_QW[''A192'']>> Nastja','2'
	if tanya > 0:gs'show_table','<<tanya>> Tanya','2'
	if kat > 0:gs'show_table','<<kat>> Kate','2'
	if vika > 0:gs'show_table','<<vika>> Vika','2'
	if Irina > 0:gs'show_table','<<Irina>> Irina','2'
	if Tamara > 0:gs'show_table','<<Tamara>> Tamara','2'
	if alla > 0:gs'show_table','<<alla>> Alla','2'
	if masha > 0:gs'show_table','<<masha>> Masha','2'
	if Givi > 0:gs'show_table','<<Givi>> Givi Karapetovich','2'
	if Ashot > 0:gs'show_table','<<Ashot>> Ashot','2'
	if Dmitry > 0:gs'show_table','<<Dmitry>> Dima','2'
	if afra > 0:gs'show_table','<<afra>> Blacks','2'
	if npc_rel['A69'] > 0:gs'show_table','<<npc_rel[''A69'']>> Mikhail Nikolaevich','2'
	if npc_rel['A89'] > 0:gs'show_table','Eugene: <<npc_rel[''A89'']>>','2'
	if npc_rel['A112'] > 0:gs'show_table','Sergei Shulgin: <<npc_rel[''A112'']>>','2'
	if npc_rel['A11'] > 0:gs'show_table','Vasily Shulgin: <<npc_rel[''A11'']>>','2'
	if hantersAndreiQw > 0:gs'show_table','Hunter Andrew: <<hantersAndreiQw>>','2'
	if hantersSergeiQw > 0:gs'show_table','Hunter Sergei: <<hantersSergeiQw>>','2'
	if hantersIgorQw > 0:gs'show_table','Hunter Igor: <<hantersIgorQw>>','2'
	if StoryLine > 0:
		if npc_rel['A29'] > 0:gs'show_table','<<npc_rel[''A29'']>> mother','2'
		if npc_rel['A28'] > 0:gs'show_table','<<npc_rel[''A28'']>> father','2'
		if npc_rel['A33'] > 0:gs'show_table','<<npc_rel[''A33'']>> Sister','2'
		if npc_rel['A34'] > 0:gs'show_table','<<npc_rel[''A34'']>> Brother','2'
	end
	if st_count > 0 :$stat_msg += '<BR> <a href="exec:Enable_showrelation = 0 & gs ''$menu_obnovit''">Relations:</a> <BR><TABLE BORDER=1> <<$show_table>></TR></TABLE>'
end
	$stat_msg += '<BR>'

if BuyHous = 2 and ArendHouseSL <= 0:'<font color="red"><b>The rent on your apartment is due today.</b></font>'

if BuyHous = 2 and ArendHouseSL <= 0:
	'<font color="red"><b>The rent on your apartment is due today.</b></font>'
	if money >= 6000 or stolmoney >= 6000 or money + stolmoney >= 6000 or karta + bankDebtLimit >= 6000:
		*P ' Renew with '
		if money >= 6000:
			*P ' <a href="exec:money -= 6000 & housr = 1 & BuyHous = 2 & ArendHouseSL += 30" & gs ''$menu_obnovit''>cash</a>'
		elseif stolmoney >= 6000:
			*P ' <a href="exec:stolmoney -= 6000 & housr = 1 & BuyHous = 2 & ArendHouseSL += 30" & gs ''$menu_obnovit''>cash from drawer</a>'
		elseif money + stolmoney >= 6000:
			!both < 6000, so money = money - 6000 + stolmoney
			*P ' <a href="exec:money -= 6000 - stolmoney & stolmoney = 0 & housr = 1 & BuyHous = 2 & ArendHouseSL += 30" & gs ''$menu_obnovit''>cash from purse and drawer</a>'
		end
		!from purse, drawer or both
		if karta >= 6000: 
			*P ' <a href="exec:karta -= 6000 & housr = 1 & BuyHous = 2 & ArendHouseSL += 30 & gs ''$menu_obnovit''">bank account</a>'
		elseif karta + bankDebtLimit >= 6000: 
			*P ' <a href="exec:karta -= 6000 & housr = 1 & BuyHous = 2 & ArendHouseSL += 30 & gs ''$menu_obnovit''">bank account, but you will be overdrawn.</a>'
		end	
	else
		*P ' But you can''t afford to pay the rent.'
	end
end

if opPRE = 0: $stat_msg += '<BR><<$pcs_makeup>> and your hair is <<$pcs_hairbsh>>.' & $stat_msg += '<BR>'

if thinkpreg = 0 and knowpreg = 0 and cycle ! 6 and pillcon2 <= 30000 and succubusflag ! 1 and cheatSlut = 0 and mesec = 0:
	if abortionbirthdate = 0 and daystart - lastmens > 21:
		if daystart - lastmens < 28:
			$stat_msg += '<BR><font color = #AB052A><b>Your period is late by <<daystart - lastmens - 20>> days.</b></font>'
		elseif daystart - lastmens < 35:
			$stat_msg += '<BR><font color = #AB052A><b>Your period is late by more than a week!</b></font>'
		elseif daystart - lastmens < 45:
			$stat_msg += '<BR><font color = #AB052A><b>You apparently missed your period this month. Maybe you should see a doctor?</b></font>'
		elseif daystart - lastmens < 69:
			$stat_msg += '<BR><font color = #AB052A><b>You didn''t have a period for two months. You really should see a doctor about it.</b></font>'
		else
			$stat_msg += '<BR><font color = #AB052A><b>You should consider the possibility of a pregnancy, since apparently you no longer have periods.</b></font>'
		end
	elseif abortionbirthdate ! 0 and daystart - abortionbirthdate <= 100:
		$stat_msg += '<BR><font color = #705243><b>You are probably still in your recovery period. Nothing to worry about.</b></font>'
	elseif abortionbirthdate ! 0 and daystart - abortionbirthdate > 100:
		$stat_msg += '<BR><font color = #820000><b>You should have recovered by know... Right?</b></font>'
	end
end
if implant_day > 0:
	if ((daystart - implant_day) * 24 + hour - implant_hour) < 5:
		$stat_msg += '<BR><font color=#AB052A>You feel a slight twinge in your abdomen.</font>'
	end
end
if mesec > 0:
	$stat_msg += '<BR><font color="red">You are currently having your period. '
	if isprok = 0 and isprokp = 0:
		$stat_msg += 'You need to use your feminine hygiene products.'
	elseif isprokp = 1 and tanga = 0:
		isprokp = 0
		$stat_msg += 'You need to wear panties to use a sanitary pad.'
	end
	$stat_msg += '</font>'
end
$stat_msg += '<BR><a href="exec:dynamic $d_cycreport_choice"><<$cycreport_txt>></a><br>'

if StoryLine > 0 and SchoolAtestat = 0 and $holyday ! '': $stat_msg += '<BR><<$holyday>>' & $stat_msg += '<BR>'
if birthday = day and birthmonth = month: $stat_msg += '<BR><b>Today is your birthday.</b>' & $stat_msg += '<BR>'

!!if shame > 0:$stat_msg += '<BR><<$shame>>' & $stat_msg += '<BR>'

if $pcs_mood ! '':$stat_msg += '<BR><font color="green"><b><<$pcs_mood>></b></font>' & $stat_msg += '<BR>'

if mentats_dose >= 1:$stat_msg += '<BR><font color = green>You feel sharp and focused.</font>'
if bcream_used = 1:$stat_msg += '<BR><font color = #DB7093>Your breasts are feeling hot and tingly.</font>'
if aphrodisiac_timer > 0:$stat_msg += '<BR><font color = #DB7093>You are feeling hot and every move causes your groin to tingle.</font>'

if pcs_energy < 5:
	$stat_msg += '<BR><font color="red">You feel faint from hunger, if you don''t have something to eat very soon, you will pass out.</font>'
elseif pcs_energy < 10:
	$stat_msg += '<BR><font color="red">You are starving, you really need to have something to eat.</font>'
elseif pcs_energy < 20:
	$stat_msg += '<BR>You are very hungry and struggle to concentrate on anything other than food.'
elseif pcs_energy < 30:
	$stat_msg += '<BR>You feel peckish and your thoughts sometimes drift towards food.'
end

if pcs_sleep < 5:
	$stat_msg += '<BR><font color="red">You can''t keep your eyes open, if you don''t go to bed now, you will fall asleep right here.</font>'
elseif pcs_sleep < 10:
	$stat_msg += '<BR><font color="red">You are exhausted, you really need to go to bed soon.</font>'
elseif pcs_sleep < 20:
	$stat_msg += '<BR>You are tired and you are struggling to concentrate.'
elseif pcs_sleep < 30:
	$stat_msg += '<BR>You feel a little tired.'
end

if tanga = 1 and clit_size >= 25:$stat_msg += '<BR><font color = #FF1493>Your panties keep rubbing against your clit and making you horny.</font>'

if pcs_horny >= 50:
	if pcs_horny < 90 and tanga = 1:
		$stat_msg += '<BR><font color = #DB7093>You are a little nervous, feeling nice between your legs <a href="exec:view''images/pc/body/pussy/briefs1.jpg''">itchy</a> from arousal.</font>'
	elseif pcs_horny < 90 and tanga = 0:
		$stat_msg += '<BR><font color = #DB7093>You are a little nervous, feeling nice between your legs <a href="exec:view''images/pc/body/pussy/briefs1a.jpg''">itchy</a> from arousal.</font>'
	elseif tanga = 1:
		$stat_msg += '<BR><font color = #FF1493>You can''t stop thinking about sex, between your legs you are <a href="exec:view''images/pc/body/pussy/briefs2.jpg''">soaked</a> from your juices.</font>'
	else
		$stat_msg += '<BR><font color = #FF1493>You can''t stop thinking about sex, between your legs you are <a href="exec:view''images/pc/body/pussy/briefs2a.jpg''">soaked</a> from your juices.</font>'
	end
end

if PillToggle = 1 and tabletki > 0:
	Stabletki += tabletki
	tabletki = 0
elseif PillToggle = 0 and Stabletki > 0:
	tabletki += Stabletki
	Stabletki = 0
end

if CondomToggle = 1 and prezik > 0:
	Sprezik += prezik
	prezik = 0
elseif CondomToggle = 0 and Sprezik > 0:
	prezik += Sprezik
	Sprezik = 0
end

if smoker >= 20:
	if smokerNeed > 0:
		if siga > 0:
			$stat_msg += '<BR><a href="exec:gs ''shortgs'',''smoker''"><font color="red"><b>You want to smoke. You have <<siga>> cigarettes left.</b></font></a><BR>'
		else
			$stat_msg += '<BR><a href="exec:gs ''shortgs'',''smoker''"><font color="red"><b>You want to smoke, but you have no cigarettes left.</b></font></a><BR>'
		end
	else
		if siga > 0:
			$stat_msg += '<BR><a href="exec:gs ''shortgs'',''smoker''"><font color="blue"><b>You have <<siga>> cigarettes left.</b></font></a><BR>'
		else
			$stat_msg += '<BR><font color="blue"><b>You feel like you should get some cigarettes.</b></font><BR>'
		end
	end
elseif siga > 0:
	$stat_msg += '<BR><a href="exec:gs ''shortgs'',''smoker''"><font color = blue><b>You have <<siga>> cigarettes.</b></font></a><BR>'
end

if smokeHour = hour and smokeday = day and smokeminut >= minut:$stat_msg += '<BR><font color="green">You are smoking a cigarette.</font><BR>'

if pcs_sweat < 0 : pcs_sweat = 0
if pcs_sweat > 69 : pcs_sweat = 69
jump 'statDswt<<pcs_sweat/10>>'
:statDswt0
	if arrsize('sparrvol') = 0 : $stat_msg += '<BR><b><font color="green">You are sparkling clean.</font></b><BR>'
	jump 'statDswtEnd'
:statDswt1
	jump 'statDswtEnd'
:statDswt2
	$stat_msg += '<BR><font color="brown">You''re sweating.</font><BR>'
	jump 'statDswtEnd'
:statDswt3
	$stat_msg += '<BR><font color="red">You are a little smelly.</font><BR>'
	jump 'statDswtEnd'
:statDswt4
	$stat_msg += '<BR><font color="red">You stink.</font><BR>'
	jump 'statDswtEnd'
:statDswt5
	$stat_msg += '<BR><b><font color="red">You really stink.</font></b><BR>'
	jump 'statDswtEnd'
:statDswt6
	$stat_msg += '<BR><b><font color="red">You are dripping wet from sweat and smell like a gym sock.</font></b><BR>'
	jump 'statDswtEnd'

:statDswtEnd

if dirtyclothes = 1 and swamp_clothes = 0:
	if pcs_sweat > 40:
		$stat_msg +='<br><font color = red>You from head to toe <a href="exec:view''images/locations/gadukino/hunters/dirtyclothes1.jpg''">smeared</a> in the mud.</font><BR>'
	else
		$stat_msg +='<br><font color = red>Your <a href="exec:view''images/locations/gadukino/hunters/dirtyclothes2.jpg''">cloth</a> is dirty.</font><BR>'
	end
end

if lactate = 1:
	if lactatemv >= lactatemm-1:
		$stat_msg += '<BR><font color = black>Your breasts are so full they hurt and a little milk is constantly dribbling out.</font><BR>'
	elseif lactatemv > lactatemm*9/10:
		$stat_msg += '<BR><font color = black>Your breasts are firm and full, and starting to hurt from all the milk they contain.</font><BR>'
	elseif lactatemv > lactatemm*3/4:
		$stat_msg += '<BR><font color = black>Your breasts feel firm and heavy, full of milk.</font><BR>'
	elseif lactatemv > lactatemm/2:
		$stat_msg += '<BR><font color = black>Your breasts are heavy, a cargo of milk inside.</font><BR>'
	elseif lactatemv > lactatemm/4:
		$stat_msg += '<BR><font color = black>Your breasts feel a little heavy.</font><BR>'
	elseif lactatemv > 0:
		$stat_msg += '<BR><font color = black>Your breasts tingle a little from the feeling of milk production.</font><BR>'
	else
		$stat_msg += '<BR><font color = black>Your breasts feel soft and empty.</font><BR>'
	end
end

if Enable_Hymenrestore = 1: pcs_vag = 0
if Enable_tightrestore = 1: pcs_ass = 0 & if pcs_vag > 0:pcs_vag = 1
if Enable_clearwrite > 0: body_write = 0 & face_write = 0
if Enable_clearcum > 0:
	killvar 'sparrvol[0]'
end
if Enable_fbreath > 0:
	cumspclnt = 2
	gs 'cum_cleanup'
	pcs_breath=1
end


$clear_write = {
	body_write = 0
	face_write = 0
	minut += 10
	gs '$menu_obnovit'
	'It takes you 10 minutes to thoroughly wash the obscene "painting" from your body.'
}

if body_write > 0 or face_write > 0:
	if body_write > 1 and face_write = 0:
		$stat_msg += '<BR><a href="exec:dynamic $clear_write"><b><font color = red>Your body is inscribed with derogatory graffiti.</font></b></a>'
	elseif body_write = 1 and face_write = 0:
		$stat_msg += '<BR><a href="exec:dynamic $clear_write"><b><font color = red>Your body is an obscene sign.</font></b></a>'
	elseif body_write = 0 and face_write > 1:
		$stat_msg += '<BR><a href="exec:dynamic $clear_write"><b><font color = red>Your face is covered with writing obscene epithets.</font></b></a>'
	elseif body_write = 0 and face_write = 1:
		$stat_msg += '<BR><a href="exec:dynamic $clear_write"><b><font color = red>On your face humiliating label.</font></b></a>'
	else
		$stat_msg += '<BR><a href="exec:dynamic $clear_write"><b><font color = red>Your body and face painted with obscene graffiti.</font></b></a>'
	end
end


killvar 'cumvolume'
killvar 'i'
killvar 'knownguy'
killvar 'cumcount'
killvar 'cumowner'

if arrsize('sparrvol') > 0:
	!!go through every location
	:locationloop
		!!start from the freshest load
		idx = arrsize('sparrloc')-1
		:spermitemloop
			!!if the currently examined load loaction is the location of the current cycle, execute
			if sparrloc[idx] = i:
				!!setting the age of the current load
				!! 0: mouth
				!! 1: in this hour
				!! 2: in four hours
				!! 3: at least five hours ago
				!! 4: in body (pussy or ass)
				!! 5: no more than 12 hours in condom
				!! 6: no more than 48 hours in condom
				!! 7: more than two days old, in condom
				if i = 12:
					j = 0
				elseif i = 17:
					if sparrage[idx] <= 12:
						j = 5
					elseif sparrage[idx] <= 48:
						j = 6
					else
						j = 7
					end
				elseif i = 0 or i = 3:
					j = 4
				else
					if sparrage[idx] <= 1:
						j = 1
					elseif sparrage[idx] <= 5:
						j = 2
					else
						j = 3
					end
				end
				
				cumvolume['<<i>>:<<j>>'] += sparrvol[idx]
				if sparrvol[idx] > 0:
					if sparridt[idx] = 0:
						!!unknown partner
						cumcount['<<i>>:<<j>>'] += 1
					elseif sparridt[idx] = 1:
						!!known partner
						!!storing the name of the cum owner (format: $cumowner[<cum location>:<age of cum>:<index of cum owner>])
						if knownguy['<<i>>:<<j>>'] > 0:
							gchk = 0
							:sameguycheck
							if $cumowner['<<i>>:<<j>>:<<gchk>>'] ! $sparrnam[idx] and gchk <= knownguy['<<i>>:<<j>>']:gchk += 1 & jump 'sameguycheck'
							if gchk > knownguy['<<i>>:<<j>>']:
								$cumowner['<<i>>:<<j>>:<<knownguy[''<<i>>:<<j>>'']>>'] = $sparrnam[idx]
								knownguy['<<i>>:<<j>>'] += 1
								cumcount['<<i>>:<<j>>'] += 1
							end
						else
							$cumowner['<<i>>:<<j>>:<<knownguy[''<<i>>:<<j>>'']>>'] = $sparrnam[idx]
							knownguy['<<i>>:<<j>>'] += 1
							cumcount['<<i>>:<<j>>'] += 1
						end
					end
				end
			end
		if idx > 0:idx -= 1 & jump 'spermitemloop'
	if i < 18:i += 1 & jump 'locationloop'
	killvar 'i'
	killvar 'idx'
	killvar 'j'
end

i = 0
:cumlocloop
	if i = 0: 
		$txtloc = 'your <a href="exec:view''images/pc/body/cum/creampie/cumpussy'+rand(1,14)+'.jpg''">pussy</a>'
	elseif i = 1: 
		$txtloc = 'your <a href="exec:view''images/pc/body/cum/cumpussy/cumpus'+rand(1,4)+'.jpg''">labia</a>'
	elseif i = 2: 
		$txtloc = 'the front of your panties'
	elseif i = 3: 
		$txtloc = 'your <a href="exec:view''images/pc/body/cum/cumanal/cumanal'+rand(1,11)+'.jpg''">ass</a>'
	elseif i = 4: 
		$txtloc = 'your <a href="exec:view''images/pc/body/cum/cumass/cumass'+rand(1,6)+'.jpg''">butt</a>'
	elseif i = 5: 
		$txtloc = 'the back of your panties'
	elseif i = 6: 
		$txtloc = 'your <a href="exec:view''images/pc/body/cum/cumclothes/'+rand(1,3)+'.jpg''">clothes near your groin</a>'
	elseif i = 7: 
		$txtloc = 'your <a href="exec:view''images/pc/body/cum/cumclothes/'+rand(4,18)+'.jpg''">clothes</a>'
	elseif i = 8: 
		$txtloc = 'your back'
	elseif i = 9: 
		$txtloc = 'your legs'
	elseif i = 10: 
		$txtloc = 'your arms'
	elseif i = 11: 
		if pcs_haircol = 0:
			$cum_face_image = 'brown/'
			$cum_face_image += rand(1, 31)
		elseif pcs_haircol = 1:
			$cum_face_image = 'black/'
			$cum_face_image += rand(1, 23)
		elseif pcs_haircol = 2:
			$cum_face_image = 'red/'
			$cum_face_image += rand(1, 19)
		elseif pcs_haircol = 3:
			$cum_face_image = 'blonde/'
			$cum_face_image += rand(1, 25)
		elseif pcs_haircol > 3:
			$cum_face_image = 'custom/'
			$cum_face_image += rand(1, 20)
		end
		$txtloc = 'your <a href="exec:view''images/pc/body/cum/cumface/<<$cum_face_image>>.jpg''">face</a>'
		killvar 'cum_face_image'
	elseif i = 13: 
		$txtloc = 'your hands'
	elseif i = 14: 
		$txtloc = 'your <a href="exec:view''images/pc/body/cum/cumbelly/cumbelly'+rand(1,12)+'.jpg''">stomach</a>'
	elseif i = 15: 
		$txtloc = 'your <a href="exec:view''images/pc/body/cum/cumtits/cumtits'+rand(1,19)+'.jpg''">breasts</a>'
	elseif i = 16: 
		$txtloc = 'your hair</a>'
	elseif i = 17: 
		$txtloc = 'in your vagina in a slipped condom'
	else
		$txtloc = 'the gusset of your panties</a>'
	end
	
	j = 0		
	:ageloop
		if cumvolume['<<i>>:<<j>>'] > 0:
			cumloc[i] = 1
			!!amount setting
			if cumvolume['<<i>>:<<j>>'] > 250:
				!! 25 ml, about four-five load, gushing
				$txtamount = 'Enormous '
			elseif cumvolume['<<i>>:<<j>>'] > 100:
				!! 10 ml, about two load, flowing
				$txtamount = 'Huge '
			elseif cumvolume['<<i>>:<<j>>'] >= 20:
				!! 2 ml, seeping out
				$txtamount = 'Decent '
			else
				!! residue amount, no leakage
				$txtamount = 'Small '
			end
			
			!! if sperm age stage is 4, it means inside the body
			if j = 4:
				if (vibratorIN = 1 and i = 0) or (analplugin = 1 and i = 3):
					$txtproxy = 'being plugged in '
				elseif i = 0 and cumvolume['<<i>>:<<j>>'] < pcs_vag*5:
					$txtproxy = 'staying inside of '
				elseif i = 3 and pcs_ass <= 25:
					$txtproxy = 'staying inside of '
				else
					if cumvolume['<<i>>:<<j>>'] > 250:
						$txtproxy = 'gushing out of '
					elseif cumvolume['<<i>>:<<j>>'] > 100:
						$txtproxy = 'flowing out of '
					elseif cumvolume['<<i>>:<<j>>'] >= 40:
						$txtproxy = 'seeping out of '
					else
						$txtproxy = 'staying inside of '
					end	
				end
			elseif j >= 5:
			!! if sperm age stage is over 4, meaning in condom
				if j = 5:
					$txtproxy = 'buried '
				elseif j = 6:
					$txtproxy = 'marinating '
				else
					$txtproxy = 'rotting '
				end
				if cumcondslip_deep > (vagina / 2): $txtproxy += 'deep '
			elseif j < 4 and j > 0:
				!!amount setting
				if cumvolume['<<i>>:<<j>>'] > 160:
					!! 10 ml, about two load, flowing
					$txtproxy = 'coating '
				elseif cumvolume['<<i>>:<<j>>'] > 40:
					!! 2 ml, seeping out
					$txtproxy = 'covering '
				else
					!! residue amount, no leakage
					$txtproxy = 'spattering '
				end
				if j = 1:
					$txtproxy += 'wetly '
				elseif j = 2:
					$txtproxy += 'dryly '
				else
					$txtproxy += 'powderly '
				end
			end
			
			$txtperson = ''
			if cumcount['<<i>>:<<j>>'] = 0:
				$txtiscum = 'some whitish substance '
			else
				$txtiscum = 'cum'
				if cumcount['<<i>>:<<j>>'] = knownguy['<<i>>:<<j>>']:
				!! everyone is known (one or more)
					if knownguy['<<i>>:<<j>>'] = 1:
						$txtperson = $npc_usedname[$cumowner['<<i>>:<<j>>:0']] + ' '
					else
						kmax = 0
						:nullocator1
						if $cumowner['<<i>>:<<j>>:<<kmax>>'] ! '':kmax += 1 & jump 'nullocator1'
						kmax -= 1
						k = 0
						:knownloop
							$txtperson += $npc_usedname[$cumowner['<<i>>:<<j>>:<<k>>']]
						if k < kmax-1:$txtperson += ', ' & k += 1 & jump 'knownloop'
						k += 1
						$txtperson += ' and ' + $npc_usedname[$cumowner['<<i>>:<<j>>:<<k>>']] + ' '
					end
				elseif knownguy['<<i>>:<<j>>'] = 0:
				!! noone is known (one or more)
					if cumcount['<<i>>:<<j>>'] = 1:
						$txtperson += 'some guy '
					else
						$txtperson += 'some guys '
					end
				else
				!! there are known and unknown guys in the mix
					if knownguy['<<i>>:<<j>>'] = 1:
						$txtperson = $npc_usedname[$cumowner['<<i>>:<<j>>:0']] + ' and some guy'
					else
						kmax = 0
						:nullocator2
						if $cumowner['<<i>>:<<j>>:<<kmax>>'] ! '':kmax += 1 & jump 'nullocator2'
						kmax -= 1
						k = 0
						:mixloop
							$txtperson += $npc_usedname[$cumowner['<<i>>:<<j>>:<<k>>']] + ', '
						if k < kmax: k += 1 & jump 'mixloop'
						$txtperson += 'and some guy'
					end
					if cumcount['<<i>>:<<j>>'] - knownguy['<<i>>:<<j>>'] > 1:
						$txtperson += 's '
					else
						$txtperson += ' '
					end
				end
			end
			if j <= 4 and swallow >= 10 and $txtiscum = 'cum':$txtiscum = '<a href="exec:gs ''cum_manage'', ''cumeater'', <<i>>"><font color="blue">' + $txtiscum + '</font></a>'
			$txtiscum += ' from '
			if i = 0 and j = 4 and vibratorIN = 0 and pcs_vag*10 < cumvolume['<<i>>:<<j>>'] and cumvolume['<<i>>:<<j>>'] >= 20:
				if tanga = 1:
					$stat_msg += '<BR><b><font color="red">' + $txtamount + 'amounts of ' + $txtiscum + $txtperson + 'is ' + $txtproxy + $txtloc + ' into your panties.</font></b>'
				else
					$stat_msg += '<BR><b><font color="red">' + $txtamount + 'amounts of ' + $txtiscum + $txtperson + 'is ' + $txtproxy + $txtloc + ' down your legs.</font></b>'
				end
			elseif sparragestage >= 5:
				if cumcondslip_aware > 0:$stat_msg += '<BR><b><font color="red">' + $txtamount + 'amounts of ' + $txtiscum + $txtperson + 'is ' + $txtproxy + $txtloc + '.</font></b>'
			elseif j ! 0:
				$stat_msg += '<BR><b><font color="red">' + $txtamount + 'amounts of ' + $txtiscum + $txtperson + 'is ' + $txtproxy + $txtloc + '.</font></b>'
			else
				$stat_msg += '<BR><b><font color="red">Your breath smells like sperm.</font></b>'
			end
		end
	if j < 7: j+=1 & jump 'ageloop'
if i < 18: i+=1 & jump 'cumlocloop'
killvar 'i'
killvar 'j'
killvar 'txtperson'
killvar 'k'
killvar 'kmax'
killvar 'txtamount'
killvar 'txtiscum'
killvar 'txtperson'
killvar 'txtproxy'
killvar 'txtloc'
killvar 'cumvolume'
killvar 'cumcount'
killvar 'knownguy'
killvar 'cumowner'


if $clothingworntype = 'nude' and lactatemess > 0:
	if lactatemess < 3:
		$stat_msg += '<BR><font color = black>You have a little milk residue around your nipples.</font>'
	elseif lactatemess < 7:
		$stat_msg += '<BR><font color = black>You have a lot of milk residue around your nipples.</font>'
	else
		$stat_msg += '<BR><font color = black>Your breasts are drenched with milk residue.</font>'
	end
elseif lactatemess > 0:
	if lactatemess < 3:
		$stat_msg += '<BR><font color = black>You have tiny wet spots on your clothes above your nipples.</font>'
	elseif lactatemess < 7:
		$stat_msg += '<BR><font color = black>Milk has soaked through your clothes and made visible circles on your breasts.</font>'
	else
		$stat_msg += '<BR><font color = black>Your shirt is drenched with milk from your breasts.</font>'
	end
end

if Gerpes >= 10:
	$stat_msg += '<BR><b><font color="red">On your pussy sores have appeared. This is genital herpes.</font></b>'
elseif Gerpes >= 5:
	$stat_msg += '<BR><b><font color="red">Your pussy is flushed and highly itchy. This is genital herpes.</font></b>'
elseif Gerpes >= 3:
	$stat_msg += '<BR><b><font color="red">On your lips sores. All signs of genital herpes.</font></b>'
end
if Gerpes >= 20:$stat_msg += '<BR> <b><font color="red">The sores also cover your ass.</font></b>'

if Sifilis >= 50:
	$stat_msg += '<BR><b><font color="red">Syphilitic rashes cover your whole body.</font></b>'
elseif Sifilis >= 21:
	$stat_msg += '<BR><b><font color="red">You have a single big, hard sore on the lip.</font></b>'
end

if Triper > 2:$stat_msg += '<BR><b><font color="red">From your vagina a white discharge comes out strong and stinging when urinating. Also from your vagina there''s a hard and nasty smell.</font></b>'
if Kandidoz > 30:$stat_msg += '<BR><b><font color="red">You have thrush.</font></b>'
if SLomka > 0:$stat_msg += '<BR><b><font color="red">You are very weak and hurt to the bone. You''re in withdrawal.</font></b>'
if narkday ! daystart and narkoman = 1:$stat_msg += '<BR><b><font color="red">You are very weak and hurt to the bone. Urgently need pale lady.</font></b>'
if fingal > 0:$stat_msg += '<BR><b><font color="red">You have a black eye.</font></b>'

! WD: Bimbo look
if bimbolevel > 0: $stat_msg += '<BR><<$bimbostatus>>'

if tanga = 1 and $clothingworntype = 'nude':
	$stat_msg += '<BR><b><font color = red>You are only wearing your panties and your boobs sway invitingly as you walk</font></b>'
elseif tanga= 1 and PCloBimbo = 1:
	$stat_msg += '<BR><b><font color = #FF00FF>You are dressed as a bimbo</font></b>'
elseif tanga = 0:
	if CloStyle = 4:
		$stat_msg += '<BR><b><font color="red">you are not wearing panties, but you are dressed as a hooker, so that''s hardly surprising.</font></b>'
	elseif PCloBimbo = 1 or bimbolevel > 0 and PCloSkirt > 3:
		$stat_msg += '<BR><b><font color = #FF00FF>You like, totally forgot to put on any panties. You are such a bimbo.</font></b>'
	elseif PCloSkirt > 3:
		$stat_msg += '<BR><b><font color="red">Your face is flushed with shame, you are not wearing panties and your naked ass flashes from under your short skirt.</font></b>'
	elseif $clothingworntype ! 'nude':
		$stat_msg += '<BR><b><font color="red">You are not wearing panties.</font></b>'
	elseif towel = 1:
		$stat_msg += '<BR><b><font color = red>Your naked body is covered only by a short towel.</font></b>'
	elseif $clothingworntype = 'nude' and pcs_exhibition < 5:
		$stat_msg += '<BR><b><font color = red>You are completely naked, and it makes you feel nervous and ashamed.</font></b>'
	elseif $clothingworntype = 'nude' and pcs_exhibition < 20:
		$stat_msg += '<BR><b><font color = red>You are completely naked, you''re not too comfortable we nudity but it is a little exciting.</font></b>'
	elseif $clothingworntype = 'nude' and pcs_exhibition >= 90:
		$stat_msg += '<BR><b><font color = blue>You are completely naked, which is far better than having to wear clothes.</font></b>'
	elseif $clothingworntype = 'nude':
		$stat_msg += '<BR><b><font color = #FF00FF>You are completely naked. It''s exciting and you feel tingly inside.</font></b>'
	end
end
$stat_msg += '<BR>'

if mosol >= 50:
	$stat_msg += '<BR><b><font color="red">Your pussy hurts and each step causes more pain, it''s crimson and visibly scuffed from walking without underwear.</font></b>'
elseif mosol >= 30:
	$stat_msg += '<BR><b><font color="red">Your pussy is flushed and itchy, it seems to be from rubbing against your clothing.</font></b>'
end

if analplugIN = 1:$stat_msg += '<BR><b><font color="red">You have a butt plug inserted in your ass.</font></b>'
if vibratorin = 1:$stat_msg += '<BR><b><font color="red">You have a vibrator inserted in your vagina.</font></b>'
$stat_msg += '<BR>'
if young_shop_work = 1:
	if hour = 15 and minut <= 5 and week < 6 and inWorkYoungShop = 0:
		'You are late for work at Pussy-Cats.'
	elseif hour = 15 and week < 6 and inWorkYoungShop = 0 and misscheck ! daystart:
		misscheck = daystart
		young_shop_miss += 1
		$stat_msg += '<BR><b><font color="red">You missed work.</font></b>'
	elseif (hour = 14 or hour = 13) and week < 6:
		$stat_msg += '<BR><b><font color="red">By 15:00 you have to be at work in the shop Pussy-Cats.</font></b>'
	end
end

if workrin = 1:
	if week = 2 or week = 4 or week = 6:
		if hour = 7:$stat_msg += '<BR><b><font color="red">At 8 c''clock you start work on the market.</font></b>'
		if hour = 8:$stat_msg += '<BR><b><font color="red">You need to work on the market now.</font></b>'
	end
end

if frost > 0:
	if frost <= 5:
		$stat_msg += '<BR><b><font>You are a little chilly.</font></b>'
	elseif frost < 11:
		$stat_msg += '<BR><b><font color="red">You are cold.</font></b>'
	else
		$stat_msg += '<BR><b><font color="red">You are freezing.</font></b>'
	end
end

if sick > 0:
	if sick = 1:
		$stat_msg += '<BR><b><font color="red">This is not good, a tickle in the throat and a little runny nose.</font></b>'
	elseif sick < 24:
		$stat_msg += '<BR><b><font color="red">You have chills. You nose is running and throat is raw, you cough and sneeze - it looks like you got a cold.</font></b>'
	elseif sick < 48:
		$stat_msg += '<BR><b><font color="red">You have a fever. Your nose is clogged, it hurts to swallow - throat aches. You continually cough and sneeze - it looks like your cold is pretty bad.</font></b>'
	elseif sick < 72:
		$stat_msg += '<BR><b><font color="red">You have a high fever. You have a stuffy nose, sore head and throat, your coughing badly - it looks like you got a strong cold. Maybe it''s the flu.</font></b>'
	else
		$stat_msg += '<BR><b><font color="red">You toss in the heat. You have a stuffy nose, headache and ache to the bone. Your throat is inflamed and very sore. You think you''re going to cough up a lung - it looks like you are seriously ill, or maybe it''s just a sore throat.</font></b>'
	end
end

if hypnoAddict > 0:
	if hypnoWithdrawal = 1:
		if missCum >= timeTresh:
			$stat_msg += '<BR><b><font color="red">You really miss the feeling of sperm in your body, you are strangely craving it. You should talk to your therapist about it.</font></b>'
		else
			$stat_msg += '<BR><b><font color="red">You feel like talking to your therapist. You miss your sessions.</font></b>'
		end
	elseif hypnoWithdrawal = 2:
		if missCum >= timeTresh:
			$stat_msg += '<BR><b><font color="red">You want some sperm inside of you NOW! You have to talk to your therapist, immediately.</font></b>'
		else
			$stat_msg += '<BR><b><font color="red">You need to talk to your therapist. You feel unbalanced without your sessions.</font></b>'
		end
	else
		if missCum >= timeTresh:$stat_msg += '<BR><b><font color="magenta">You miss the feeling of sperm in your body and you feel moody.</font></b>'
	end
end

if $mudnerd ! '':$stat_msg += '<BR><<$mudnerd>>'

if housr = 1:
	$streetev_home = 'your apartment'
elseif tanwork = 1:
	$streetev_home = 'Tanya''s apartment'
elseif student > 0:
	$streetev_home = 'your dorm'
else
	$streetev_home = 'the train station'
end

if hour < meethour and svidanieA = 1:$stat_msg += '<BR><<$boydescA>> will be waiting for you near <<$streetev_home>> at <<meethour>>:00.'
if hour = meethour and svidanieA = 1:$stat_msg += '<BR><b><font color="red"><<$boydescA>> is waiting for you by <<$streetev_home>>.</font></b>'

if meetday < daystart and svidanieA = 1:bfA -= 10 & svidanieA = 0
if StrongNarkota > 20:$stat_msg += '<BR><b><font color="red">You are stoned.</font></b>'

if alko > 0:
	if alko < 3:$stat_msg += '<BR><b><font color="red">You''re a little dizzy.</font></b>'
	if alko >= 3 and alko < 6:$stat_msg += '<BR><b><font color="red">You''re drunk.</font></b>'
	if alko >= 6:$stat_msg += '<BR><b><font color="red">You''re wasted.</font></b>'
end

if StoryLine = 1:
	if day = 31 and month = 12 and SchoolAtestat = 0 and hour >= 5 and hour <= 14 and StoryLine = 1:$stat_msg += '<BR>At 14:00 your school''s New Year''s party will start.'

	if sisboyday + 1 = daystart and hour < 19 and (npc_QW['A33'] = 3 or npc_QW['A33'] = 5 or npc_QW['A33'] = 7 or (npc_QW['A33'] = 9 and sisboytrioQW ! 1)):$stat_msg += '<BR>' & $stat_msg += '<BR>You promised your sister not to go into your room at 18:00.'
	if sisboyday + 1 = daystart and hour < 19 and sisboytrioQW = 1:$stat_msg += '<BR>' & $stat_msg += '<BR>You promised your sister to meet her in your room at 18:00.'

	if week = 6 and hour < 9 and detention_set = 1:$stat_msg += '<BR><b>You have a detention this morning and must be at school between 8 and 9.</b>'
end

if model > 0 and model_week = (daystart - week) / 7:
	$stat_msg += '<BR><b>You have worked as a model this week.</b>'
elseif model > 0 and model_week < (daystart - week) / 7:
	$stat_msg += '<BR><b>You can do paid modelling this week.</b>'
end
if pornstack > 0:
	aa = 0
	:pfilmtime
	if aa < 200 and pfilmday[aa] = 0: aa += 1 & jump 'pfilmtime'
end
if pornstack > 0 and pfilmday[0] = 0:
	if pfilmday[1] = 0:
		$stat_msg += '<br><b>You will star in another porno in <<aa>> days.</b>'
	else
		$stat_msg += '<br><b>You have a porno shoot scheduled tomorrow.</b>'
	end
end
if pfilmday[0] > 0:
	if pornnow = 1:
		$stat_msg += '<br><b>You are at a porn shooting.</b>'
	else
		if hour < 11: $stat_msg += '<br><b>You are expected in the porno studio at 10:00.</b>'
		if hour >= 11 and hour < 13: $stat_msg += '<br><b>You should be at the porno studio, shooting!</b>'
		if hour >= 13: $stat_msg += '<br><b>You missed a scheduled porno shoot!</b>'
	end
end
if pornmiss = 1: $stat_msg += '<br><b>You missed a scheduled porno shoot!</b>'
if prodcosttrue > 0: $stat_msg += '<br><b>You owe to the porn studio <<prodcost>>?</b>'
if firstkasting > 0 and pfilmNO < 1 and pornstack < 10: $stat_msg += '<br><b>You can contract to <<10-pornstack>> porno movie.</b>'
if hour < 22 and week >= 2 and week <= 4 and workKafe = 1:$stat_msg += '<BR><b><font color="red">Today you have a shift in cafe starting at 11:00.</font></b>'
if hour < 17 and week < 5 and workSec >= 1 or hour < 17 and week < 5 and PersSecWork = 1:$stat_msg += '<BR><b><font color="red">Today you have a shift as a secretary from 9:00 to 17:00</font></b>'
if hour < 20 and week => 1 and week <= 5 and age < 18 and workFabrika = 1:$stat_msg += '<BR><b><font color = red>Today you have work in the garment factory in Pavlovsk. Shift starts at 16:00.</font></b>'
if hour < 16 and week => 1 and week <= 5 and age >= 18 and workFabrika = 1:$stat_msg += '<BR><b><font color = red>Today you have work in the garment factory in Pavlovsk. Shift starts at 8:00.</font></b>'
if hour < 16 and week > 1 and week <= 5 and workhosp = 1:$stat_msg += '<BR><b><font color="red">You work as a nurse in the clinic. Your shift starts at 8:00.</font></b>'
if hour < 10 and worksalon = 1 and (week = 1 or week = 3 or week = 5):$stat_msg += '<BR><b><font color = red>Today you have work as masseuse in beauty salon. Shift starts at 9:00.</font></b>'
if leonidVisit = 1: $stat_msg += '<br><b>You have to serve Leonid at 20:00 in his Pavlovsk office!</b>'

gs 'brother', 'brotherSexCount'


!!zero checks marriage
proverka1 = 0
proverka2 = 0
proverka3 = 0
proverka4 = 0
proverka5 = 0

if boletus + bilberry > 0:
	if boletus + bilberry >= 10:
		$stat_msg +='<br><b><font color = Olive>You have a full basket.</b></font> '
	else
		$stat_msg +='<br><b>In Your basket</b>:'
	end
	if boletus > 0:$stat_msg +='mushrooms: <b><<boletus>></b> kg.'
	if bilberry > 0:$stat_msg +='berries: <b><<bilberry>></b> kg.'
end

$stat_msg += '<br><a href="exec:savegame ''quicksave.sav'' && pl''Quicksave Done''">Q.Save</a>  <a href="exec:savegame">Save</a>  <a href="exec:opengame">Load</a>  <a href="exec:if input(''Input Anything to confirm Quick Load'') ! null: opengame ''quicksave.sav''">Q.Load</a>'

$stat_android +=  ' <a href="exec:savegame ''quicksave.sav'' && pl''Quicksave Done''">Q.S</a>  <a href="exec:if input(''Input Anything to confirm Quick Load'') ! null: opengame ''quicksave.sav''">Q.L</a>'

if Enable_statfsize > 0: $stat_msg += '<BR></font>' & !disable this, all texts comes from pl will have the same font size, like[time]
if Enable_faceturn = 1:	view FUNC('$face_image')
if Enable_Android = 0:
        pl '<<$weatherImage>>'
	pl '<<$stat_msg>>'
else
	'<<$stat_android>>'
	$stat_msg = 'Android mode, <a href="exec:gs ''$menu_obnovit''">Refresh</a><<$stat_msg>>'
end

--- stat_display ---------------------------------

