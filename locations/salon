# salon
$location_type = 'public_indoors'

if $ARGS[0] = 'start':

	$sexloc = $CURLOC

	if week = 1 and hour >= 9 and hour <= 10 and worksalon = 1:gt 'salon', 'beginwork'
	if week = 3 and hour >= 9 and hour <= 10 and worksalon = 1:gt 'salon', 'beginwork'
	if week = 5 and hour >= 9 and hour <= 10 and worksalon = 1:gt 'salon', 'beginwork'

	cla
	clr
	*clr
	salonrand = rand(0, 9)
	minut += 5
	gs 'stat'
	if night_mode = 1:
		fcolor = rgb(255, 255, 255)
		bcolor = rgb(0, 0, 0)
		lcolor = rgb(106, 90, 205)
	else
		fcolor = rgb(0, 0, 0)
		bcolor = rgb(255, 255, 255)
		lcolor = rgb(106, 90, 205)
	end
	'<center><b><font color="maroon">Beauty salon</font></b></center>'
	'<center><img <<$set_imgh>> src="images/locations/city/citycenter/mall/salon/salon.jpg"></center>'
	'Approach the <a href="exec:gt ''salon'', ''1''">beauty technician.</a>'

	'See the <a href="exec:gt ''salon'', ''resnici''">eyelash specialist.</a>'
	if money >= 1000:
		'<a href="exec:gt ''salon'', ''solyariy''">Solarium - 1,000 <b>₽</b></a>'
	else
		'Solarium - 1,000 <b>₽</b>'
	end
	if money >= 5000:		
		'<a href="exec:gt ''salon'', ''ozdormas''">Wellness massage (accelerates fat loss and rejuvenates the body), 5,000 <b>₽</b></a>'
	else
		'Lengthening eyelashes - 5,000 <b>₽</b>'
		'Wellness massage (accelerates fat loss and rejuvenates the body), 5,000 <b>₽</b>'
	end
	if money >= 5000 and lashair = 0:'<a href="exec:gt ''salon'', ''epilyaciya''">Full body depilation - 5,000 <b>₽</b></a>'
	if money < 5000 or lashair = 1:'Full body depilation - 5,000 <b>₽</b>'
	if money >= 15000:'<a href="exec:gt ''salon'', ''uhodkosha''">Skin Care - 15,000 <b>₽</b></a>'
	if money < 15000:'Skin Care - 15,000 <b>₽</b>'
	if money >= 250000:'<a href="exec:gt ''salon'', ''omoloshenie''">Full body skin rejuvenation - 250,000 <b>₽</b></a>'
	if money < 250000:'Rejuvenation - 250,000 <b>₽</b>'

	if hour < 8 or hour > 20:
		cls
		'As the salon closes you are politely asked to leave and come again.'

		act 'Leave':minut += 5 & gt 'torgcentr'

		exit
	end

	act 'Leave':minut += 5 & gt 'torgcentr'
end

if $ARGS[0] = 'solyariy':
	minut += 60
	pcs_tan += 10
	money -= 1000

	if salonrand > 6 and npc_QW['A192'] > 2:gt 'etoexhib', 'pos1'
	if salonrand = 0 and masha = 0:gt 'salon', '0'

	gs 'stat'
	'<center><img <<$set_imgh>> src="images/locations/city/citycenter/mall/salon/solarium.jpg"></center>'
	'You take off your clothes and get in the tanning booth.'
	act 'Leave':gt 'salon', 'start'
end

if $ARGS[0] = 'resnici':
	'You are met by a young, dolled-up woman who explains the various lash treatments she can perform:'
	''
	'"Well," she says, "I can put on temporary false lashes for you if you''re just going out for the night; you''ll look great, but they won''t last much longer than that."'
	'She motions over to a small display of stacked boxes, "I can also, if you want a more permanent solution, treat your lashes to help stimulate their growth, but you won''t get the kind of dramatic results you get from falsies."'
	'"There''s also lash extensions," she explains, "with those we bond either two, four, or six individual lashes to each of your own for what can be a truly dramatic, always-on look. It''s very glam, but they''re expensive and you will have to keep up maintenance on them by coming in regularly."'
	'"I guess I could also manually remove your eyelash extensions if you ever needed me to," she muses, but seems to hesitate before saying, "but.. I really wouldn''t recommend it. Better to let them grow out naturally."'
	''
	''
	if money >= 5000 and pcs_naturallashes < 2:
		'<a href="exec:gt ''salon'', ''lashTreatment''">Eyelash growth treatment</a> - 5000<b> ₽</b>'
		else
		'Eyelash growth treatment - 5000<b> ₽</b>'
	end
	if money >= 500 and pcs_lashes < 3 and false_lashes <= 0:
		'<a href="exec:lashfalsiesstyle = 1 & gt ''salon'', ''lashFalsies''">Simple false lashes</a> - 500<b> ₽</b>'
		else
		'Simple false lashes - 500<b> ₽</b>'
	end
	if money >= 1000 and pcs_lashes < 4 and false_lashes <= 0:
		'<a href="exec:lashfalsiesstyle = 2 & gt ''salon'', ''lashFalsies''">Dramatic false lashes</a> - 1000<b> ₽</b>'
		else
		'Dramatic false lashes - 1000<b> ₽</b>'
	end
	if money >= 12500 and lashextensionduration <= 0:
		'<a href="exec:lashextensionstyle = 2 & lashextensionnew = 1 & gt ''salon'', ''lashExtension''">2D volume eyelash extensions</a> - 12500<b> ₽</b>'
		else
		'2D volume eyelash extensions - 12500<b> ₽</b>'
	end
	if money >= 20000 and lashextensionduration <= 0:
		'<a href="exec:lashextensionstyle = 4 & lashextensionnew = 1 & gt ''salon'', ''lashExtension''">4D volume eyelash extensions</a> - 20000<b> ₽</b>'
		else
		'4D volume eyelash extensions - 20000<b> ₽</b>'
	end
		if money >= 45000 and lashextensionduration <= 0:
		'<a href="exec:lashextensionstyle = 6 & lashextensionnew = 1 & gt ''salon'', ''lashExtension''">6D volume eyelash extensions</a> - 45000<b> ₽</b>'
		else
		'6D volume eyelash extensions - 45000<b> ₽</b>'
	end
	if money >= 6500 and lashextensionduration >= 1 and lashextensionduration <= 4 and lashextensionstyle = 2:
		'<a href="exec:lashextensionnew = 0 & gt ''salon'', ''lashExtension''">Refill your 2D lash extensions</a> - 6500<b> ₽</b>'
		else
		'Refill your 2D lash extensions - 6500<b> ₽</b>'
	end
	if money >= 10500 and lashextensionduration >= 1 and lashextensionduration <= 4 and lashextensionstyle = 4:
		'<a href="exec:lashextensionnew = 0 & gt ''salon'', ''lashExtension''">Refill your 4D lash extensions</a> - 10500<b> ₽</b>'
		else
		'Refill your 4D lash extensions - 10500<b> ₽</b>'
	end
	if money >= 27500 and lashextensionduration >= 1 and lashextensionduration <= 4 and lashextensionstyle = 6:
		'<a href="exec:lashextensionnew = 0 & gt ''salon'', ''lashExtension''">Refill your 6D lash extensions</a> - 27500<b> ₽</b>'
		else
		'Refill your 6D lash extensions - 27500<b> ₽</b>'
	end
	if money >= 500 and lashextensionduration > 0:
		'<a href="exec:lashextensionnew = -1 & gt ''salon'', ''lashExtension''">Remove your eyelash extensions</a> - 500<b> ₽</b>'
		else
		'Remove your eyelash extensions - 500<b> ₽</b>'
	end
	
	act 'back':gt 'salon','start'
end

if $ARGS[0] = 'lashTreatment':
	minut += 30
	if pcs_lashes < 2:pcs_lashes += 1
	if pcs_naturallashes < 2:pcs_naturallashes += 1
	money -= 5000
	gs 'body_shape', 'basebeautycalc', 1
	
	gs 'stat'
	'The specialist collects your payment and moves you over to a chair with a headrest, which she has you tilt your head back up against.'
	'It scarcely takes any time at all -- she takes a cleansing wipe up to your eyes and dabs at them gently until she''s convinced they''re clean, and very carefully brushes an applicator not unlike a mascara wand up against the base of your lashes.'
	'She gently blots what little excess there is and sends you on your way, letting you know you should see results fairly soon.'
	act 'Leave':gt 'salon', 'start'
end

if $ARGS[0] = 'lashFalsies':
	minut += 20
	'The specialist collects your payment and moves you over to a chair with a headrest, which she has you tilt your head back up against.'
	if lashfalsiesstyle = 1:
		if pcs_lashes < 3:pcs_lashes = 3
		false_lashes = 1
		money -= 500
		'She spends a few minutes selecting and trimming a set of simple, if dramatic false lashes to get them to line up neatly with your own natural set, and with practiced ease she glues them delicately in place, only having to give them one or two gentle, settling nudges.'
	end
	if lashfalsiesstyle = 2:
		if pcs_lashes < 4:pcs_lashes = 4
		false_lashes = 1
		money -= 1000
		'She spends a few minutes selecting and trimming a set of ornate, dramatically-feathered false lashes to get them to line up neatly with your own natural set, and with practiced ease she glues them delicately in place, only having to give them one or two gentle, settling nudges.'
	end
	gs 'body_shape', 'basebeautycalc', 1
	gs 'stat'
	'"There you go! You''re all set! Have fun with wherever you''re going!"'
	act 'Leave':gt 'salon', 'start'
end

if $ARGS[0] = 'lashExtension':
	'The specialist collects your payment and brings you into a back room, asking you to lay down and relax upon a salon-style table.'
	if lashextensionnew = 1:
		if false_lashes >= 1:
			'She gently peels off your old, daily-wear false lashes, and spends a few minute cleaning off the glue that held them there. "You won''t be needing these anymore!"'
			minut += 5
			false_lashes = 0
			pcs_lashes = pcs_naturallashes
		end
		if lashextensionstyle = 2:
			if pcs_lashes < 3:pcs_lashes = 3
			money -= 12500
			'Over the next hour and a half, you have to lay there quietly, keeping your eyes closed while the technician works at very carefully and precisely bonding a pair of dark, longer lashes to every single one of your natural ones.'
			minut += 90
		end
		if lashextensionstyle = 4:
			if pcs_lashes < 4:pcs_lashes = 4
			money -= 20000
			'Over the two hours and change, you have to lay there quietly, keeping your eyes closed while the technician works at very carefully and precisely bonding two pairs of dark, longer lashes to each and every one of your real ones.'
			minut += 140
		end
		if lashextensionstyle = 6:
			if pcs_lashes < 5:pcs_lashes = 5
			money -= 45000
			'It feels like you''re there forever, listening to customers come and go in the salon''s main room while you lay there still, eyes shut. It takes the technician almost four hours to bond the countless lashes to your own, some even studded with tiny, lightweight jewels. You can feel the weight of them upon your own natural set, hidden somewhere beneath.'
			minut += 210
		end
		'"Alright!" she says, expelling a deep breath after her hard work. "Now, just be sure to come in every two weeks or so for your lash fills; if you let most of them fall out, I''m going to charge you for a whole new set, okay?"'
		lashextensionduration = 16	
	end
	if lashextensionnew = 0:
		!! Actually, this should not even be possible to do, but just in case...
		if false_lashes >= 1:
			'She carefully removes the false lashes that you''ve frankly ill-advised glued on over the vastly more expensive set. "Don''t put glue on these!" she admonishes you, "you should know better!" It takes her a while to clean them properly.'
			minut += 15
			false_lashes = 0
			pcs_lashes = pcs_naturallashes
		end
			if lashextensionstyle = 2:
			if pcs_lashes < 3:pcs_lashes = 3
			money -= 6500
			'Over the next hour or so, you lay there to have your lashes tidied up; the technician re-glues any loose ones and applies new ones to the new growth you''ve had over the past few weeks.'
			minut += 60
		end
		if lashextensionstyle = 4:
			if pcs_lashes < 4:pcs_lashes = 4
			money -= 10500
			'While about an hour and a half passes, the technician makes idle small talk with you while maintaining your bonded extensions, applying new ones to newly-grown lashes and re-affixing any that might''ve come loose.'
			minut += 90
		end
		if lashextensionstyle = 6:
			if pcs_lashes < 5:pcs_lashes = 5
			money -= 27500
			'Even the maintenance on your extravagantly ornate lashes takes forever, the lash technician hunched uncomfortably forward while she applies countless new lashes to each of your newly-sprouted ones, and tidies up any loose ones among the fold.'
			minut += 120
		end
		'"Alright!" she says, expelling a deep breath after her lengthy maintenance. "You''re back to being beautiful! See you in another two weeks or so?"'
		lashextensionduration = 16	
	end
	if lashextensionnew = -1:
		if false_lashes >= 1:
			'"Well, I''m afraid I''ll have to get these falsies off first, I''m sorry," she explains while carefully removing the glued-on lash strips you have on over your extensions.'
			minut += 5
		end
		minut += 20
		'Though the technician seems reluctant, she compliantly starts the slightly-painful process of trying to soak off and remove the lash extensions you have left. It stings a little, and you feel like you probably lost some of your natural ones in the bargain as well.'
		if pcs_naturallashes > 0:pcs_naturallashes -= 1
		pcs_lashes = pcs_naturallashes
		money -= 500
		killvar lashextensionnew
		killvar lashextensionduration
		killvar lashextensionstyle
	end
	
	gs 'body_shape', 'basebeautycalc', 1
	gs 'stat'
	'After what seems like forever, you''re finally allowed to open your eyes and take a look.'
	act 'Leave':gt 'salon', 'start'
end

if $ARGS[0] = 'epilyaciya':
	minut += 60
	pcs_leghair = -10
	pcs_pubes = -10
	money -= 5000

	if salonrand = 0 and masha = 0:gt 'salon', '0'

	gs 'stat'
	'<center><img <<$set_imgh>> src="images/locations/city/citycenter/mall/salon/depilation.jpg"></center>'
	'You get a full body hair removal treatment, giving you a smooth and completely hair free body.'
	act 'Leave':gt 'salon', 'start'
end

if $ARGS[0] = 'ozdormas':
	minut += 60
	vidageday += 3
	money -= 5000
	gs 'stat'

	if salonrand = 0:
		cla
		*clr
		picrand = rand(25, 28)
		'<center><img <<$set_imgh>> src="images/locations/city/citycenter/mall/salon/mass.jpg"></center>'
		'During your massage, you moan with pleasure as the guy really knows how to get your muscles to relax, it even makes you a little turned on. You feel his had slide down to your ass and before you can even protest he slips a oiled up finger into your pussy and his thumb in your ass.'

		act 'Indignant':gt 'salon', 'start'

		act 'Enjoy':
			guy += 1
			gt 'sex', 'minet'
		end
	end


	act 'Leave':gt 'salon', 'start'
end

if $ARGS[0] = 'uhodkosha':
	minut += 60
	pcs_skin += 20
	money -= 15000
	gs 'body_shape', 'basebeautycalc', 1
	if salonrand = 0 and masha = 0:gt 'salon', '0'

	gs 'stat'
	'<center><img <<$set_imgh>> src="images/locations/city/citycenter/mall/salon/facialmask.jpg"></center>'
	'You get a beauty mask treatment, afterwards your face feels smooth and your skin tight.'

	act 'Leave':gt 'salon', 'start'
end

if $ARGS[0] = 'omoloshenie':
	minut += 60
	vidageday += 50
	money -= 250000
	gs 'body_shape', 'basebeautycalc', 1
	if salonrand = 0 and masha = 0:gt 'salon', '0'

	gs 'stat'
	'<center><img <<$set_imgh>> src="images/locations/city/citycenter/mall/salon/bodyscrub.jpg"></center>'
	'You get a full body rejuvenation procedure, afterwards your skin feels soft and silky smooth.'

	act 'Leave':gt 'salon', 'start'
end

if $ARGS[0] = '0':
	cla
	*clr
	i = arrsize('$sparrnam')
	stat['cum_count'] += 1
	sparrloc[i] = 7
	$sparrnam[i] = 'Z1'
	sparrppt[i] = 0
	sparridt[i] = 1
	sparrvol[i] = 20
	sparrslc[i] = 0
	sparrcnt[i] = stat['cum_count']
	'<center><img <<$set_imgh>> src="images/locations/city/citycenter/mall/salon/sex/pri1.jpg"></center>'
	'During the procedure, the girl manages to stain your clothes with a clearish white liquid, which looks very similar to sperm.'
	'You start to get very upset, not knowing if the stain will come out and not willing to wear clothes that look like someone has cummed on them.'
	'The manager quickly comes over to see what the problem is. "What is the problem?"'
	'You show him the stain on your clothes. "You girl her spilled that stuff on my clothes look at it, it''s ruined."'
	'He gives the girl a glare for a moment before he gives you a reassuring smile. "Oh no that will come right out with a cleaning, we will do all we can to make this right, we will even pay for the cleaning and I will assure you she will be punished."'

	act 'Agree':money += 5000 & gt 'salon', 'start'

	if pcs_dom > 10:
		act 'Require punish now':
			cla
			orgasm += 1
			pcs_dom += 1
			girl += 1
			'You nod at his offer. "Fine but I want this bitch punished now!'
			'He seems a bit taken back by your attitude. "Very well, how would you like her punished? Perhaps we can take it out of her pay, or she can do this procedure at no wages." He seems to think these are fitting punishments.'
			'You shake your head no, no the bitch made your clothes look like they have cum on them. It''s only fitting you return the favor. "No I want you to fuck her right here as I tell you to."'
			'"I''m sorry, but I can not do that." stammered the guy.'

			act 'Punish the bitch':
				cla
				*clr
				'<center><img <<$set_imgh>> src="images/locations/city/citycenter/mall/salon/sex/pri2.jpg"></center>'
				'"You asked me how I''d like her to be punished, now fuck her right here as I tell you to." you say, <<$boydesc>> looks from you to her and back to you.'
				'Clearly he likes your idea of punishment, the bulge now straining against his pants is clear evidence of that. He seems unsure what to say, so you decided to take matters into your own hands.'
				'You reach over and unzip his pants, reaching inside you grab his now hard <<dick>>cm <<$dick_girth>> dick in your hand and pull it out. As you do you start stroking it, any further protest he might have had vanishes.'
				gs 'arousal', 'hj', 5, 'dom'
				gs 'stat'

				act 'Make the bitch suck it':
					cla
					*clr
					'<center><img <<$set_imgh>> src="images/locations/city/citycenter/mall/salon/sex/pri3.jpg"></center>'
					'When he no longer protest any further, you reach over with your other hand and grab the bitch by her hair. You pull her face over to his cock and you say. "Open your mouth bitch and suck his cock."'
					'With these words, you push her mouth at his cock, she obediently opens her mouth letting his <<dick>>cm <<$dick_girth>> cock in her mouth, she wraps her lips tightly around it.'
					'You pull the girls head by her hair back and forth, occasionally forcing her lips against his balls, gagging her as you make her deep throat him.'
					gs 'arousal', 'voyeur', 5, 'dom'
					gs 'stat'

					act 'Doggystyle':
						cla
						*clr
						'<center><img <<$set_imgh>> src="images/locations/city/citycenter/mall/salon/sex/pri4.jpg"></center>'
						'After several minutes of making her gag on his <<dick>>cm <<$dick_girth>> cock, you want a change in the action. You pull her head away and command her. "Take your clothes off bitch."'
						'She obediently strips naked with out a word, once she is naked you force her to bend over as you look to the man and command him. "Fuck her pussy." He obediently slips his dick in her pussy, both of them let out moans of pleasure.'
						'He beings to get into it and starts pounding her balls deep. You pull down your panties and spread you legs. Taking one of her hands in yours you put it on your pussy, she diligently begins to massage your clitoris.'
						gs 'arousal', 'voyeur', 5, 'dom'
						gs 'arousal', 'vaginal_finger', 5, 'dom'
						gs 'stat'

						act 'Put her on top':
							cla
							*clr
							'<center><img <<$set_imgh>> src="images/locations/city/citycenter/mall/salon/sex/pri5.jpg"></center>'
							'A few minutes later you get off the chair and look at him. "Sit in the chair." Which he does at once, then you pull the girl over and point at him.'
							'"Crawl on top of him and straddle him, ride him." She wastes no time doing as you command, once straddling him she beings to rhythmically ride his cock. As she moans in pleasure you grab her hips and force her all the way down, so he is once again balls deep in her twat.'
							gs 'arousal', 'voyeur', 5, 'dom'
							gs 'stat'

							act 'Lay them on the floor':
								cla
								*clr
								'<center><img <<$set_imgh>> src="images/locations/city/citycenter/mall/salon/sex/pri6.jpg"></center>'
								'Watching this you begin to masturbate, you feel yourself getting turned on as you pussy gets wetter. Unable to be left out anymore, you grab the girl and pull her off, shoving her down on the floor. "Lay on your back."'
								'She does as you tell her and then you look at the man. "Lay on your side next to her and start fucking her again." His grin tells you he is not only enjoying fucking her but loves that you are telling him how to fuck her.'
								'He lays down next to her and immediately slips his <<dick>>cm <<$dick_girth>> dick back in her and starts fucking her again. You pull the rest of your clothes off and kneel down over her face. "Lick my pussy, bitch." Which she eagerly does, her tongue laps and teases your clit.'
								'You can tell she has some experience at eating pussy as it feels great.'
								gs 'arousal', 'masturbate', 5, 'dom'
								gs 'arousal', 'cuni', 5, 'dom'
								gs 'stat'

								act 'Finish':
									cla
									*clr
									telmasha = 1
									masha += 1
									'<center><img <<$set_imgh>> src="images/locations/city/citycenter/mall/salon/sex/pri7.jpg"></center>'
									$orgasm_txt = 'You begin to pant and moan and finally you have a intense orgasm, coating the girls face in your pussy juices. Seeing this the guy grunts and starts pounding harder, obviously getting near to blowing his load as well.'
									$orgasm_or = 'custom'
									gs 'arousal', 'cuni', 5, 'dom'
									gs 'arousal', 'end'
									gs 'stat'
									'Standing up you look at him. "Don''t cum in her, stand up." With a slight frown from being so close he does as you tell him. You grab the girl by the hair and pull her up till she is kneeling in front of the guy.'
									'"Stroke his cock till he cums all over your face." The guy is grinning again as she grabs his cock and begins jerking him off for all she''s worth, while you get dressed. It doesn''t take long before he is shooting his cum all over her pretty face.'
									'You look at the man. "That''s how you punish bitches." Glancing at her you add. "Isn''t that right bitch?" She nods in agreement, though you are not sure if it is because she really does agree or she is just doing what you say.'
									'Either way you find it a turn on, with that you take the offered money and grab one of her cards off the counter reading it. "Masha huh? You really know how to use your tongue, I might just call you, so you can use it again." You say to her and then without another glance at the two of them or waiting for a reply you walk out, leaving Masha kneeling on the floor covered in sperm.'

									act 'Leave':money += 5000 & gt 'salon', 'start'
								end
							end
						end
					end
				end
			end
		end
	end
end

if $ARGS[0] = '1':
	cla
	minut += 5

	if worksalon = 0:
		'You ask the manager if he has a job opening.'

		if masash < 10:
			'After asking the manager about a job he tells you. "You need to complete a massage course at the community center before I can hire you."'

			act 'Move away':gt 'salon', 'start'
		elseif masash >= 10:
			'The manager tells you. "We need a masseuse. You''d work Monday, Wednesday and Friday be here between 9:00 and 10:00, your shift will finish at 19:00. Your wages will be 800<b> ₽</b> per shift."'
            '"You''ll need to file your paperwork with the personnel department."'

			if (work = 0 and worksalon = 0) or (cheatWork = 1 and worksalon = 0):
				act 'Take the job':
					cla
					worksalon = 1
					work = 1
					'You file your paperwork with the personnel department'

					act 'Move away':gt 'salon', 'start'
				end
			end

			act 'Move away':gt 'salon', 'start'
		end
	elseif worksalon = 1:
		act 'Resign':gt 'salon', 'discharge'
		act 'Move away':gt 'salon', 'start'
	end
end

if $ARGS[0] = 'beginwork':
	cla
	*clr
	'<center><b><font color="maroon">Beauty</font></b></center>'
	'<center><img <<$set_imgh>> src="images/locations/city/citycenter/mall/salon/salon.jpg"></center>'
	'You go to your workstation and prepare it as you wait for your first customer.'

	act 'Start work':
		smena += 1
		gt 'worksalon', 'start'
	end

	act 'Resign':gt 'salon', 'discharge'
end

if $ARGS[0] = 'discharge':
	cla
	minut += 10
	worksalon = 0
	work = 0
	pay = smena * 800
	money += pay
	smena = 0
	'You tell your boss you have had enough and that you quit. He looks at you a moment and then says. "Here is your paycheck <<pay>> <b>₽</b> and employment history. Goodbye."'

	act 'Leave':gt 'salon', 'start'
end

--- salon ---------------------------------

