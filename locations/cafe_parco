# cafe_parco

$location_type = 'public_indoors'

if $args[0]= 'start':
	'<center><h4><font color="maroon">Cafe "Del Parco"</font></h4></center>'
	'<img src="'+'images/qwest/alter/caffe_del_parco'+iif(hour > 20 or hour < 7,'_night','')+'.jpg'+'">'
	'The newly opened cafe "Del Parco", with its striking facade, there are lots of tables and a summer terrace. Opening hours between 14.00 to 21.00.'

	if hour >= 14 and hour < 21:
		act 'Enter': gt 'cafe_parco', 'inner'
	end
	act 'Go back to the square': gt 'gskver'
end

if $args[0]= 'inner':
	'<img src="images/qwest/alter/caffe_del_parco_in.jpg">'

	act 'Go back outside': gt 'gskver'
	gs 'family'

	if (sunWeather = 0 or temper < 5) and week mod 2 = 0 and (hour >= 16 and hour < 20) and mey_vika_qw_day ! daystart:
		mey_vika_qw_day = daystart
		minut += rand(10,20)
		'You notice Vicky and Vanya Yanka sitting at a table.'

		if mey_vika_qw < 3:
			!! monochromatic
			'You''re not really close with either of them, you don''t really know how to approach them.'

		elseif mey_vika_qw < 5:
			'While in passing of their table you greet them with a simple "Hi".'

			!! connected relationship and response
			if mey_vika_relation < 60:
				'Vicky and Vanya Yanka only glance in your direction and turn away, having nothing more to say you quickly moved on...'
			else
				'Vicky and Vanya Yanka answer "Hi" back at the same time, you just wanted to greet them so you continued on your way.'
			end

		elseif mey_vika_qw >= 5:
			if mey_vika_relation < 60:
				'You approach the couple and greet them, but they don''t pay any attention to you.'
			else
				'You approach the couple and greet them, right away Vicky starts gossiping, as always, Vanya Yanka, mostly joked about the things you two were talking about. You chatted for a few minutes...'
			end

			!! something is going to happen here
			if mey_vika_relation >= 80:
				if mey_vika_qw = 10:
					cla
					'Vicky and Vanya Yanka invite you to join them.'
					if money < 300: 
						act 'Refuse':
							cla
							'You''re pretty much broke, and you don''t want to be impudent by asking them to pay for you, so you decide to thank them for the invite but that you have a thing elsewhere that you need to attend...'

							act 'Leave': gt 'park'
						end
					end
					act 'Agree':
						mey_vika_qw = 15
						gt 'mey_vika_events', 'cafe_qw'
					end
				elseif mey_vika_qw >= 7: 
					'As you''re getting up from the table, you notice Vicky whispering something to Vanya Yanka. You notice Vanya Yanka''s hand stroking and squeezing Vicky''s butt, as they stare at you with lust in their eyes...'
				end
			end
		end
	else
		'You don''t see anyone familiar here.'
	end
end

--- cafe_parco ---------------------------------

