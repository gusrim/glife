# bank
if $ARGS[0] = 'start':
	cla
	clr
	*clr
	minut += 5
	gs 'stat'
	fcolor = rgb(0, 0, 0)
	bcolor = rgb(255, 255, 255)
	lcolor = rgb(106, 90, 205)
	'<center><b><font color="maroon">Bank</font></b></center>'
	'<center><img src="images/etogame/bank.jpg"></center>'

	if week <= 5 and hour = 16 and minut >= 30 and mey_vika['mey_vika_qw'] >= 32: 
		!r125
		'Looks like <a href="exec:gs''mey_tamara_events'',''tamara''">Tamara Mikhailovna</a> is going home.'
	end	
	
	if bankAccount = 0:
		'Do you want to open your account?'
	else
		if karta >= 0:
			'You have <<karta>> <b>₽</b> in your account.'
			'You have an approved credit of <font color="blue"><<bankDebtLimit>></font>!'
		else
			'You hava a debt of <font color="red"><<-1 * karta>></font> <b>₽</b>.'
			'You have an approved credit of <font color="blue"><<bankDebtLimit + karta>></font>!'
		end
	end

	if BankPapper = 1 and dayBankPapper = daystart:
		act 'Give documents':
			cls
			minut += 15
			BankPapper = 2
			gs 'stat'
			'You went to the office and gave the documents to the manager.'

			act 'Leave':
				minut += 5
				if bankloc = 1:
					gt 'down'
				else
					gt 'pavCommercial'
				end
			end
		end
	end

	if money > 0:
		act 'Deposit money':
			cla
			minut += 5
			kartaIN = input ("How much money you want to deposit into the account?")

			if kartaIN <= 0 or kartaIN > money:
				'Not correct operation.'
			else
				karta += kartaIN
				money -= kartaIN
				
				if bankAccount = 0: 
					bankAccount = 1
					bankDebtLimit = 5000
				end
				
				'<br>You put on the account <<kartaIN>> <b>₽</b>, now you have <<karta>> <b>₽</b> on it.'
			end

			act 'Move away':gt 'bank', 'start'
		end
	end

	if karta + bankDebtLimit > 0:
		act 'Withdraw money':
			cla
			minut += 5
			kartaOUT = input ("How much do you want to withdraw from account?")

			if kartaOUT <= 0 or kartaOUT > karta + bankDebtLimit:
				'Not correct operation.'
			else
				karta -= kartaOUT
				money += kartaOUT
				'<br>You debited <<kartaOUT>> <b>₽</b>, now you have <<karta>> <b>₽</b> on it.'
			end

			act 'Move away':gt 'bank', 'start'
		end
	end

	act 'Leave':
		minut += 5
		if bankloc = 1:
			gt 'down'
		else
			gt 'pavCommercial'
		end
	end
end

--- bank ---------------------------------

