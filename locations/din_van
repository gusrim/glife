# din_van

$showerdin = {
	cumspclnt = 1
	gs 'cum_cleanup'
	if sweat > 10: sweat = 10 + rand(0,4)
}

$bteeth = {
	if fbreath ! 1:
		act 'Brush your teeth (0:05)':
			cla
			*clr
			minut += 5
			cumspclnt = 2
			gs 'cum_cleanup'
			fbreath = 1
			'<center><img src="images/pics/brushteeth.jpg"></center>'
			*nl
			'You brush your teeth and now they are clean and minty fresh.'
			act 'Finish':gt $locM, $metkaM
		end
	end
}

$dryOff = {
	act 'Get out and dry off':
		if indorf = 0 and vanr_lock = 0 and $loc = 'vanrPar':
			gs 'vanrPar', 'peek'
		else
			mirror_steam = hour & gt $locM, $metkaM
		end
	end
}

$shower = {
	act 'Take a shower (0:15)':
		cla
		*clr
		shampoo -= 1
		minut += 15
		hapri = 0
		mop = 1

		if clrbelo > 0 and tanga = 1:
			clrbelo -= 1
			dirtbelo += 1
		elseif clrbelo = 0 and tanga = 1:
			tanga = 0
			dirtbelo += 1
		end

		if shampoo < 0:
			shampoo = 0
		end

		dynamic $showerdin

		if frost > 0:frost = 0

		'<center><img src="images/pics/dush.jpg"></center>'
		*nl
		'You undress and turn on the water in the shower. When it hits the right temperature you enter the shower and let the warm water rain down on you.'
		if daystart < 10 and StoryLine = 0:
			horny += rand(6,10)
			'It''s really arousing to see your new body with all its smooth curves, breasts with large nipples, wide hips, and smooth legs. You begin to lather your body noticing how soft and tender it feels. Once you''re sure you didn''t miss a single spot on your feminine body you begin to wash yourself off, continuing to experience new and strange sensations.'
		elseif daystart < 30 and StoryLine = 0:
			horny += rand(2,6)
			'You have already gotten used to your feminine body and its softness doesn''t surprise you very much anymore. But you still let your hands wander a bit over your soft curves before you begin to wash yourself in earnest.'
		else
			horny += rand(0,2)
			'You carefully soap yourself trying not to miss any spot and then wash off.'
		end
		if hypnoAddict > 0:'You have no reason to, but you carefully avoid cleaning out your pussy and ass.'

		if btwarn > 0:
			if btwarn = 1: $weightwarn = 'losing'
			if btwarn = 2: $weightwarn = 'gaining'
			'<font color="red"><b>You seem to be <<$weightwarn>> weight.</b></font>'
			killvar '$weightwarn' & btwarn = 0
		end
		if normbuffpick = 1:
			*nl
			'<b>You notice that your muscles are starting to show through your skin.</b>'
			act 'Examine yourself closer while drying off and getting dressed...': gt 'din_van', 'buffpick'
		end

		if vanr_lock = 0 and $loc = 'vanrPar':
			*nl
			'In your absentmindedness you forgot to close the door.'
			'The idea that the door is open occupies your mind while you are showering. It would be very embarrassing if your brother or stepfather looked in at the wrong time.'
		end

		if (leghair > 0 or lobok > 0) and stanok > 0:
			act 'Shave':dynamic $brit
		end

		if horny >= 40:
			act 'Use the shower head on your pussy':
				cla
				*clr
				minut += 30
				'<center><img src="images/pics/wash2.jpg"></center>'
				*nl
				'You place the shower head onto your pussy and a powerful jet of warm water begins to caress you.'

				if horny >= 90:
					horny = 0
					stat['mast'] += 1
					orgasm += 1
					manna += 15
					willpower += 15
					'Soon you feel a warm wave spreading over your entire body and you shudder as an orgasm overtakes you.'
				else:
					horny += 5
					manna += 10
					willpower += 10
					'At first it feels very nice, but after a while it starts to be a little bit painful. You decide to stop masturbating.'
				end
				if hypnoAddict ! 0:
					'You unconsciously avoid spraying water into your vagina.'
				else
					cumspclnt = 8
					gs 'cum_cleanup'
				end

				dynamic $dryOff
			end
		end	

		dynamic $dryOff
	end
}

$bath = {
	act 'Soak in the bath (0:30)':
		cla
		*clr
		shampoo -= 1
		minut += 30
		sweat = 7 + rand(0,4)
		hapri = 0
		mop = 1
		manna += 20

		if clrbelo > 0 and tanga = 1:
			clrbelo -= 1
			dirtbelo += 1
		elseif clrbelo = 0 and tanga = 1:
			tanga = 0
			dirtbelo += 1
		end

		if shampoo < 0:
			shampoo = 0
		end

		dynamic $showerdin

		if frost > 0:frost = 0

		skinvan += 1
		gs 'stat'
		'<center><img src="images/pics/wash4.jpg"></center>'
		*nl
		'While the bathtub fills up you get undressed and then dip a toe into the water. It has just the right temperature so you climb in and let yourself sink into the warm water.'

		if daystart < 10 and StoryLine = 0:
			horny += rand(6,10)
			'You stare at those smooth, soft legs sticking out of the water. You''re still not used to this new body and the look of all this roundness.'
			'Starting to soap yourself you unwittingly turn your attention to your breasts. They''re so soft and big, and when you touch them you feel an unfamiliar but pleasant sensation. You take your time getting familiar with your new body, then you begin to wash yourself in earnest.'
		elseif daystart < 30 and StoryLine = 0:
			horny += rand(2,6)
			'You have become a little accustomed to your female body and casually soap up all these soft curves and rinse yourself off, rubbing your hands all over your feminine body to make sure you didn''t miss a spot.'
		else
			horny += rand(0,2)
			'You lean back and relax for a while. Then you begin to lather your body, gently gliding your soft hands over your smooth curves, and wash yourself thoroughly.'
		end

		if $loc = 'vanrPar':
			dynamic $brother_voyeurism
		end

		if btwarn > 0:
			if btwarn = 1: $weightwarn = 'losing'
			if btwarn = 2: $weightwarn = 'gaining'
			'<font color="red"><b>You seem to be <<$weightwarn>> weight.</b></font>'
			killvar '$weightwarn' & btwarn = 0
		end
		if normbuffpick = 1:
			''
			'<b>You notice that your muscles are starting to show through your skin.</b>'
			act 'Examine yourself closer while drying off and getting dressed...': gt 'din_van', 'buffpick'
		end

		if (leghair > 0 or lobok > 0) and  stanok > 0:
			act 'Get your razor':dynamic $brit
		end

		if obdysh = 1:vannmastrand = rand(0, 5)

		if horny >= 40:
			act '"Wash" your pussy':
				cla
				*clr
				if obdysh = 1 and vannmastrand = 1:gt 'vann', 'v4'
				minut += 30

				'<center><img src="images/pics/wash7.jpg"></center>'
				*nl
				'You turn on the faucet, lift your legs and scoot your ass over to align your pussy with the jet of warm water.'

				if horny >= 90:
					horny = 0
					stat['mast'] += 1
					orgasm += 1
					manna += 15
					willpower += 15
					'Soon you feel like warm waves wash trough your entire body and you begin to shudder as you reach your peak.'
				else:
					horny += 5
					manna += 10
					willpower += 10
					'It feels really nice at first, but after a while it gets a bit uncomfortable and your legs fall asleep. You better stop before an awkward accident happens.'
				end

				dynamic $dryOff
			end
		end

		if horny >= 50:
			act 'Masturbate':gt 'selfplay'
		end

		dynamic $dryOff
	end
}

$condslip = {
	if cumcondslip >= 1:
		act 'Try to pull out the condom slipped inside (0:30)':
			cla
			minut += 30

			if cumcondslipdeep > 0:
				'The condom is slipped too deep inside to be pulled out by your hand. You will need to ask for professional help!'

			else
				cumspclnt = 18
				gs 'cum_cleanup'
				cumcondslip = 0
				gs 'stat'
				'With some effort, you managed to remove the condom.'
			end
			act 'Leave':gt $curloc

		end
	end
}

$brit = {
	cla
	'You rinse the razor blade which looks in good enough shape for <b><<stanok>></b> more shaves.'

	if leghair > 0 and stanok > 0:
		act 'Shave your legs (0:15)':
			cla
			*clr
			minut += 15
			leghair = -5
			horny += 5
			stanok -= 1
			'<center><img src="images/pics/shave.jpg"></center>'
			*nl
			if daystart < 10 and StoryLine = 0:
				'You can''t shake off the fact that you''re a girl now, so you decide it''d be best to shave your legs. You begin to lather your legs, still marveling at how soft and round they are. Then you take the razor in your hand and begin to shave your legs.'
				'You feel really awkward, never having done this before as a guy. Luckily, you had more than enough experience with shaving your face and technically this isn''t really all that different. So in the end you get a pretty decent job done.'
			elseif daystart < 30 and StoryLine = 0:
				manna += 10
				'With a newfound routine you lather up your legs and start shaving, careful not to miss a spot. After washing away the foam you softly glide your hands over your smooth legs, feeling good to know that you don''t need to hide your legs in pants. Maybe you should wear a skirt tomorrow?'
			else:
				'You lather up your legs and then start carefully shaving them. Once you''re finished you gently glide your hands over your smooth legs, making sure you didn''t miss a spot.'
			end

			act 'Put the razor away':dynamic $brit
		end
	end

	if lobok > 0 and stanok > 0:
		act 'Shave your pussy (0:15)':
			cla
			*clr
			minut += 15
			lobok = -5
			horny += 5
			stanok -= 1

			if obdysh = 1 and obbritinvent = 0:
				britrand = rand(0, 5)
				if britrand = 5:gt 'vann', 'brit1'
			end

			'<center><img src="images/pics/shave3.jpg"></center>'
			*nl
			'You lather your crotch with some soapy water and carefully shave your pubic hair. After shaving you can''t help but run your fingers over your pussy, enjoying the smooth feeling and getting slightly more aroused by the touch.'

			act 'Put the razor away':dynamic $brit
		end
	end

	dynamic $dryOff
}

$tampon = {
	if sanpad > 0 and mesec > 0 and isprokp = 0 and isprok = 0 and tanga = 1:
		act 'Use a sanitary pad (0:05)':
			cla
			*clr
			sanpad -= 1
			isprokp = 1
			minut += 5
			manna -= 5
			'<center><img src="images/pics/wash3.jpg"></center>'
			*nl
			'You use a sanitary pad.'
			act 'Finish':gt $locM, $metkaM
		end
	end
	if tampon > 0 and mesec > 0 and isprok = 0 and isprokp = 0:
		act 'Use a tampon (0:05)':
			cla
			*clr
			tampon -= 1
			isprok = 1
			minut += 5
			manna -= 5
			'<center><img src="images/pics/wash3.jpg"></center>'
			*nl
			'You use a tampon.'
			act 'Finish':gt $locM, $metkaM
		end
	end
}


$quickwash = {
	if spdirty > 0:
		act 'Quick wash (0:15)':
			cla
			*clr

			if clrbelo > 0 and tanga = 1:
				clrbelo -= 1
				dirtbelo += 1
			elseif clrbelo = 0 and tanga = 1:
				tanga = 0
				dirtbelo += 1
			end
			
			cumspclnt = 4
			gs 'cum_cleanup'
			if sweat < 30 : sweat = 15 + rand(0,4) else sweat -= 10 + rand(0,4)
			minut += 15
			'<center><img src="images/pics/wash5.jpg"></center>'
			*nl
			'You give yourself a catlick, thus removing any traces of cum.'

			act 'Finish':gt $locM, $metkaM
		end
	end
}

$wearpan = {

if tanga = 0 and clrbelo > 0:
	act 'Wear Underwear':
		cla
		tanga = 1
		clrbelo -= 1
		gt $locM, $metkaM
	end
end
}

$basin = {

	if tanga = 1:
		act 'Remove Underwear':
			cla
			tanga = 0
			dirtbelo += 1
			gt $locM, $metkaM
		end
	end

	if spdirtyv > 0 or spdirtyb > 0:
		act 'Clean your face from sperm (0:10)':
			cla
			*clr
			mop = 1
			cumspclnt = 9
			gs 'cum_cleanup'
			cumspclnt = 13
			gs 'cum_cleanup'
			if sweat > 19 : sweat -= 5
			minut += 10
			'<center><img src="images/etogame/facesp.jpg"></center>'
			*nl
			'You clean your face and get rid of any trace of cum.'

			act 'Finish':gt $locM, $metkaM
		end
	end

	if body_write > 0 or face_write > 0:
		act 'Wash the writings off your body (0:10)':
			cla
			*clr
			body_write = 0
			face_write = 0
			minut += 10
			gs'stat'

			'<center><img src="images/pics/wash1.jpg"></center>'
			*nl
			'You wash off the indecent inscriptions on your body.'

			act 'Finish':gt $locM, $metkaM
		end
	end

	if mop ! 1:
		act 'Wash off cosmetics (0:10)':
			cla
			mop = 1
			cumspclnt = 9
			gs 'cum_cleanup'
			cumspclnt = 11
			gs 'cum_cleanup'
			minut += 10
			*nl
			'You wash off your makeup.'

			act 'Finish':gt $locM, $metkaM
		end
	end
}

$pirManage = {
	cls
	cla
    *nl
    if pirsA > 0:
        if pirsA = 1:
            '<a href="exec:pirsA = 2 & dynamic $pirManage">Remove tongue barbell</a>.'
		else
            '<a href="exec:pirsA = 1 & dynamic $pirManage">Insert tongue barbell</a>.'
        end
    end
    if pirsB > 0:
        if pirsB = 1:
            '<a href="exec:pirsB = 2 & dynamic $pirManage">Remove lip ring</a>.'
		else
            '<a href="exec:pirsB = 1 & dynamic $pirManage">Insert lip ring</a>.'
        end
    end
    if pirsC > 0:
        if pirsC = 1:
            '<a href="exec:pirsC = 2 & dynamic $pirManage">Remove ear piercings</a>.'
		else
            '<a href="exec:pirsC = 1 & dynamic $pirManage">Insert ear piercings</a>.'
        end
    end
    if pirsD > 0:
        if pirsD = 1:
            '<a href="exec:pirsD = 2 & dynamic $pirManage">Remove nose ring</a>.'
		else
            '<a href="exec:pirsD = 1 & dynamic $pirManage">Insert nose ring</a>.'
        end
    end
    if pirsE > 0:
        if pirsE = 1:
            '<a href="exec:pirsE = 2 & dynamic $pirManage">Remove eyebrow stud</a>.'
		else
            '<a href="exec:pirsE = 1 & dynamic $pirManage">Insert eyebrow stud</a>.'
        end
    end
    if pirsF > 0:
        if pirsF = 1:
            '<a href="exec:pirsF = 2 & dynamic $pirManage">Remove nipple piercings</a>.'
		else
            '<a href="exec:pirsF = 1 & dynamic $pirManage">Insert nipple piercings</a>.'
        end
    end
    if pirsN > 0:
        if pirsN = 1:
            '<a href="exec:pirsN = 2 & dynamic $pirManage">Remove navel piercing</a>.'
		else
            '<a href="exec:pirsN = 1 & dynamic $pirManage">Insert navel piercing</a>.'
        end
    end
    if pirsG > 0:
        if pirsG = 1:
            '<a href="exec:pirsG = 2 & dynamic $pirManage">Remove labia piercings</a>.'
		else
            '<a href="exec:pirsG = 1 & dynamic $pirManage">Insert labia piercings</a>.'
        end
    end
    if pirsGL > 0:
        if pirsGL = 1:
            '<a href="exec:pirsGL = 2 & dynamic $pirManage">Remove clit piercing</a>.'
		else
            '<a href="exec:pirsGL = 1 & dynamic $pirManage">Insert clit piercing</a>.'
        end
    end
    
    act'<b>Back</b>':gt $locM, $metkaM
}

if $ARGS[0] = 'private':
    if stanok > 0:'Your razor will last for <b><<stanok>></b> more shavings.'
    !WD: heal chafe
    if mosolmaz>0:'On a shelf is a tube of ointment for chafing, which will be enough for <b><<mosolmaz>></b> applications.'
    if tampon = 0:
		'<b><font color="red">You have no tampons.</font></b>'
	elseif tampon = 1:
		'<font color="red">You have only <b><<tampon>></b> tampon left.</font>'
	else
		'You have <b><<tampon>></b> tampons.'
	end

    if dirtbelo > 0 and poroshok > 0 and $loc ! 'vanrPar':
        act 'Wash clothes':
            cla
            *clr
            minut += dirtbelo * 5
            clrbelo += dirtbelo
            poroshok -= 1
            dirtbelo = 0
            manna = manna - 10
            '<center><img src="images/etogame/stir.jpg"></center>'
            *nl
            'You soak the clothes in the basin and pour in washing powder, then you wash your clothes until they are finally clean.'

            act 'Finish':gt $locM, $metkaM
        end
    end

    if shampoo > 0 or $loc = 'vanrPar':
            if $loc = 'vanrPar':
                if shampoo < 1:shampoo = 1
                'You are using your parents'' shampoo.'
            else
                'You have enough shampoo for <b><<shampoo>></b> uses.'
            end
		dynamic $shower
		dynamic $bath
    elseif shampoo = 0 and $loc = 'tanvanr':
    	shampoo = 1
		'You can use Tanyas shampoo.'
    else
    	'<font color="red">You''ve run out of shampoo and will have to buy some more before you can wash yourself.</font>'
    end

	if painkiller > 0:
		$pluralS = 's'
		if painkiller = 1:$pluralS = ''
		'You have <<painkiller>> painkiller<<$pluralS>>.'
		if pain['total'] > 0 and pain['killer'] = 0:
			act 'Take a painkiller':
				cla
				pain['killer'] += 1
				painkiller -= 1
				water += 20
				gs 'stat'

				*nl
				'You take a painkiller and gulp it down with a glass of water.'

				act 'Finish':gt $locM, $metkaM
			end
		end
	end

    if pirsA + pirsB + pirsC + pirsD + pirsE + pirsF + pirsN + pirsG + pirsGL > 0:'<a href="exec:dynamic $pirManage">Manage Piercings</a>'

    if fillimplant = 1 and siliconeBag >= 1:'<a href="exec:cls & siliconeBag -= 1 & silicone += 10 & gt $locM, $metkaM">Inject silicone into breast implants</a>'
    if fillimplant = 1 and silicone >= 20:'<a href="exec:cls & silicone -= 10 & gt $locM, $metkaM">Drain silicone from breast implants</a>'

    if breastpump > 0:'<a href="exec:dynamic $milk_me">Use breast pump</a></center>'

    dynamic $bteeth

    dynamic $tampon

    dynamic $condslip

    if clrbelo <= 4 and $loc = 'vanrPar': clrbelo = 5

    dynamic $wearpan
    dynamic $basin

    if dirtbelo > 0 and BuyHous = 0 and $loc = 'vanrPar':
    	*nl
        'In the bathroom you find the laundry Mom washed for you.'
        clrbelo += dirtbelo
        dirtbelo = 0
    end

    if spdirtyc > 0:
        act 'Wipe the cum off your clothes (0:10)':
            cla
            *clr
            cumspclnt = 6
            gs 'cum_cleanup'
            minut += 10
            gs 'stat'
            '<center><img src="images/etogame/frotsp.jpg"></center>'
            *nl
            'You remove the traces of cum from your clothes.'

            act 'Finish':gt $locM, $metkaM
        end
    end

    if leghair > 0 and stanok > 0:
        act 'Shave your legs (0:15)':
            cla
            *clr
            minut += 15
            leghair = -5
            horny += 5
            stanok -= 1
            '<center><img src="images/pics/shave.jpg"></center>'
            *nl
            'You lather your legs up with some soapy water and shave your legs. After a few swift movements, your legs are smooth as silk. The feeling of your smooth legs turns you on a little.'

            act 'Return to showering':gt $locM, $metkaM
        end
    end

    if lobok > 0 and stanok > 0:
        act 'Shave your pussy (0:15)':
            cla
            *clr
            minut += 15
            lobok = -5
            horny += 5
            stanok -= 1
            '<center><img src="images/pics/shave3.jpg"></center>'
            *nl
            'You lather your crotch with some soapy water and carefully shave your pubic hair. After shaving you can''t help but run your fingers over your pussy, enjoying the smooth feeling and getting slightly more aroused by the touch.'

            act 'Return to showering':gt $locM, $metkaM
        end
    end

    if analplug = 1 and analplugin = 1:
        act 'Remove your butt plug (0:05)':
            cla
            *clr
            minut += 5
            analplugIN = 0

            if anus < 30:anus += 1

            '<center><img src="images/body/analplug.jpg"></center>'
			*nl
            'You squat and tug gently at the butt plug, gasping as you push it out of you. You can''t help but feel a bit empty, now that it''s out.' & !!0.2.6: Removed unneccessary var check

            act 'Finish':gt $locM, $metkaM
        end
    elseif analplug = 1 and analplugin = 0 and klismaday = daystart:
        act 'Insert your butt plug (0:05)':
            cla
            *clr
            minut += 5
            analplugIN = 1

            if anus < 10:anus += 5
            if anus >= 10 and anus < 20:anus += rand(1, 2)
            if anus >= 20 and anus < 30:anus += rand(0, 1)

            '<center><img src="images/body/analplug.jpg"></center>'
			*nl
            if butpluguse > 0:'You squat and gently rub the butt plug against your anus, after lubricating it with a bit of saliva. After a short while, you''ve relaxed enough to be able to push it in. Once it''s in you give it a few gentle tugs, wondering if anyone will catch you wearing it today.'
            if butpluguse = 0:butpluguse = 1 & '"If I''m not going to use it, then why did I buy it?" you ask yourself. You look at the butt plug for a while, thinking "It is necessary to start developing my ass, so I could have great adventures someday... you could even say huge and thick adventures with crimson heads, uh, now I got carried away..." You''re slightly embarrassed at the thought for a second, but are then overcome by your horny fantasies. Determined to get the plug all the way in, you slowly press it against your asshole after lubricating it with a bit of saliva. It hurts a bit at first, but once the thickest part is in the rest follows rather easily. Patting yourself on your ass once it''s in, you wonder if anyone will notice you''re wearing it today and then immediately feel naughty for even having thought that.'

            act 'Finish':gt $locM, $metkaM
        end
    end

    if klisma = 1 and (klismaday ! daystart or arrpos('sparrloc', 3) ! -1):
        act 'Give yourself an enema (0:05)':
            cla
            *clr
            cumspclnt = 7
            gs 'cum_cleanup'
            minut += 5
            klismaday = daystart
            klismaday1 = 1

            '<center><img src="images/picv/klisma.jpg"></center>'
            *nl
            'You give yourself an enema, feeling clean down there. Your ass is now ready for anal sex without any awkward surprises.'

            act 'Finish':gt $locM, $metkaM
        end
    end

    if vibrator = 1 and vibratorIN = 1:
        act 'Remove the vibrator (0:05)':
            cla
            *clr
            minut += 5
            vibratorIN = 0

            if vagina < 30:vagina += 1

            '<center><img src="images/etogame/vibrator.jpg"></center>'
            *nl
            'You spread your legs and take the vibrator out of your pussy, letting out a slight moan as you do so. You feel somewhat empty now.'

            act 'Finish':gt $locM, $metkaM
        end
    elseif vibrator = 1 and vibratorIN = 0 and vagina ! 0:
        act 'Insert the vibrator in your vagina (0:05)':
            cla
            *clr
            minut += 5
            vibratorIN = 1

            if vagina < 30:vagina += 2

            '<center><img src="images/etogame/vibrator.jpg"></center>'
            *nl
            'You spread your legs and slowly insert the vibrator in your pussy, wondering if anyone will notice you wearing it today.'

            act 'Finish':gt $locM, $metkaM
        end
    end

    if mosolmaz > 0 and mosol > 0:
        act 'Rub ointment onto your pussy (0:05)':
            cla
            minut += 5
            mosolmaz -= 1
            mosol -= 5
            'You take a tube of cream and rub it onto your itchy vulva.'

            act 'Finish':gt $locM, $metkaM
        end
    end

    if pregtest > 0:
        act 'Do a pregnancy test (0:05)':
            cla
            minut += 5
            pregtest -= 1
            gs 'stat'

            if pregchem > 120:
                knowpreg = 1
				thinkpreg = 1
                'The test showed two strips. You are pregnant.'
            else
				knowpreg = 0
                'The test showed one band. Not pregnant.'
            end

            act 'Finish':gt $locM, $metkaM
        end
    end
end

if $ARGS[0] = 'misery':

    $stirkad = {
        cla
        *clr
        minut += dirtbelo * 5
        suhbelo += dirtbelo
        poroshok -= 1
        dirtbelo = 0
        manna -= 10

        '<center><img src="images/etogame/stir.jpg"></center>'
        *nl
        'You soak your dirty clothes in a basin and then pour washing powder on them. You wash them until they are finally clean.'

        if $curloc = 'vanr':
            act 'Hang the laundry out to dry':gt 'korr'
        elseif $curloc = 'vanr2x':
            act 'Hang the laundry out to dry':gt'korr2x'
        end
    }

    if stiralka > 0:
        if husband > 0 and husbanday > 0 and husporday ! daystart and poroshok >= 2:
            husband += 5
            poroshok -= 2
            husporday = daystart
            'The washing machine washed your husband''s dirty laundry.'
        end

        if dirtbelo > 0 and poroshok >= dirtbelo:
            clrbelo += dirtbelo
            poroshok -= dirtbelo
            dirtbelo = 0
            'The washing machine washed your laundry.'
        elseif poroshok < dirtbelo:
            'You ran out of washing powder for the washing machine.'
        end
    end

    if dirtbelo>0:'The laundry basket currently has <font color="red"><b><<dirtbelo>></b></font> pieces of dirty laundry.'
    if poroshok > 0:
        if dirtbelo = 0:$stirka = ''
        if dirtbelo > 0:$stirka = '<a href="exec:dynamic $stirkad">You can wash the laundry.</a>'
        'Under the sink there''s detergent powder, which is enough for <b><<poroshok>></b> washes. <<$stirka>>'
    else
        '<b><font color="red">You have no detergent powder and nothing to wash clothes with.</font></b>'
    end

    if clrbelo > 0:
        'A cupboard contains <font color="blue"><b><<clrbelo>></b></font> pairs of clean underwear. '
    else
        '<b><font color="red">You do not have any clean underwear.</font></b> '
    end

    if husband > 0 and husbanday > 0 and husporday ! daystart and poroshok >= 2:
        act 'Wash your husbands clothes (2 units of washing powder) (1:00)':
            cla
            *clr
            husband += 5
            poroshok -= 2
            minut += 60
            husporday = daystart
            manna -= 25
            '<center><img src="images/etogame/stir.jpg"></center>'
            *nl
            'You spend an hour washing your husband''s dirty laundry.'

            act 'Leave the bathroom':gt $curloc
        end
    end

    if husband > 0 and husbanday > 0 and huspolday ! daystart:
        act 'Clean the apartment (1:00)':
            cla
            *clr
            husband += 5
            minut += 60
            huspolday = daystart
            manna -= 25
            !!'<center><img src="images/pics/cook.jpg"></center>'
            *nl
            'You take a rag and start cleaning the apartment.'

            act 'Finish':gt $curloc
        end
    end
end

if $ARGS[0] = 'buffpick':
	'<center><img src="images/body/fit2.jpg"></center>'
	*nl
	'While drying off and getting dressed you examine the sleek muscles that are showing through your skin.'
	'As you do so you think about how you look and decide:'
	act 'You like how you look.': normbuffpick = 2 & mirror_steam = hour & gt $locM, $metkaM
	act 'You don''t like how you look.': nrmbfpckct += 1 & normbuffpick = 0 & mirror_steam = hour & gt $locM, $metkaM
end

--- din_van ---------------------------------

