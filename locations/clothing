# clothing




$clothing_list_line = {
	!! a single line in a clothing list (wardrobe, etc)
	!! ARGS 0 - action type (wardrobe, sell, resize, store, forsale)
	!! ARGS 1 - clothing type
	!! ARGS 2 - clothing index

	if night_mode = 1:
		$bgcolor = iif($bgcolor='#0C0B11','#000000','#0C0B11')
	else
		$bgcolor = iif($bgcolor='#f3f4ee','#ffffff','#f3f4ee')
	end

	$RESULT = '<TR bgcolor='+$bgcolor+'>'

	if Enable_clothwidth > 0:
		clothing_temp = Enable_clothwidth - 1
	else
		clothing_temp = 75 & !clothing widdefault to 75
	end
	$RESULT +='<TD><a href="exec:gt ''clothing'', ''view_clothing_item'', ''<<$ARGS[0]>>'', ''<<$ARGS[1]>>'', <<ARGS[2]>>"><img src="<<FUNC(''$clothing_image'', $ARGS[1], ARGS[2])>>" width="<<clothing_temp>>"></a></TD>'
	killvar 'clothing_temp'
	gs 'clothing_attributes', $ARGS[1], ARGS[2]

	if $ARGS[1] ! 'school' or ARGS[2] ! 6:
		$RESULT += '<TD>' + dyneval '$RESULT += <<$ARGS[1]>>H[<<ARGS[2]>>]' + '</TD>'

		if $ARGS[1] ! 'exercise' and $ARGS[1] ! 'coat' and CloStyle ! 5:
			$RESULT += '<TD>' + dyneval '$RESULT += <<$ARGS[1]>>B[<<ARGS[2]>>]' + '</TD>'
		else
			$RESULT += '<TD></TD>'
		end
	else
		$RESULT += '<TD colspan="2"></TD>'
	end

	if $ARGS[1] ! 'coat' and $ARGS[1] ! 'swimwear':
		gs 'clothing_descriptions'
		$RESULT += '<TD><<FUNC(''$short_description'', $ARGS[1])>> <<ARGS[2]>><br/><br/><<$description>></TD>'
	else
		$RESULT += '<TD>' + FUNC('$clothing_name', $ARGS[1], ARGS[2]) + '</TD>'
	end

	if CloBimbo = 1:
		$RESULT += '<TD>Yes</TD>'
	else
		$RESULT += '<TD>No</TD>'
	end

	if CloStyle = 4:
		$RESULT += '<TD>Yes</TD>'
	else
		$RESULT += '<TD>No</TD>'
	end

	$RESULT += '<TD><a href="exec:gt ''clothing'', ''view_clothing_item'', ''<<$ARGS[0]>>'', ''<<$ARGS[1]>>'', <<ARGS[2]>>">View</a>'

	if $ARGS[0] ! 'sell' and $ARGS[0] ! 'resize':
		if StoryLine = 1 and SchoolAtestat = 0 and SchoolBlock = 0 and $ARGS[1] = 'school' and ARGS[2] = 6:
			$RESULT += ' <a href="exec:gs ''clothing'', ''wear'', ''<<$ARGS[1]>>'', <<ARGS[2]>> & gt ''clothing'', ''view_clothing_list'', ''wardrobe''">Wear</a></TD>'
		elseif ($ARGS[1] ! 'coat' and $ARGS[1] ! 'swimwear') and dyneval('RESULT = <<$ARGS[1]>>H[<<ARGS[2]>>]') > 0:
			if $ARGS[1] ! 'exercise' and CloStyle ! 5 and (dyneval('RESULT = <<$ARGS[1]>>B[<<ARGS[2]>>]') < (pcs_hips - 8) or dyneval('RESULT = <<$ARGS[1]>>B[<<ARGS[2]>>]') > (pcs_hips + 8)) or (ARRPOS('$CloLosTyp', $ARGS[1]) ! -1 and ARRPOS('CloLosNum',ARGS[2]) ! -1):
				$RESULT += '</TD>'
			else
				$RESULT += ' <a href="exec:gs ''clothing'', ''wear'', ''<<$ARGS[1]>>'', <<ARGS[2]>> & gt ''clothing'', ''view_clothing_list'', ''wardrobe''">Wear</a></TD>'
			end
		else
			$RESULT += '</TD>'
		end

		$RESULT += '<TD>'
		if cloc ! 1:$RESULT += ' <a href="exec:gt ''clothing'', ''dest1'', ''<<$ARGS[0]>>'', ''<<$ARGS[1]>>'', <<ARGS[2]>>">Storage</a><BR>'
		if cloc ! 0:$RESULT += ' <a href="exec:gt ''clothing'', ''dest0'', ''<<$ARGS[0]>>'', ''<<$ARGS[1]>>'', <<ARGS[2]>>">Wardrobe</a><BR>'
		if $ARGS[1] ! 'school' or ARGS[2] ! 6:
			if cloc ! 2:$RESULT += ' <a href="exec:gt ''clothing'', ''dest2'', ''<<$ARGS[0]>>'', ''<<$ARGS[1]>>'', <<ARGS[2]>>">Unwanted</a>'
		end
		$RESULT += '</TD>'
	end
}

!!cloc, 0 = wardrobe, 1 = storage, 2 = unwanted


if $ARGS[0] = 'dest0':
	*clr
	dynamic '<<$ARGS[2]>>S[<<ARGS[3]>>] = 0'
	gt 'clothing', 'view_clothing_list', $ARGS[1]
end

if $ARGS[0] = 'dest1':
	*clr
	dynamic '<<$ARGS[2]>>S[<<ARGS[3]>>] = 1'
	gt 'clothing', 'view_clothing_list', $ARGS[1]
end

if $ARGS[0] = 'dest2':
	*clr
	dynamic '<<$ARGS[2]>>S[<<ARGS[3]>>] = 2'
	gt 'clothing', 'view_clothing_list', $ARGS[1]
end


if $ARGS[0] = 'view_clothing_list':
	!! ARGS 0 - view_clothing_list
	!! ARGS 1 - action type (wardrobe, sell, resize, store, unwanted)
	!! ARGS 2 - Shop name (kats, moncheri, gm, fashionista, sexshop, exhibitshop, dolls, sports)

	if $ARGS[2] = 'gm':
		'<center><b><font color="maroon">Wardrobe list - Viewing clothes from G&M</font></b></center>'
		'<center><img src="images/locations/city/citycenter/mall/GM.png"></center>'
	elseif $ARGS[2] = 'kats':
		'<center><b><font color="maroon">Wardrobe list - Viewing clothes from PussyKats</font></b></center>'
		'<center><img src="images/locations/city/citycenter/mall/pussycat.png"></center>'
	elseif $ARGS[2] = 'fashionista':
		'<center><b><font color="maroon">Wardrobe list - Viewing clothes from Fashionista</font></b></center>'
		'<center><img src="images/locations/city/citycenter/mall/fashionistat.png"></center>'
	elseif $ARGS[2] = 'moncheri':
		'<center><b><font color="maroon">Wardrobe list - Viewing clothes from Mon Cheri</font></b></center>'
		'<center><img src="images/locations/city/citycenter/mall/moncheri.png"></center>'
	elseif $ARGS[2] = 'sexshop':
		'<center><b><font color="maroon">Wardrobe list - Viewing clothes from the sex shop</font></b></center>'
		'<center><img src="images/locations/city/citycenter/sexshop/shop_name.png"></center>'
	elseif $ARGS[2] = 'exhibitshop':
		'<center><b><font color="maroon">Wardrobe list - Viewing clothes from the Exhibitionist shop</font></b></center>'
		'<center><img src="images/locations/oldtown/exhibitshop/shop_name.png"></center>'
	elseif $ARGS[2] = 'dolls':
		'<center><b><font color="maroon">Wardrobe list - Viewing clothes from Patchwork Dolls</font></b></center>'
		'<center><img src="images/locations/city/island/dolls.png"></center>'
	elseif $ARGS[2] = 'sports':
		'<center><b><font color="maroon">Wardrobe list - Viewing sportswear from Danilovich</font></b></center>'
		'<center><img src="images/locations/city/citycenter/mall/sports.png"></center>'
	else
		'<center><b><font color="maroon">Wardrobe list - click on a shop name to view clothes from that shop</font></b></center>'
	end
	*nl
	*nl

	if $ARGS[1] ! 'sell':
		$clothing_header = '<center><table border=1>'
		if $ARGS[1] ! 'wardrobe': $clothing_header += '<TH><a href="exec:gt ''clothing'', ''view_clothing_list'', ''wardrobe'', ''<<$ARGS[2]>>''">Switch to wardrobe list</a></TH>'
		if $ARGS[1] ! 'store': $clothing_header += '<TH><a href="exec:gt ''clothing'', ''view_clothing_list'', ''store'', ''<<$ARGS[2]>>''">Switch to stored clothing list</a></TH>'
		if $ARGS[1] ! 'unwanted': $clothing_header += '<TH><a href="exec:gt ''clothing'', ''view_clothing_list'', ''unwanted'', ''<<$ARGS[2]>>''">Switch to unwanted clothing list</a></TH>'
		$clothing_header += '<TH><a href="exec:gt ''clothing_QV'', ''list''">Switch to icons</a></TH></center>'

		$clothing_header
	end

	if $ARGS[1] = 'wardrobe':
		cloc = 0
		if $clothingworntype ! 'nude':act 'Strip':gs 'clothing', 'strip' & gt $loc, $metka
	end

	act 'Leave':gt $loc, $metka

	if $ARGS[2] = '':
		'<center><a href="exec:gt ''clothing'', ''view_clothing_list'', ''<<$ARGS[1]>>'', ''gm''"><img src="images/locations/city/citycenter/mall/GM.png"></a></center>'

		if ARRSIZE('average') > 0:'<center><a href="exec:gt ''clothing'', ''view_clothing_list'', ''<<$ARGS[1]>>'', ''kats''"><img src="images/locations/city/citycenter/mall/pussycat.png"></a></center>'

		if ARRSIZE('expensive') > 0:'<center><a href="exec:gt ''clothing'', ''view_clothing_list'', ''<<$ARGS[1]>>'', ''fashionista''"><img src="images/locations/city/citycenter/mall/fashionistat.png"></a></center>'

		if ARRSIZE('formal') > 0:'<center><a href="exec:gt ''clothing'', ''view_clothing_list'', ''<<$ARGS[1]>>'', ''moncheri''"><img src="images/locations/city/citycenter/mall/moncheri.png"></a></center>'

		if ARRSIZE('fetish') > 0 or ARRSIZE('burlesque') > 0:'<center><a href="exec:gt ''clothing'', ''view_clothing_list'', ''<<$ARGS[1]>>'', ''sexshop''"><img src="images/locations/city/citycenter/sexshop/shop_name.png"></a></center>'

		if ARRSIZE('exhibit') > 0:'<center><a href="exec:gt ''clothing'', ''view_clothing_list'', ''<<$ARGS[1]>>'', ''exhibitshop''"><img src="images/locations/oldtown/exhibitshop/shop_name.png"></a></center>'

		if ARRSIZE('alternative') > 0:'<center><a href="exec:gt ''clothing'', ''view_clothing_list'', ''<<$ARGS[1]>>'', ''dolls''"><img src="images/locations/city/island/dolls.png"></a></center>'

		if ARRSIZE('exercise') > 0:'<center><a href="exec:gt ''clothing'', ''view_clothing_list'', ''<<$ARGS[1]>>'', ''sports''"><img src="images/locations/city/citycenter/mall/sports.png"></a></center>'

	else


		$bgcolor='#f3f4ee'

		if $ARGS[1] = 'sell':
			'<table border=0 cellspacing=0 cellpadding=5><TH></TH><TH>Strength</TH><TH>Hip size</TH><TH>Description</TH><TH>Bimbo</TH><TH>Prostitute</TH><TH>Actions</TH>'
		else
			'<table border=0 cellspacing=0 cellpadding=5><TH></TH><TH>Strength</TH><TH>Hip size</TH><TH>Description</TH><TH>Bimbo</TH><TH>Prostitute</TH><TH>Actions</TH><TH>Move to</TH>'
		end
		
		if $ARGS[1] = 'store':
			cloc = 1
		end
		
		if $ARGS[1] = 'unwanted':
			cloc = 2
		end

		if $ARGS[2] = 'gm':

			if StoryLine = 1 and SchoolAtestat = 0 and SchoolBlock = 0 and schoolS[6] = cloc and ($clothingworntype ! 'school' or clothingwornnumber ! 6):*P dyneval($clothing_list_line, $ARGS[1], 'school', 6)

			i = 1
			:loopcheap
				if cheap[i] = 1 and cheapS[i] = cloc and ($clothingworntype ! 'cheap' or clothingwornnumber ! i):*P dyneval($clothing_list_line, $ARGS[1], 'cheap', i)
				i += 1
			if i <= ARRSIZE('cheap'):jump 'loopcheap'

			i = 1
			:loopoffice
				if office[i] = 1 and officeS[i] = cloc and ($clothingworntype ! 'office' or clothingwornnumber ! i):*P dyneval($clothing_list_line, $ARGS[1], 'office', i)
				i += 1
			if i <= ARRSIZE('office'):jump 'loopoffice'

			i = 1
			:loopschool
				if school[i] = 1 and schoolS[i] = cloc and ($clothingworntype ! 'school' or clothingwornnumber ! i):*P dyneval($clothing_list_line, $ARGS[1], 'school', i)
				i += 1
				if i = 6: i = 7
			if i <= ARRSIZE('school'):jump 'loopschool'

			i = 1
			:loopuniform
				if uniform[i] = 1 and uniformS[i] = cloc and ($clothingworntype ! 'uniform' or clothingwornnumber ! i):*P dyneval($clothing_list_line, $ARGS[1], 'uniform', i)
				i += 1
			if i <= ARRSIZE('uniform'):jump 'loopuniform'

			i = 1
			:loopswimwear
				if swimwear[i] = 1 and swimwearS[i] = cloc :*P dyneval($clothing_list_line, $ARGS[1], 'swimwear', i)
				i += 1
			if i <= ARRSIZE('swimwear'):jump 'loopswimwear'

			act 'Choose a different store': gt 'clothing', 'view_clothing_list', $ARGS[1]
		end

		if $ARGS[2] = 'kats':

			i = 1
			:loopaverage
				if average[i] = 1 and averageS[i] = cloc and ($clothingworntype ! 'average' or clothingwornnumber ! i):*P dyneval($clothing_list_line, $ARGS[1], 'average', i)
				i += 1
			if i <= ARRSIZE('average'):jump 'loopaverage'

			act 'Choose a different store': gt 'clothing', 'view_clothing_list', $ARGS[1]
		end

		if $ARGS[2] = 'fashionista':

			i = 1
			:loopexpensive
				if expensive[i] = 1 and expensiveS[i] = cloc and ($clothingworntype ! 'expensive' or clothingwornnumber ! i):*P dyneval($clothing_list_line, $ARGS[1], 'expensive', i)
				i += 1
			if i <= ARRSIZE('expensive'):jump 'loopexpensive'

			act 'Choose a different store': gt 'clothing', 'view_clothing_list', $ARGS[1]
		end

		if $ARGS[2] = 'moncheri':

			i = 1
			:loopformal
				if formal[i] = 1 and formalS[i] = cloc and ($clothingworntype ! 'formal' or clothingwornnumber ! i):*P dyneval($clothing_list_line, $ARGS[1], 'formal', i)
				i += 1
			if i <= ARRSIZE('formal'):jump 'loopformal'

			act 'Choose a different store': gt 'clothing', 'view_clothing_list', $ARGS[1]
		end

		if $ARGS[2] = 'sexshop':

			i = 1
			:loopfetish
				if fetish[i] = 1 and fetishS[i] = cloc and ($clothingworntype ! 'fetish' or clothingwornnumber ! i):*P dyneval($clothing_list_line, $ARGS[1], 'fetish', i)
				i += 1
			if i <= ARRSIZE('fetish'):jump 'loopfetish'

			act 'Choose a different store': gt 'clothing', 'view_clothing_list', $ARGS[1]

			i = 1
			:loopburlesque
				if burlesque[i] = 1 and burlesqueS[i] = cloc and ($clothingworntype ! 'burlesque' or clothingwornnumber ! i):*P dyneval($clothing_list_line, $ARGS[1], 'burlesque', i)
				i += 1
			if i <= ARRSIZE('burlesque'):jump 'loopburlesque'

			act 'Choose a different store': gt 'clothing', 'view_clothing_list', $ARGS[1]
		end

		if $ARGS[2] = 'exhibitshop':

			i = 1
			:loopexhibit
				if exhibit[i] = 1 and exhibitS[i] = cloc and ($clothingworntype ! 'exhibit' or clothingwornnumber ! i):*P dyneval($clothing_list_line, $ARGS[1], 'exhibit', i)
				i += 1
			if i <= ARRSIZE('exhibit'):jump 'loopexhibit'

			act 'Choose a different store': gt 'clothing', 'view_clothing_list', $ARGS[1]
		end

		if $ARGS[2] = 'dolls':

			i = 1
			:loopalternative
				if alternative[i] = 1 and alternativeS[i] = cloc and ($clothingworntype ! 'alternative' or clothingwornnumber ! i):*P dyneval($clothing_list_line, $ARGS[1], 'alternative', i)
				i += 1
			if i <= ARRSIZE('alternative'):jump 'loopalternative'

			act 'Choose a different store': gt 'clothing', 'view_clothing_list', $ARGS[1]
		end

		if $ARGS[2] = 'sports':

			i = 1
			:loopexercise
				if exercise[i] = 1 and exerciseS[i] = cloc and ($clothingworntype ! 'exercise' or clothingwornnumber ! i):*P dyneval($clothing_list_line, $ARGS[1], 'exercise', i)
				i += 1
			if i <= ARRSIZE('exercise'):jump 'loopexercise'

			act 'Choose a different store': gt 'clothing', 'view_clothing_list', $ARGS[1]
		end

		'</table>'
	end
end

if $ARGS[0] = 'view_clothing_item':
	!! ARGS 0 - view_clothing_item
	!! ARGS 1 = action type (wardrobe, shop, sell, resize)
	!! ARGS 2 - clothing type
	!! ARGS 3 - clothing index
	!! ARGS 4 - price for shop

	cla
	'<center><img src="<<FUNC(''$clothing_image'',  $ARGS[2], ARGS[3])>>"></center>'
	gs 'clothing_attributes', $ARGS[2], ARGS[3]
	$RESULT = FUNC('$short_description', $ARGS[2], CloStyle2)
	$RESULT += '<<ARGS[3]>>'
	'<<$RESULT>>'
	if $ARGS[2] ! 'coat' and $ARGS[2] ! 'swimwear':
		gs 'clothing_descriptions'
		'<<$description>>'
	else
		FUNC('$clothing_name', $ARGS[2], ARGS[3])
	end

	if CloBimbo = 1:'This item is considered bimbo clothing.'

	if CloStyle = 4:'This outfit can be used for prostitution.'
	
	if CloExhibit + 10 > pcs_exhibition: 'You find this outfit more revealing than you are completely comfortable with but that makes it quite exciting too.'

	if $ARGS[2] = 'uniform':
		if CloStyle2 = 1:
			'This outfit is considered to be a maid uniform.'
		elseif CloStyle2 = 3:
			'This is a stripper outfit.'
		else
			'This outfit is a server uniform, suitable for jobs that require one.'
		end
	end

	if $ARGS[2] = 'school':
		if CloSkirtShortness < 5 and CloThinness < 5 and CloBra = 0:
			'<font color="blue">This uniform complies with the school regulations on skirt length.</font>'
		elseif CloSkirtShortness = 5 and CloThinness < 5 and CloBra = 0:
			'<font color="blue">This uniform slightly breaches the school regulations on skirt length.</font>'
		else
			'<font color="red">This uniform makes a mockery of the school uniform regulations.</font>'
		end
	end

	if $ARGS[1] ! 'shop' and ($ARGS[2] ! 'school' or ARGS[3] ! 6):
		$RESULT = '(strength '
		dynamic '$RESULT += <<$ARGS[2]>>H[<<ARGS[3]>>]'
		if $ARGS[2] ! 'exercise' and $ARGS[2] ! 'coat' and CloStyle ! 5:
			$RESULT += ', hip size '
			dynamic '$RESULT += <<$ARGS[2]>>B[<<ARGS[3]>>]'
		end
		$RESULT += ')'
		'<<$RESULT>>'
	end

	if $ARGS[1] = 'shop':
		if dyneval('RESULT = <<$ARGS[2]>>[<<ARGS[3]>>]') = 1:
			'You already own this item.'
			act 'Leave': gt $loc, $metka
		else
			if $ARGS[2] = 'coat' and ARGS[3] = 5:
				price = ARGS[4]
			else
				price = ('<<ARGS[4]>>' * ((5 * CloQuality) + 100) / 100) * 1000 / (1250 - Clothingstock[ARGS[3]]) * 3 / 2
				price = price / 50 * 50
			end
			'Price: <<price>> <b>₽</b>'
			act 'Leave': gt $loc, $metka
			if CloExhibit > pcs_exhibition:
				'You don''t feel daring enough to wear an outfit this revealing and can''t bring yourself to buy it.'
			elseif money >= Price:
				act 'Buy':
					h = 0
					if CloStyle = 2:h = 10
					if CloStyle = 5:h = 20
					h += (3 * CloQuality)
					money -= price
					dynamic $ARGS[2] + '[<<ARGS[3]>>] = 1'
					if $ARGS[2] = 'cheap' or $ARGS[2] = 'office':
						dynamic $ARGS[2] + 'H[<<ARGS[3]>>] = 70 + <<h>>'
					elseif $ARGS[2] = 'average':
						dynamic $ARGS[2] + 'H[<<ARGS[3]>>] = 90 + <<h>>'
					elseif $ARGS[2] = 'formal':
						dynamic $ARGS[2] + 'H[<<ARGS[3]>>] = 15 + <<h>>'
					elseif $ARGS[2] = 'fetish' or $ARGS[2] = 'exhibit' or $ARGS[2] = 'burlesque':
						dynamic $ARGS[2] + 'H[<<ARGS[3]>>] = 35 + <<h>>'
					elseif $ARGS[2] = 'school' or $ARGS[2] = 'uniform':
						dynamic $ARGS[2] + 'H[<<ARGS[3]>>] = 120 + <<h>>'
					elseif $ARGS[2] = 'exercise':
						dynamic $ARGS[2] + 'H[<<ARGS[3]>>] = 100 + <<h>>'
					elseif $ARGS[2] = 'coat':
						dynamic $ARGS[2] + 'H[<<ARGS[3]>>] = 250 + <<h>>'
					else
						dynamic $ARGS[2] + 'H[<<ARGS[3]>>] = 60 + <<h>>'
					end
					dynamic $ARGS[2] + 'B[<<ARGS[3]>>] = pcs_hips'
					dynamic $ARGS[2] + 'S[<<ARGS[3]>>] = 0'
					gt $loc, $metka
				end
			else
				'You cannot afford this outfit.'
			end
		end
	elseif $ARGS[1] = 'wardrobe':
		act 'Leave in closet':gt 'clothing', 'view_clothing_list', $ARGS[1]

		if $ARGS[2] ! 'school' or ARGS[3] ! 6:
			!! if the clothing is worn out
			if dyneval('RESULT = <<$ARGS[2]>>H[<<ARGS[3]>>]') <= 0:
				'This item is worn and is not suitable for further wear.'
				if pcs_sewng >= 60 and tkan > 0:
					cla
					'You can repair this thanks to your sewing skill.'
					act 'Repair item':
						if rand (1, 100) < 30:
							cla
							tkan -= 1
							minut += 30
							dynamic $ARGS[2] + 'H[<<ARGS[3]>>] += 30'
							'You spend 30 minutes fixing the outfit.'
							act 'Return':gt 'clothing', 'view_clothing_list', $ARGS[1]
						else
							cla
							tkan += 3
							minut += 30
							dynamic $ARGS[2] + '[<<ARGS[3]>>] = 0'
							'Despite your skill with a needle, this outfit has proved beyond repair. All that is left of it is only good for your cloth pile.'
							act 'Return':gt 'clothing', 'view_clothing_list', $ARGS[1]
						end
					end
					act 'Leave in closet': gt 'clothing', 'view_clothing_list', $ARGS[1]
				end
			exit

			end

			!! if the clothing is a type that checks for hip size
			if $ARGS[2] ! 'exercise' and $ARGS[2] ! 'coat' and CloStyle ! 5:
				!! if the hipsize is too far from your hipsize
				dynamic 'RESULT = <<$ARGS[2]>>B[<<ARGS[3]>>]'
				if RESULT < pcs_hips - 8 or RESULT > pcs_hips + 8:
					if RESULT < pcs_hips:
						'This item is too small for you to wear.'
					else
						'This item is too large for you to wear.'
					end
				   
					if pcs_sewng >= 60 and tkan > 0:
						cla
						'You can resize this thanks to your sewing skill.'
						act 'Resize item':
							cla
							tkan -= 1
							minut += 30
							dynamic $ARGS[2] + 'B[<<ARGS[3]>>] = pcs_hips'
							'You spend 30 minutes resizing the outfit.'
							act 'Return':gt 'clothing', 'view_clothing_list', $ARGS[1]
						end

						act 'Leave in closet': gt 'clothing', 'view_clothing_list', $ARGS[1]
					end
				exit

				end
			end
		end

		if $ARGS[2] ! 'coat' and $ARGS[2] ! 'swimwear':
		
			if CloExhibit > pcs_exhibition:
				'You don''t feel daring enough to wear an outfit this revealing.'
			elseif ARRPOS('$CloLosTyp', $ARGS[2]) ! -1 and ARRPOS('CloLosNum',ARGS[3]) ! -1 :
				'You lost these clothes somewhere, maybe you can find them again?.'				
			else
				act 'Wear':
					gs 'clothing', 'wear', $ARGS[2], ARGS[3]

					gt 'clothing', 'view_clothing_list', $ARGS[1]
				end
			end
		end
	elseif $ARGS[1] = 'store' or $ARGS[1] = 'unwanted':
		act 'Return':gt 'clothing', 'view_clothing_list', $ARGS[1]

		if $ARGS[2] ! 'coat':
			act 'Move back to wardrobe':
				dynamic $ARGS[2] + 'S[<<ARGS[3]>>] = 0'
				gt 'clothing', 'view_clothing_list', $ARGS[1]
			end
		end

	elseif $ARGS[1] = 'sell':
		act 'Keep item':gt 'clothing', 'view_clothing_list', $ARGS[1]

		!! value is based on the strength remaining
		dynamic 'RESULT = <<$ARGS[2]>>H[<<ARGS[3]>>]'
		if RESULT > 45:
			'You can sell this item for 500 <b>₽</b>.'
			act 'Sell':
				dynamic '<<$ARGS[2]>>[<<ARGS[3]>>] = 0'
				money += 500
				gt 'clothing', 'view_clothing_list', $ARGS[1]
			end
		elseif RESULT > 30:
			'You can sell this item for 300 <b>₽</b>.'
			act 'Sell':
				dynamic '<<$ARGS[2]>>[<<ARGS[3]>>] = 0'
				money += 300
				gt 'clothing', 'view_clothing_list', $ARGS[1]
			end
		elseif RESULT > 0:
			'You can sell this item for 100 <b>₽</b>.'
			act 'Sell':
				dynamic '<<$ARGS[2]>>[<<ARGS[3]>>] = 0'
				money += 100
				gt 'clothing', 'view_clothing_list', $ARGS[1]
			end
		else
			'This item is too worn to have any sale value.'
			act 'Throw it away':
				dynamic '<<$ARGS[2]>>[<<ARGS[3]>>] = 0'
				gt 'clothing', 'view_clothing_list', $ARGS[1]
			end
		end
	elseif $ARGS[1] = 'resize':
		act 'Leave item alone':gt 'clothing', 'view_clothing_list', $ARGS[1]

		if $ARGS[2] ! 'exercise' and $ARGS[2] ! 'coat' and CloStyle ! 5:
			if dyneval('RESULT = <<$ARGS[2]>>B[<<ARGS[3]>>]') ! pcs_hips and money >= 500:
				'You can have this resized for 500 <b>₽</b>.'
				act 'Resize':
					money -= 500
					dynamic '<<$ARGS[2]>>B[<<ARGS[3]>>] = pcs_hips'
					gt 'clothing', 'view_clothing_list', $ARGS[1]
				end
			end
		end
	end
end

if $ARGS[0] = 'strip':
	!$ARGS[1] = location when stripping outdoors
	if $clothingworntype = 'nude': exit
	cumspclnt = 6
	gs 'cum_cleanup'
	if cloth_vid = 1:
		last_vid = 1
	else
		last_vid = 0
	end
	
	if $clothingworntype = 'school':schtype = clothingwornnumber
		
	if $ARGS[1] ! '' :
		DYNAMIC '$CloLos<<$clothingworntype>>[<<clothingwornnumber>>]=<<$ARGS[1]>>'		
		$CloLosLoc[] = $ARGS[1]
		$CloLosTyp[$ARGS[1]] = $clothingworntype
		CloLosNum[$ARGS[1]] = clothingwornnumber
		CloLosDay[$ARGS[1]] = daystart
		KILLVAR '$lastwornclothingtype'
		KILLVAR 'lastwornclothingnumber'
	else
		$lastwornclothingtype = $clothingworntype
		lastwornclothingnumber = clothingwornnumber
	end
	

	$clothingworntype = 'nude'
	clothingwornnumber = 0
	CloQuality = 0
	CloThinness = 0
	CloTopCut = 0
	CloBra = 0
	CloPantsShortness = 0
	CloSkirtShortness = 0
	CloPanties = 0
	CloStyle = 0
	CloStyle2 = 0
	CloBimbo = 0
	PShoQuaility = 0
	PShoCut = 0
	PShoHeels = 0
	PShoStyle = 0
	PCloQuality = 0
	PCloThinness = 0
	PCloTopCut = 0
	PCloPants = 0
	PCloSkirt = 0
	PCloStyle = 0
	PCloStyle2 = 0
	PCloBimbo = 0
	PCloExhibit = 0
	PXCloThinness = 0
	PXCloTopCut = 0
	PXCloSkirtShortness = 0
	PXCloPantsShortness = 0
	glamur = 0
	sport = 0
	ski = 0
	ofclo = 0
	sexual = 0
	odevnesh = 0
	! WD: Bimbo look
	cloth_vid = 0
	exhibiHideNewCloth = 0
end


if $ARGS[0] = 'recover_lost_clothes':
	!$ARGS[1] = location
	!ARGS[2] = 1 : wear the clothes, ARGS[2] = 2 : Return to shop (lost FOREVAH!)
	$cloLoc = $ARGS[1]
	cloAction = ARGS[2]
	
	$clothType = $CloLosTyp[$cloLoc]
	clothNumber = CloLosNum[$cloLoc]
	dayLost = CloLosDay[$cloLoc]
		
	DYNAMIC 'KILLVAR ''$CloLos<<$clothingworntype>>'', <<clothingwornnumber>>'
	findLocInd = ARRPOS('$CloLosTyp', $clothType)
	KILLVAR '$CloLosTyp', findLocInd
	findLocInd = ARRPOS('CloLosNum', clothNumber)
	KILLVAR 'CloLosNum', findLocInd
	findLocInd = ARRPOS('CloLosDay', CloLosDay[$cloLoc])
	KILLVAR 'CloLosDay', findLocInd
	findLocInd = ARRPOS('$CloLosLoc', $cloLoc)
	KILLVAR '$CloLosLoc', findLocationIndex
	KILLVAR 'findLocInd'
	
	if cloAction = 1:
		gs 'clothing', 'wear', $clothType, clothNumber
	elseif cloAction = 2 :
		dynamic $clothType + 'S[<<clothNumber>>] = 1'
	end
	
	KILLVAR '$cloLoc'
	KILLVAR 'cloAction'
	KILLVAR '$clothType'
	KILLVAR 'clothNumber'
end

if $ARGS[0] = 'wear':
	!! ARGS 0 - action
	!! ARGS 1 - clothing type name
	!! ARGS 2 - clothing index

	gs 'clothing', 'strip'

	$clothingworntype = $ARGS[1]
	clothingwornnumber = ARGS[2]

	dynamic $ARGS[1] + 'W[<<ARGS[2]>>] = 1'

	if $ARGS[1] ! 'coat' and $ARGS[1] ! 'swimwear':
		gs 'clothing_descriptions'
	else
		FUNC('$clothing_name', $ARGS[1], ARGS[2])
	end

	gs 'clothing_attributes', $ARGS[1], ARGS[2]

	dynamic '<<$ARGS[1]>>S[<<ARGS[2]>>] = 0'

	PCloQuality = CloQuality
	PCloThinness = CloThinness
	PCloTopCut = CloTopCut
	PCloBra = CloBra
	PCloPants = CloPantsShortness
	PCloSkirt = CloSkirtShortness
	PCloPanties = CloPanties
	PCloStyle = CloStyle
	PCloStyle2 = CloStyle2
	PCloExhibit = CloExhibit
	if $cheatBimbo ! '<b>ON</b>':
		PCloBimbo = CloBimbo
		if CloBimbo = 1:bimbocl = 1
	else
		PCloBimbo = 0
		bimbocl = 0
	end

	if PCloTopCut > 1:
		if tits = 2 or tits = 3:PCloTopCut += 1
		if tits = 4 or tits = 5:PCloTopCut += 2
		if tits >= 6:PCloTopCut += 3
	end

!Calculations for clothing beauty, multiplied by natural beauty
	if PCloThinness > 0:
		if PCloThinness = 1:
			PXCloThinness = 25
		elseif PCloThinness = 2:
			PXCloThinness = 50
		elseif PCloThinness = 3:
			PXCloThinness = 100
		elseif PCloThinness = 4:
			PXCloThinness = 150
		elseif PCloThinness = 5:
			PXCloThinness = 200
		elseif PCloThinness = 6:
			PXCloThinness = 300
		elseif PCloThinness >= 7:
			PXCloThinness = 400
		end
	end

	if PCloTopCut > 0:
		if PCloTopCut = 1:
			PXCloTopCut = 25
		elseif PCloTopCut = 2:
			PXCloTopCut = 50
		elseif PCloTopCut = 3:
			PXCloTopCut = 100
		elseif PCloTopCut = 4:
			PXCloTopCut = 150
		elseif PCloTopCut = 5:
			PXCloTopCut = 200
		elseif PCloTopCut = 6:
			PXCloTopCut = 300
		elseif PCloTopCut >= 7:
			PXCloTopCut = 400
		end
	end

	if PCloBra = 1:
		PXCloTopCut = 400
	elseif PCloBra = 2:
		PXCloTopCut = 500
	end

	if PCloSkirt > 0:
		if PCloSkirt = 1:
			PXCloSkirtShortness = 25
		elseif PCloSkirt = 2:
			PXCloSkirtShortness = 50
		elseif PCloSkirt = 3:
			PXCloSkirtShortness = 100
		elseif PCloSkirt = 4:
			PXCloSkirtShortness = 150
		elseif PCloSkirt = 5:
			PXCloSkirtShortness = 200
		elseif PCloSkirt = 6:
			PXCloSkirtShortness = 300
		elseif PCloSkirt >= 7:
			PXCloSkirtShortness = 400
		end
	end

	if PCloPants > 0:
		if PCloPants = 1:
			PXCloPantsShortness = 25
		elseif PCloPants = 2:
			PXCloPantsShortness = 50
		elseif PCloPants = 3:
			PXCloPantsShortness = 100
		elseif PCloPants = 4:
			PXCloPantsShortness = 150
		elseif PCloPants = 5:
			PXCloPantsShortness = 200
		elseif PCloPants = 6:
			PXCloPantsShortness = 300
		elseif PCloPants >= 7:
			PXCloPantsShortness = 400
		end
	end

	if PCloPanties = 1: PXCloPantsShortness = 400

end

if $ARGS[0] = 'wear_last_worn':
	gs 'clothing', 'wear', $lastwornclothingtype, lastwornclothingnumber
end

--- clothing ---------------------------------

