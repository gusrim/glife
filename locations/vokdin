# vokdin
$din_vokzalg_kassi = {
	cls
	gs 'stat'
	'<center><h4>Ticket office</h4></center>'
	'<center><img src="images/qwest/alter/kassa.jpg"></center>'
	'A ticket to the city costs 200 <b>₽</b>, a ticket to Gadyukino costs 50 <b>₽</b>.'
	'The long-distance trains are at the station platforms between 11:00-11:30 and 18:00-18:30.'

	if money >= 200:
		act 'Buy a ticket to the City (200 <b>₽</b>)':
			cls
			money -= 200
			minut += 15
			gs 'stat'
			'<center><img src="images/etogame/electri.jpg"></center>'
			'You pay for a ticket to the city, and board the local train waiting at the station.'
			'15 minutes later the train stops at the station near the village of Gadyukino.'

			act 'Continue towards the city':
				cls
				minut += 15
				gs 'stat'
				'<center><img src="images/etogame/electri.jpg"></center>'
				'After 15 minutes the train stops at a small station. It''s abandoned, and there is not much activity outside.'

				act 'Continue towards the city':
					cls
					minut += 15
					gs 'stat'
					'<center><img src="images/etogame/electri.jpg"></center>'
					'After 15 minutes the train stops at another small station. It''s abandoned, and there is not much activity outside.'

					act 'Continue towards the city':
						cls
						minut += 15
						gs 'stat'
						'<center><img src="images/etogame/electri.jpg"></center>'
						'After 15 minutes the train reaches its final stop, in the northern district of the City.'

						act 'Leave the train':minut += 5 & nroad = 0 & gt 'Vokzal'
					end

					act 'Leave the train':minut += 5 & nroad = 5 & gt 'road'
				end

				act 'Leave the train':minut += 5 & nroad = 10 & gt 'road'
			end

			act 'Leave the train at Gadyukino':minut += 5 & nroad = 15 & gt 'road'
		end
	end

	if money >= 50:
		act 'Buy a ticket to Gadyukino (50 <b>₽</b>)':
			cls
			money -= 50
			minut += 15
			gs'stat'

			'<center><img src="images/etogame/electri.jpg"></center>'
			'You pay for a ticket, and board the local train going to Gadyukino.'
			'After 15 minutes, the train stops at the station near the village of Gadyukino.'

			act 'Leave the train':minut += 5 & nroad = 15 & gt'road'
		end
	end

	act 'Leave the ticket office':minut += 1 & gt 'vokzalGin'
}

$din_vokzalg_kabinet = {
	cls
	gs 'stat'
	'When you walk past the station manager''s office, you notice a sign next to the door:'
	'"Cleaner wanted! 2 hours per day at any time, salary 250 <b>₽</b>. Ask inside for more info."'

	act 'Return to the train station':minut += 1 & gt 'vokzalGin'

	if hour >= 8 and hour <= 16:
		act 'Enter the office':
			cls
			gs 'stat'
			'You knock on the door and enter the station manager''s office. An elderly man looks up from the papers on his desk and says, "Can I help you?"'

			act 'Apologize and leave':minut += 1 & gt 'vokzalGin'

			if vokzalVork = 0:
				act 'Apply for the cleaning job':
					cls
					vokzalVork = 1
					minut += 15
					gs 'stat'
					'You give him a friendly nod: "Hello sir, I would like to apply for the cleaning job."'
					'He''s delighted, and somewhat surprised that he actually has a candidate at such low wages. "Excellent, come on in! Take a seat."'
					'He explains what you have to do while he takes your details: "Your job would be to wash the station floor, and clean both the restrooms. You can come work on any day you like, and can skip any day you don''t want to work."'
					'"You have to clean all three areas before the end of the day, otherwise we have to hire someone else finish it. If you have only cleaned part of the rooms, you do not get paid at all." In conclusion he shows you the maintenance closet where the cleaning supplies are stored and shakes your hand, before returning to his work.'

					act 'Leave the office':minut += 1 & gt 'vokzalGin'
				end
			end
		end
	end
}

$din_vokazalg_podbros2 = {
	cls
	gs 'stat'
	'You give back the money you took from his wallet, but he acts insulted: "That''s not all of it! I had much more in my wallet, where is it!? I want it back now or we''re going to the police station! This is your last chance!"'

	act 'Give him your money':
		cls
		money = 0
		podbros = 1
		gs 'stat'
		'<center><h4>Pavlovsk train station interior</h4></center>'
		'<center><img src="images/qwest/alter/vokzal.jpg"></center>'
		'You don''t want a scandal. Who knows what could happen if you get arrested for theft? Hoping to placate him with a few bills of your own money, you decide to get your wallet. You pull out your own money, and start to count some of it out. Before you can finish, he pulls the entire pile out of your hands: "There it is! You''re lucky, girl... I''ll leave it at this, for just this once."'
		'Before you can object, he walks away with all of your money.'
		
		act 'Leave':minut += 1 & gt 'vokzalGin'
	end
}

$din_vokzalg_podbros = {
	cls
	gs 'stat'
	'<center><h4>Pavlovsk train station interior</h4></center>'
	'<center><img src="images/qwest/alter/vokzal.jpg"></center>'
	'As you reach for the wallet, a foot steps on it just before you can pick it up. You look up, straight into the eyes of a slim girl. She looks around quickly and then softly asks: "Want to share?"'

	act 'Keep it all for yourself':
		cls
		gs 'stat'
		'<center><h4>Pavlovsk train station interior</h4></center>'
		'<center><img src="images/qwest/alter/vokzal.jpg"></center>'
		'You tell the girl to get lost, and quickly push her away and snag the wallet before she can protest. As you remove the money from it and stuff it in your pocket, you feel a hand firmly grasping your shoulder. It''s the man who the wallet belongs to... that girl must have ratted you out!'
		'You sigh and give him the empty wallet back, hoping he won''t check. Unfortunately for you, he checks. He bellows: "Young lady, you have 5 seconds to give me my money back or we''re going to the police station!"'

		act 'Give him back the money you stole':dynamic $din_vokazalg_podbros2
	end

	act 'Let her have the wallet and leave':minut += 1 & gt 'vokzalGin'

	act 'Share the money':
		cls
		gs 'stat'
		'<center><h4>Pavlovsk train station interior</h4></center>'
		'<center><img src="images/qwest/alter/vokzal.jpg"></center>'
		'You pick up the wallet and drag the girl off to the side. Making sure you are not being watched, you quickly split the money in two even piles and give her half. She quickly walks away while you put your share in your pocket. Suddenly, you feel a hand firmly grasping your shoulder. It''s the man who the wallet belongs to...'
		'You sigh and give him the empty wallet back, hoping he won''t check. Unfortunately for you, he checks. He bellows: "Young lady, you have 5 seconds to give me my money back or we''re going to the police station!"'

		act 'Give him back the money you stole':dynamic $din_vokazalg_podbros2
	end
}

--- vokdin ---------------------------------

