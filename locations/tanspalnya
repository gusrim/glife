# tanspalnya
CLOSE ALL

$metka = $ARGS[0]
$loc = $CURLOC
$metkaM = $ARGS[0]
$locM = $CURLOC

minut += 5
tanhouse1 = 1
gs 'stat'
'<center><b>Your Bedroom in Nicholas'' Apartment</b></center>'
'<center><img src="images/img/centr/spaln.jpg"></center>'

'Your bedroom is fairly large, considering you''re only an employee. There is a small <a href="exec:gt ''bed'', ''start''">double bed</a> and an <a href="exec:gt ''budilnik'', ''start''">alarm clock</a>.'
'Half a wall is taken up by an expensive-looking wardrobe (where you can <a href="exec:gt ''clothing'', ''view_clothing_list'', ''wardrobe''">organize your clothes</a> or <a href="exec:gt ''clothing_QV'', ''list''">pick something to wear</a>). There''s a make-up table with a <a href="exec:gt ''mirror'', ''start''">mirror</a> next to it.'
if komp = 1:
	$komp = 'with your <a href="exec:gt ''Komp'', ''start''">laptop</a>.'
else
	$komp = ''
end
'A luxurious <a href="exec:gt ''divan'', ''start''">leather sofa</a> is also part of the ensemble. Mounted on the wall opposite the sofa is a huge plasma <a href="exec:gt ''TV'', ''start''">TV</a>. Under the window, you see a brand-new <a href="exec:gt ''stol'', ''start''">desk</a><<$komp>>.'
if obruch > 0:'<br>Your hula hoop stands in a corner of the room. '
if skak > 0:'Your jump rope is sitting on a shelf. '
if bookYog > 0:'A yoga mat is rolled up in a corner next to the wardrobe. '

if mishka > 0:'On one of the shelves, you''ve placed your <<mishka>> teddy bear.'

sports = 0
$sportStringPrefix = ''

if obruch > 0 and obruchday ! daystart:
	$sitrobr = $sportStringPrefix + ' <a href="exec:dynamic $sitrobrd">Hula Hoop</a>'
	sports = 1
	$sportStringPrefix = ', '
else
	$sitrobr = ''
end

if bookYog > 0 and bookYogday ! daystart:
	$sitryog = $sportStringPrefix + ' <a href="exec:dynamic $sitryogd">Meditate</a>'
	sports = 1
	$sportStringPrefix = ', '
else
	$sitryog = ''
end

if skak > 0 and skakday ! daystart:
	$sitrskak = $sportStringPrefix + ' <a href="exec:dynamic $sitrskakd">Jumping rope</a>'
	sports = 1
	$sportStringPrefix = ', '
else
	$sitrskak = ''
end

if pressday ! daystart:
	$sitrpress = $sportStringPrefix + ' <a href="exec:dynamic $sitrpressd">Abdominal exercises</a>'
	sports = 1
	$sportStringPrefix = ', '
else
	$sitrpress = ''
end

if pushday ! daystart:
	$sitrpush = ' <a href="exec:dynamic $sitrpushd">Push-ups</a>'
	sports = 1
	$sportStringPrefix = ', '
else
	$sitrpush = ''
end


if sports = 1:
	$sportsitr = '<<$sitrpush>><<$sitrpress>><<$sitrskak>><<$sitrobr>><<$sitr_tread>><<$sitryog>>'
	'Despite the plentiful furnishings, the room still has enough space for a variety of sporting activities:<<$sportsitr>>.'
end

if suhbelo2 > 0:'<br>Lying on the table are <<suhbelo2>> pieces of dried laundry that need <a href="exec:dynamic $gladbelo">to be ironed</a>.'

act 'Go to the corridor':
	if $clothingworntype ! 'nude':
		gt 'tanhouse'
		else
		msg'<b><font color = red>You probably shouldn''t walk around naked, especially not at your workplace. You need to get dressed.</font></b>'
		gt $CURLOC
	end
end

act 'Study (2:00)':
	cla
	minut += 120
	intel += 1
	'You spend two hours studying and feel a little smarter now.'

	act 'Finish':gt 'tanspalnya'
end

if student > 0 and kursovik < 100:
	act 'Write course work (1:00)':
		cla
		*clr
		minut += 60
		kursovik += rand(intel / 20, intel / 10)

		if kursovik > 100:kursovik = 100

		manna -= 10
		'<center><img src="images/etogame/study.jpg"></center>'
		'You work on your course work and have now completed <<kursovik>> percent.'

		act 'Finish':gt 'tanspalnya'
	end
end

gs 'events', 'read'

if magik >= 6 and spellbefshild = 1 and tobiQW = 0:gt'tobiQW','start'

if sick >= 1:
	act 'Get some bed rest (illness)':
		cla
		*clr
		minut += 240
		sick -= 1
		if sick < 0:sick = 0
		gs'stat'

		'<center><img src="images/inBed/sick+rand(11,17)+.jpg"></center>'

		'You lie in bed, coughing, sneezing, blowing your nose... and, as if that wasn''t enough, you also think you might be getting a headache.'
		'Drifting in and out of sleep, you spend about 4 hours tossing and turning, trying to sweat out the illness, and you actually feel better when you decide to stretch your legs.'
 
		act 'Get up': gt 'bedr'
	end
end

if horny >= 50:
	act 'Masturbate':
		gt'selfplay'
	end
end

if pornMagazine > 0:'You hide a porn magazine under your mattress.' & dynamic $d_read_porn

if tanga = 0 and clrbelo > 0:
	act 'Wear underwear':
		cla
		tanga = 1
		gt'tanspalnya'
	end
else
	act 'Remove underwear':
		cla
		tanga = 0
		gt'tanspalnya'
	end
end

if dur > 0:'You have hidden <<dur>> doses of Pale Lady behind your wardrobe.'

if dur > 0 and narkday ! daystart:
	act 'Take some cocaine (0:05)':
		cla
		*clr
		minut += 5
		narkday = daystart
		health = vital * 10 + stren * 5
		willpower = intel * 5 + will * 5
		manna = (intel * magik) + magik * 100 + vital * 10 + rikudo
		horny = 100
		nark += 10
		dur -= 1
		'<center><img src="images/etogame/dur.jpg"></center>'
		'You take a small dose of coke and not long after, you start to feel great, as if you could do anything, <b>be</b> anything - but you''re also starting to feel incredibly horny.'

		act 'Leave':gt $curloc
	end
end

if MaidWorkDay ! daystart:
	MaidWorkDay = daystart
	MaidWork = 0
	TanHallClean = 0
	TanRoomClean = 0
	TanBathClean = 0
	TanMasterClean = 0
	TanLivingClean = 0
	TanMaidRoomClean = 0
	TanKitchenClean = 0
	TanMadeBreakfast = 0
end

if tan_work_evalDay ! daystart and tan_work_evaluation = 1:
	tan_work_evalDay = daystart
	tan_work_evaluation = 0

elseif tan_work_evalDay ! daystart and tan_work_evaluation = 0:
	tan_work_evalDay = daystart
	tan_work_rep -= 1
	''
	'You did not go to see Nicholas yesterday evening. Regardless of how much or little work you did, this is sure to decrease your professional standing with him as your employer.'
	tan_work_evaluation = 0
end

if TanMaidRoomClean = 0:
	act 'Clean your Room':
		cla
		minut += 30
		TanMaidRoomClean = 1
		MaidWork += 1
		gs 'stat'
		nl
		''
		'You vacuum, wet wipe, dust and polish all the surfaces and objects in your room, knowing well that, if a guest wanted to take a look inside, you''d embarrass Nicholas if this room wasn''t in as perfect a condition as is expected of all the others.' 
		'At least you are free to wear whatever you want while you clean your room, you think.'
		act 'Finish cleaning':
			gt 'tanspalnya'
		end
	end
end

--- tanspalnya ---------------------------------

