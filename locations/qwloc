# qwloc
$location_type = 'event_outdoors'
$qwnamedy = {
	act '<<$name>>':
		cla
		vladimirQW = 10
		'"<<$name>>," you introduce yourself.'
		'Vladimir smiled. "A beautiful name to match a beautiful woman. I unfortunately have to go, but I would like to meet with you again. We could go to the restaurant to talk. Leave me your phone number, I''ll call you tomorrow.'

		act 'No (Go)':
			cla
			vladimirQW = 2
			'You simply turned on the man and walked away from him.'

			act 'Leave':gt 'park', 'luna'
		end

		act 'Give him your number':
			cla
			vladimirQW = 20
			vladimirday = daystart
			'You dictated your phone number to Vladimir and he wrote it down in a small notebook with a leather cover.'

			act 'Leave':gt 'park', 'luna'
		end
	end
}

$qwvladdy = {
	'The man bowed to you. "Hello. My name is Vladimir and I think I love you, I would like the honour of your name.'
	dynamic $qwnamedy

	act 'Sorry, I''m busy':
		cla
		vladimirQW = 1
		'"Sorry, I''m busy," you excuse yourself.'
		'Vladimir shakes his head to that. "Well, what are you busy with? I will not bite. I just want to know the name of a charming stranger.'
		dynamic $qwnamedy

		act 'Leave anyway':
			cla
			vladimirQW = 2
			'You simply turned on the man and walked away from him.'

			act 'Leave':gt 'park', 'luna'
		end
	end
}

minut += 5
gs 'stat'

'<center><b><font color="maroon">Man</font></b></center>'
'<center><img src="images/qwest/vladimir/001.jpg"></center>'

if mop > 1 and hapri = 1:
	'A man standing near the fence noticed your gaze and smiled.'

	act 'Approach the man':
		cla
		'You approached the man and he left the fence to walk towards you.'
		dynamic $qwvladdy
	end

	act 'Smile':
		cla
		'You smiled at the man and he left the fence to talk towards you.'
		dynamic $qwvladdy
	end

	act 'Ignore':
		cla
		'You turned away from the man but hearing his footsteps approach you, you turn back towards him.'
		dynamic $qwvladdy
	end
else
	'The man returned your gaze for a moment before turning away.'

	act 'Leave':gt 'park', 'luna'
end

--- qwloc ---------------------------------

