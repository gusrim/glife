# beg
if $ARGS[0] = 'start':
	cla
	*clr
	minut += 5
	'<center><img <<$set_imgh>> src="images/locations/shared/gym/fit1.jpg"></center>'
	'You are stretching and warming up before your practice run. From where you are you can see your coach, Igor Yurisovich, talking to other athletes.'

	act 'Do a couple of practice races':
		cla
		*clr
		minut += 30
		abonement -= 1
		pcs_mood += 2
		fat -= 2
		pcs_sweat += 30
		pcs_stam -= 30
		run_exp += rand(2, 3)
		'<center><img <<$set_imgh>> src="images/locations/city/citycenter/gym/exercise/fit5.jpg"></center>'
		'You spend half an hour practising 100 meter dashes to improve your speed, as well as doing several rounds around the stadium to work on your stamina. You are sweaty all over by the end of it, obviously, but you also feel that you''re a little better than before.'

		! WD: Argument is 'dressing room'
		act 'Go to the dressing room':gt 'fit', 'dressing room'
	end
end

if begsec > 0:
	act 'Go to your coach':
		cla
		'Igor Yurisovich is your coach, a man with a rough voice who never seems to gets tired of motivational one-liners. "The only one who can keep you from winning is you" is his current favorite.'

		if pcs_run < 50:
			'Despite looking occupied with the other trainees, Igor Yurisovich not only watched your practice runs but timed them as well. He doesn''t look too happy, though - not that he ever does.'
			'"You have to train harder, girl. With a time like this, you would come in dead last. Hell, you wouldn''t even qualify for the main race! If you want to be better, you have to watch your diet, maybe go to the banya after training, but most importantly: Practice, practice, practice!"'

			act 'Leave':gt 'beg', 'start'
		elseif pcs_run >= 50 and razradbegEG = 0:
			'Despite looking occupied with the other trainees, Igor Yurisovich not only watched your practice runs but timed them as well. And while you''re pretty sure he is physically incapable of looking excited or even content, you think that he looks a little less grim than usual as he looks at you.'
			'"You''re doing fine, girl. Better than fine; you could have a great future in athletics if you play your cards right. If you keep working hard, you might even have it in you to become European champion, perhaps more. But remember, girl, the key to victory is your spirit: If you have the right mindset, you can do anything."'
			
			act 'Leave':gt 'beg', 'start'
		else
			'Igor Yurisovich eagerly watches your practice runs and times them as well. Despite his grim demeanour, he nods approvingly at your times and even gives you a hug when you set a PB.'
			'"You''re doing great, <<$pcs_nickname>>. We''re all so proud of you here and I really can''t give you much more advice except to tell you to keep pushing and making us proud."'
			
			act 'Leave':gt 'beg', 'start'
		end
	end
end

--- beg ---------------------------------

