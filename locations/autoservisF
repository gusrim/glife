# autoservisF
if $ARGS[0] = 'start':
	$metka = $ARGS[0]
	$loc = $CURLOC
	cla
	*clr
	minut += 5
	gs 'stat'
	gs 'avtonorm'
	'<center><b>Car Service Center</b></center>'
	'<center><img src="images/etogame/autoservis1.jpg"></center>'

	if cardrive = 7:
		if wrek = 0:
			if teh >= tehcar * 75 / 100:
				'The foreman inspects your car quickly but thoroughly.' 
				'After only a few minutes, he returns to you and announces that your car doesn''t need any repairs or maintenance.'

			if teh < tehcar * 75 / 100:
				'The foreman inspects your car quickly but thoroughly.' 
				'Here and there, he seems to be murmurring to himself, maybe assessing a defect. After only a few minutes, he returns to you, points out what needs to be fixed.' 
				'It would cost you <<normrem>> <b>₽</b>.'

				if money >= normrem:
					act 'Pay and wait while they repair the car':
						cla
						*clr
						money -= normrem
						minut += 60
						teh = tehcar
						wrek = 0
						'<center><img src="images/etogame/autoservis2.jpg"></center>'
						'Since he tells you that it''s not gonna take long, you go into a waiting or recreation room, with couches and a TV. After about an our of flipping through channels, the car is ready and you pay <<normrem>> <b>₽</b>.'

						act 'Leave':gt 'Nord'
					end
				end
			end
		elseif wrek = 1:
			'The foreman looks onto your car, if this wreckage even qualifies as one, with a professional boredom that tells you he''s seen vehicles in similar or worse states a million times over.' 
			'He quickly but thoroughly inspects it, murmurring all the while to take mental notes on what needs to be done and, when he returns to you, simply states that it would cost <<normrem>> <b>₽</b> to repair the damage.'

			if money >= normrem:
				act 'Pay and wait while they repair the car':
					cla
					*clr
					money -= normrem
					minut += 60
					tehcar = tehcar / 2
					teh = tehcar
					wrek = 0
					'<center><img src="images/etogame/autoservis2.jpg"></center>'
					'Since he tells you that it''s not gonna take long - at first you thought he was being sarcastic but then realized that his no-nonsense attitude didn''t leave much room for humour - you go into a waiting or recreation room, with couches and a TV. After about an our of flipping through channels, you are called back to the garage and can hardly believe your eyes.'
					'The car looks brandnew! Not a single dent, not one scratch, and when you start the engine to make sure it''s working, it sounds better than ever before. You ask the foreman how he and his crew managed to repair the car, and that quickly, but his only response is a shrug.'
					'Before he sends you back into the office to pay the <<normrem>> <b>₽</b>, he asks you to not bring them cars in this pitiable state anymore - you''re not sure whether he was joking or not.'

					act 'Leave':gt 'Nord'
				end
			end
		end
	end

	if cardrive ! 7:'The foreman sighs. "Where is your car, girl? I need to look at your car to see if and what is wrong with it. Bring your car and I''ll examine it."'

	if wrek = 1 and cardrive ! 7 and car > 0:
		'The garage offers a towing service for damaged cars, to bring them to this service centers. The price: 2500 <b>₽</b>'

		if money >= 2500:
			act 'Pay for the towing service (2,500 <b>₽</b>) (1:00)':
				cla
				*clr
				money -= 2500
				cardrive = 7
				minut += 60
				'<center><img src="images/etogame/autoservis2.jpg"></center>'
				'You sit down and wait at the service station. It takes about an hour until your car arrives, but when it does, it is almost immediately moved into the garage and it looks like the foreman is about ready to inspect it.'

				act 'See what the foreman has to say':gt 'autoservisF', 'start'
			end
		end
	end

	act 'Leave':gt 'Nord'
end

--- autoservisF ---------------------------------

