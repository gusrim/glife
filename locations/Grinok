# Grinok
!{change log
March 31, 2016
Added rice wine quest
April 20, 2016
Fixed Rice wine event
}
$location_type = 'public_outdoors'
if $ARGS[0] = '':
	$metka = $ARGS[0]
	$loc = $CURLOC
	CLOSE ALL
	if sound = 0:
		if month >=11 and month <= 12 or month >=1 and month <=3:
			if hour >= 8 and hour <= 23: PLAY 'sound/rynok.mp3',30 & PLAY 'sound/zima.mp3',30
			if hour >= 0 and hour <= 7: PLAY 'sound/street_night.mp3',30 & PLAY 'sound/zima.mp3',30
		else
			if hour >= 8 and hour <= 23: PLAY 'sound/rynok.mp3',30 & PLAY 'sound/street_leto.mp3',30
			if hour >= 0 and hour <= 7:PLAY 'sound/street_night.mp3',30
		end
	end

	clr
	gs 'stat'
	'<center><h4>Market</h4></center>'
	'<center><img src="images/pic/grinok.jpg"></center>'
	'This small market is location near the station.'

	if arturQW = 1 and (week = 6 or week = 7) and hour < 12:
		act 'Wait for Arthur to arrive':hour = 12 & gt 'grinok'
	end

	if hour >= 8 and hour <= 18:
		'In the market the sellers of goods are beckoning shoppers.'
		if gorslut > 2 and rand(0,9) = 0:
			'An Armenian man comes up to you and in broken Russian says, "You girl who like men, no? Come meet real men have good time."'
			act 'Go with him':gt 'gevent', '3'
			act 'Reject him':
				'You pretend not to understand what he is saying.'
				act 'Return':gt 'Grinok'
			end
			
			exit
		end
		act 'Wander through the market': gt 'Grinok','events'
		!expand market events from 1.2.4
		
		if pornmarkonce = 1:
			act 'Go to the area with the porn':gt 'Gpornmark'
		end

		if arturKnow = 1 and arturQW = 0:
			act 'Go to Arthur':gt 'arturRinok', 'start'
		elseif arturQW = 1 and (week = 6 or week = 7):
			if hour = 12 or hour = 13:
				act 'Go to Arthur':gt 'arturRinok', 'work_1'
			end
		elseif arturQW = 2:
			if hour = 16 and minut <= 30:
				act 'Go to Arthur':gt 'arturRinok', 'work_2'
			elseif hour < 16:
				act 'Wait for Arthur to arrive':hour = 16 & minut = 0 & gt 'Grinok'
			end
		end

		act 'Go to the book seller':
			cls
			minut += 15
			gs 'stat'
			'On the shelves are very entertaining fantasy books, which costs only 150 <b>₽</b> each and there are also women''s romance paperback novels for 300 <b>₽</b> each.'

			if money >= 150:
				act 'Buy a fantasy book (150 <b>₽</b>)':
					cls
					minut += 15
					money -= 150
					f_book += 1
					gs 'stat'
					'You bought a fantasy book for 150 <b>₽</b>.'

					act 'Leave':gt $curloc
				end
			end

			if money >= 300:
				act 'Buy a romance novel (300 <b>₽</b>)':
					cls
					minut += 15
					money -= 300
					w_book += 1
					gs 'stat'
					'You bought a romance novel for 300 <b>₽</b>.'

					act 'Leave':gt $curloc
				end
			end

			act 'Leave':gt $curloc
		end

		
		if gobelen > 0 and gobQW = 0:
			act'Sell ​​tapestry':
				cls
				gobQW = 1
				minut += 15
				gs'stat'
				'You stood quietly by the entrance to the market, waiting for customers and displaying your tapestries. Shortly buyers started to gather, but a large, muscular man approached you and said, "It is illegal to trade in the market without a trader''s licence, but I may be interesting in buying your tapestries, 500 <b>₽</b> apiece. Come and see me at my stall."'
				'Not wanting to get into trouble, you packed your stuff away. The man was already gone, but you saw which direction he went and he should be easy to find.'
				act 'Leave':gt $curloc
			end
		end

		if gobelen > 0 and gobQW >= 1:
			act'Sell ​​tapestry':
				cls
				minut += 15
				gs'stat'
				'You went to the man who offered to buy your tapestries for 500 <b>₽</b>. You found him by his stall, smiling.'

				if gobQW = 2:
					'Roman welcomed you. Interested and businesslike, he asked what you have for sale.'
				end

				if gobQW = 1:
					gobQW = 2
					minute + 5
					'The man greeted you and introduced himself as Roman, you told him that your name is <<$name>>. After a brief chat Roman said that he is interested in seeing what you have for sale.'
				end

				act'Show tapestry':
					cls
					gobelen -= 1
					money += 500
					gs'stat'
					'You show your tapestry to Roman and he gives you five hundred for it. You take the money and give him the tapestry.'
					act 'Leave':gt $curloc
				end

				if gobelen > 1:
					act'Sell ​​all tapestries':
						cls
						kolgob = gobelen
						gobelen = 0
						moneyGob = kolgob*500
						money += moneyGob
						gs'stat'
						'You show Roman your tapestries and he gives you <<moneyGob>> <b>₽</b> for them. You take the money and give him the tapestries.'
						act 'Leave':gt $curloc
					end
				end

				act 'Leave':gt $curloc
			end
		end
		if ShowerIvan = 5 and porndeal = 0 and rand(1,100) <= 10 and lookwine ! daystart:
			cls
			cla
			gs 'stat'
			minut += 20
			lookwine = daystart
			'<center><img src="images/locations/pavlovsk/market/chinese_stall01.jpg"></center>'
			'You were looking through the market when you came upon a stall selling Chinese foods. On a shelf behind the old woman minding the stall there are a number of bottles.'
			act 'Ask about huangjiu':
				cla
				'You asked the old woman if she has any rice wine to sell. She smiles at you and points to a basket filled with rice.'
				'"Onry 2 kopec, hundred glam." she said in badly accented Russian, "You buy?"'
				'"No," you replied, "I need rice wine, rice wine."'
				'She looked at you in confusion for a moment then suddenly brightened, "Ah!" she exclaimed, "Come, come!"'
				'The old woman hurried to the other side of the stall and you followed her to where a number of smoked piglets hung in a row.'
				'"Here," the old woman crowed handing you one of r the pigs, "Nice swine, vely nice swine."'
				'"No, not nice swine," you cried in dispair, "Rice. Wine."'
				'"Yes, yes," the woman countered, "Nice swine, only 5 lubles."'
				'Just as you were about to give up, a young Chinese boy about your brother''s age arrived.'
				'"I am sorry, my grandmother does not speak much Russian." he said as he stepped up beside the old woman who started to speak rapidly to him in what you assumed was Chinese.'
				'"My grandmother asks why you don''t like the piglets." he translated as the old woman stopped speaking.'
				'"I don''t want a pig," you replied in relief as his Russian is quite good, "I am looking for rice wine, wang-goo."'
				'"Ah, huangjiu!" he said in understanding then spoke to his grandmother in Chinese.'
				'The old woman glared at you then hung the smoked pig back on its hook. She shuffled over to the shelf with the bottles and peered short-sightedly at the labels. She finally picked up one of them and brought it over to you.'
				'"Why you not say wanted lice wine." she asked in aggrieved tones, "2000 lubles."'
				if money >= 2000:
					act 'Buy the wine':
						cla
						money -= 2000
						ricewine = 1
						'You hand the woman the money and she gives you the wine.'
						act 'Leave': gt $curloc
					end
				else
					'You are astonished by the price and realize that you cannot afford the wine. You apologize and turn away.'
					act 'Leave the market': gt $curloc
					act 'Continue browsing the market':
						cls
						cla
						gs 'stat'
						minut += 15
						'<center><img src="images/locations/pavlovsk/market/market_boy01.jpg"></center>'
						'You continued to wander through the market for another 15 minutes when the boy from the Chinese stall came up to you.'
						'"Hey, you really want that rice wine?" he asked after checking to make sure no one was watching.'
						'"Yes," you answered him, "But it costs too much. I can''t afford it."'
						'"I''ll make a deal with you." he said, "Go to the porn dealer and buy me a porn magazine and I''ll get you the wine."'
						if pornmarkonce = 0: pornmarkonce = 1 & '"Where is the porn dealer?" you asked and he gives you directions.'
						act 'Agree': porndeal = 1 & gt 'Gpornmark'
						act 'Refuse':
							'You declined his offer and he shrugged and walked away.'
							act 'Leave': gt $curloc
						end
					end
				end
			end
		elseif ShowerIvan = 5 and lookwine ! daystart:
			cla
			cls
			gs 'stat'
			lookwine = daystart
			'<center><img src="images/pic/grinok.jpg"></center>'
			'You look around the market, but the Chinese vendors don''t seem to be here today.'
			act 'Leave': gt $curloc
		end

		if porndeal = 1 and porndealday ! daystart:
			cls 
			cla
			gs 'stat'
			porndealday = daystart
			lookboy = 0
			'<center><img src="images/locations/pavlovsk/market/market_boy01.jpg"></center>'
			'The boy from the Chinese stall comes up to you as you entered the market.'
			'"Do you have it?" he asked eagerly.'
			if haveporn = 1: 
				porndeal = 2 & ! to close the ricewine event
				ricewine = 1
				'"Do you have the wine?" you countered.'
				'He pulls the bottle out of his backpack and you trade items.'
				act 'Leave': gt $curloc
			else
				'You told him you didn''t have his magazine yet but you were working on it.'
				act 'Leave': gt $curloc
			end 
		end
		if porndeal = 1 and porndealday = daystart and lookboy = 0:
			cls
			cla
			gs 'stat'
			lookboy = 1
			'<center><img src="images/locations/pavlovsk/market/chinese_stall01.jpg"></center>'
			'You look around the market for the boy from the Chinese stall, but you can''t find him. You decide to look for him another day.'
			act 'Leave': gt $curloc
		end
	else
		'There is no one in the market.'
	end

	act 'Go back to the station':minut += 3 & gt 'vokzalG'
end

if $ARGS[0] = 'events':
	cls
	cla
	'<center><h4>Market</h4></center>'
	minut += 10
	gs 'stat'

	!modify from 1.2.4(force 50%) and old one, set it 30~80% for events, with intel /2  by illume
	if (30 + intel/2) > RAND(0,100):
		rinevrand = rand(1,21)
	else
		! no events
		rinevrand = 0
	end

	if rinevrand <= 1:
		$grinok_text[0]='You came across a beggar'
		$grinok_text[1]='You see a beggar asking alms'
		$grinok_text[2]='You see a beggar'
		$grinok_text[3]='disguised as a girl'
		$grinok_text[4]='what sort of a woman picks up her underwear'
		$grinok_text[5]='half-naked girl'
		$grinok_text[6]='girl trying on bra'
		$grinok_text[7]='Anything unusual you do not come across, all hlame, vegetables and tasteless clothing'
		$grinok_text[8]='You are not lucky and you continually run across old women in dressing gowns, Slippers and vests with pockets.'
		$grinok_text[9]='You don''t find anything interesting'
		$grinok_text[10]='You come across one Chinese consumer goods and nothing more'
		$grinok_text[11]='The market works in a regular mode. People with bags scurrying back and forth, children trick-or-treat toy from their parents and vociferous sellers.'
		$grinok_text[12]='The market as always a lot of people. Hustle and bustle makes you focus on your desires'	
		!will killvar after events
	end	
	
	! def action
	act 'Wander further': gt 'Grinok','events'
	act 'Stop wandering': gt $curloc
	
	if rinevrand = 0:
		'<center><img src="images/qwest/alter/grinok/brodit'+rand(1,8)+'.jpg"></center>'
		'You are strolling through the market. ' + $grinok_text[rand(3,12)]
		killvar '$grinok_text'
	elseif rinevrand = 1:
		'<center><img src="images/qwest/alter/grinok/poproshaika'+rand(1,3)+'.jpg"></center>'
		*pl $grinok_text[rand(0,2)]
		killvar '$grinok_text'
		act 'Throw a trifle': 
			cla
			*clr
			money -= rand(3,20)
			willpower += 5
			manna += 5
			gs 'stat'
			'<center><img src="images/qwest/alter/grinok/podaet.jpg"></center>'
			'You gave the beggar a trifle'
			act 'Wander further': gt 'Grinok','events'
		end
	elseif rinevrand = 2:
		'<center><img src="images/qwest/alter/grinok/dedgitara.jpg"></center>'
		'You see old man playing the guitar.'
	elseif rinevrand = 3:
		'<center><img src="images/qwest/alter/grinok/girlvoyeur.jpg"></center>'
		'You see a girl bending, putting her pussy on display.'
		if girl > 0: horny += 10
		gs 'stat'
	elseif rinevrand = 4:
		'<center><img src="images/qwest/alter/grinok/goodass.jpg"></center>'
		'You saw a pretty girl and ogled her beautiful ass.'
	elseif rinevrand = 5:
		'<center><img src="images/qwest/alter/grinok/dedgitara.jpg"></center>'
		'You see old man playing the guitar.'
	elseif rinevrand = 6:
		'<center><img src="images/qwest/alter/grinok/nenavizublyadcigan.jpg"></center>'
		'You met a Gypsy woman. She offers to tell fortunes for you.'
		act 'Agree':
			cla
			*clr
			minut += 5
			'<center><img src="images/qwest/alter/grinok/gadaet'+rand(1,2)+'.jpg"></center>'
			'You agreed and Gypsy taking your hand began to predict you great happiness and lots of money. After graduating, she promptly walked away.'
			act 'Look':
				cla
				*clr
				money = 0
				manna -= 50
				gs 'stat'
				'<center><img src="images/qwest/alter/grinok/pusto.jpg"></center>'
				'With a sneaking suspicion you start looking through your stuff. But your wallet is nowhere to be found.'
				act 'Hail a Gypsy':
					cla
					*clr
					'<center><img src="images/qwest/alter/grinok/okliknut.jpg"></center>'
					'You turned around hoping to see the thief, but she already dissolved into the crowd.'
					act 'Back': gt $curloc
				end
			end
		end
	elseif rinevrand = 7:
		'<center><img src="images/qwest/alter/grinok/kosmetic.jpg"></center>'
		tovpay = rand(3, 8) * 100
		'You see some cosmetics on sale for <<tovpay>> <b>₽</b>.'

		if money >= tovpay:
			act 'Buy':
				cls
				minut += 15
				money -= tovpay
				kosmetica += rand(10, 40)
				gs 'stat'
				gt $curloc
			end
		end
	elseif rinevrand = 8:
		'<center><img src="images/qwest/alter/grinok/vitaminki.jpg"></center>'
		tovpay = rand(2, 5) * 100
		'You see some vitamins on sale for <<tovpay>> <b>₽</b>.'

		if money >= tovpay:
			act 'Buy':
				cls
				minut += 15
				vitamin += 20
				money -= tovpay
				gt $curloc
			end
		end
	elseif rinevrand = 9:
		'<center><img src="images/qwest/alter/grinok/condoms.jpg"></center>'
		tovpay = rand(2, 8) * 10
		'You see a booth selling condoms for <<tovpay>> <b>₽</b>.'

		if money >= tovpay:
			act 'Buy':
				cls
				minut += 15
				prezik += 5
				money -= tovpay
				gt $curloc
			end
		end
	elseif rinevrand = 10:
		'<center><img src="images/qwest/alter/grinok/balzam.jpg"></center>'
		tovpay = rand(5, 10) * 100
		'There is lip balm on sale for <<tovpay>> <b>₽</b>.'

		if money >= tovpay:
			act 'Buy':
				cls
				minut += 15
				lipbalm += 20
				money -= tovpay
				gt $curloc
			end
		end
	elseif rinevrand = 11:
		if pornmarkonce = 0:pornmarkonce = 1
		'Strolling through the market you accidentally wander into the far corner where they sell porn.'

		act 'Go to the area with the porn':gt 'Gpornmark'
	elseif rinevrand = 12:
		if arturKnow = 0:
			'Someone suddenly called out to you, "Hey pretty girl, do you want a fresh, juicy tangerine?"'
			act 'Look': gt 'arturRinok','start'
		else
			'Someone suddenly called out to you, "Hey <<$nickname>>, do you want a fresh, juicy tangerine?"'
			act 'Come': gt 'arturRinok','start'
		end
	elseif rinevrand = 13:
		'<center><img src="images/qwest/alter/grinok/cheburek.jpg"></center>'
		'Walking through the market, you came across a grandmother selling pies.'
		if energy <= 80:
			act 'Buy a pie for 20 <b>₽</b>':
				cla
				*clr
				fat+=3
				frost = 0
				health += 5
				manna += 5
				water -= 5
				energy+=5
				money-=20
				minut+=10
				gs 'stat'
				'<center><img src="images/qwest/alter/grinok/eat.jpg"></center>'
				'You ate a delicious pie with pleasure.'
				act 'Wander further': gt 'Grinok','events'
			end
		else
			*p ', But you are not hungry right now.'
		end
	elseif rinevrand = 14:
		'<center><img src="images/qwest/alter/grinok/dropwallet.jpg"></center>'
		'You see woman ahead of you dropped her purse'
		cla
		act 'Tell her':
			cla
			*clr
			minut += 2
			'<center><img src="images/qwest/alter/grinok/vernutwallet.jpg"></center>'
			'You call the woman and show her the wallet. She looked up gratefully at you and raising his lost, walked away'
			act 'Wander further': gt 'Grinok','events'
		end
		act 'Take the purse':
			cla
			*clr
			minut += 5
			i = rand(40,200)
			'<center><img src="images/qwest/alter/grinok/zaglanut.jpg"></center>'
			'In the purse are <<i>> <b>₽</b>'
			act 'Take the money and throw the wallet':
				cla
				*clr
				money += i
				manna += 10
				gt $curloc
			end
			act 'Catch up with the woman and return her purse':
				cla
				*clr
				manna += 5
				'<center><img src="images/qwest/alter/grinok/proverka.jpg"></center>'
				'You caught up with the woman and telling her she dropped something you hand her the purse. Woman immediately began to count the money. Seeing that everything is in place, she thanked you and walked away'
				act 'Wander further': gt 'Grinok','events'
			end
		end
	elseif rinevrand = 15:
		'<center><img src="images/qwest/alter/grinok/rebenok.jpg"></center>'
		'You see a young mother and her crying baby.'
	elseif rinevrand = 16:
		'<center><img src="images/qwest/alter/grinok/rezba.jpg"></center>'
		'You find a tent with beautiful things carved out of wood and metal.'
	elseif rinevrand = 17:
		'<center><img src="images/qwest/alter/grinok/gruzchik.jpg"></center>'
		'Gazing around, he almost collided with a loader.'
	elseif rinevrand = 18:
		'<center><img src="images/qwest/alter/grinok/meatlavka.jpg"></center>'
		'You wandered into the meat Department and see the butcher working with an ax.'
	elseif rinevrand = 19:
		if vnesh >= 30:
			'<center><img src="images/qwest/alter/grinok/walk'+rand(1,2)+'.jpg"></center>'
			'You walk, defiantly wagging booty. Some guys stare at you, unable to avert their eyes.'
			manna += 5
		else
			'<center><img src="images/qwest/alter/grinok/walk'+rand(3,4)+'.jpg"></center>'
			'You walk, carefully twisting your waist. But people don''t give you slightest attention'
			manna -= 5
		end
		gs 'stat'
	elseif rinevrand = 20:
		'<center><img src="images/qwest/alter/grinok/dirka.jpg"></center>'
		'When you crawl series with things you notice a small hole in the tent'
		act 'Look':
			cla
			*clr
			if girl > 1: horny += 5
			'<center><img src="images/qwest/alter/grinok/pereodev'+rand(1,15)+'.jpg"></center>'
			'You look inside the tent and see ' + $grinokrandtext[rand(3,7)]
			act 'Leave': gt $curloc
		end
	else
		tovpay = rand(1, 5) * 1000
		'You see someone selling a handbag for <<tovpay>> <b>₽</b>.'

		if bag = 0:
			if money >= tovpay:
				act 'Buy':
					cls
					minut += 15
					bag = 1
					money -= tovpay
					if menu_option = 1:dynamic $menu_addoldmenu
					if menu_option = 0:gs 'obj_din', 'new'
					gt $curloc
				end
			end
		else
			'But you already have a bag, so you move on.'
		end
	end	
end

--- Grinok ---------------------------------