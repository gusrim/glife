# Gaddvor

if $ARGS[0]='':
	$metka = ''
	$loc = 'Gaddvor'
	$location_type = 'public_outdoors'
	clr
	gs 'stat'
	CLOSE ALL
	gs 'gadukino_event', 'sound'

	'<center><h4>Your grandparents'' yard</h4></center>'
	if month >= 4 and month <= 10:
		if hour >= 6 and hour < 22:
			'<center><img src="images/qwest/gadukino/dvor.jpg"></center>'
		else
			'<center><img src="images/qwest/gadukino/dvor_night.jpg"></center>'
		end
	else
		if hour >= 8 and hour < 18:
			'<center><img src="images/qwest/gadukino/dvor_winter.jpg"></center>'
		else
			'<center><img src="images/qwest/gadukino/dvor_winter_night.jpg"></center>'
		end
	end
	'A fenced yard behind your grandparents'' house. The yard is surrounded by a small barn packed with junk and a separate building that is used as a banya where you can take a bath. On the back porch there are a few chairs to sit in.'
	if hour >= 6 and hour < 20 and month >= 4 and month <= 10:'In the yard there are <a href="exec:gt ''Gaddvor'',''chickens''">chickens</a> running around.'

	if mira_guest = 1:'Your friend <a href="exec: gt ''Miroslava''">Mira</a> stands next to you.'


	act 'Enter your grandparents'' house':minut += 5 & gt 'Gadhouse'
	act 'Enter the barn':minut += 5 & gt 'Gadsarai'
	act 'Enter the banya':minut += 5 & gt 'Gadbana'
	act 'Enter garden':minut += 5 & gt'Gadgarden','garden'
	if meadow > 0 and hour >= 6 and hour < 20 and month >= 4 and month <= 10:act 'Go to the meadow':minut += 20 & gt 'Meadow', 'start'
	act '<b>Go to the village</b>':minut += 5 & gt 'gadukino'

	!removed from 1.2.5
	if hour >= 17 and hour < 20:
		if mitkaday!daystart:'<a href="exec:gt ''mitka''">Mitka</a> is hanging out near the barn in your grandparents'' backyard, he is following you while taking a smoke.'
	elseif hour >= 20:
		'Several <a href="exec:gt ''mitkabuh''">young men</a> are sitting near your grandparents'' backyard, drinking moonshine and talking loudly.'
	end
	!125s
	mira_temp = rand(1,5)
	if mira_temp = 3 and hour >= 9 and hour <= 19 and miraQW >= 15 and Mira_no = 0 and sunWeather = 1 and mira_guestday ! daystart and (mitkasextimes < 13 or miragopQW >= 10 or mirasex > 1):
		cla
		*clr
		mira_guestday = daystart
		minut += 5
		mira_guest = 1
		gs 'stat'
				
		dynamic $miraclothes
		'You went out into the yard and noticed Mira standing there.'
		'"Oh, <<$nickname>> hi, hope you don’’t mind me visiting. I was bored being home alone." she said smiling.'

		 act 'Further':gt'Gaddvor'
	end     
end

if $ARGS[0]='chickens':
	cls
	minut += 5
	gs 'stat'
	
    '<center><img src="images/qwest/gadukino/new/chickens1.jpg"></center>'
	'Ordinary chickens clucking about their pen.'
	!job Granny
	if feed_chickens = 1:
		act 'Feed the chickens':
			cla
			*clr
			minut += 30
			grandmahelp += 1
			feed_chickens = 0
			gs 'stat'
			
			'<center><img src="images/qwest/gadukino/new/chickens2.jpg"></center>'
			'You decide to feed the chickens. You pick up a bucket full of bird''s seed and grain mix and toss it on the ground. They immediately rush over and start to greedily eat.  You watch them for a bit before you go about your business.'
			
			act 'Finish':gt'Gaddvor'  
		end
	end
	act 'Go': gt'Gaddvor'
end
!!125e
	
--- Gaddvor ---------------------------------