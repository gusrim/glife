# shoes

!!This is the main file for shoes, like clothes but for your feet.

$shoe_list_line = {
	!! a single line in a shoe list (shoe rack, etc)
	!! ARGS 0 - action type (shoe rack, sell, store, forsale)
	!! ARGS 1 - shoe type
	!! ARGS 2 - shoe index

	if night_mode = 1:
		$bgcolor = iif($bgcolor='#0C0B11','#000000','#0C0B11')
	else
		$bgcolor = iif($bgcolor='#f3f4ee','#ffffff','#f3f4ee')
	end

	$RESULT = '<TR bgcolor='+$bgcolor+'>'

	if Enable_clothwidth > 0:
		shoe_temp = Enable_clothwidth - 1
	else
		shoe_temp = 75 & !shoe widdefault to 75
	end
	$RESULT +='<TD><a href="exec:gt ''shoes'', ''view_shoe_item'', ''<<$ARGS[0]>>'', ''<<$ARGS[1]>>'', <<ARGS[2]>>"><img src="<<FUNC(''$shoe_image'', $ARGS[1], ARGS[2])>>" width="<<shoe_temp>>"></a></TD>'
	killvar 'shoe_temp'
	gs 'shoe_attributes', $ARGS[1], ARGS[2]

	gs 'shoe_description2', $ARGS[2]
	$RESULT += '<TD><<FUNC(''$shoe_description'', $ARGS[1])>> <<ARGS[2]>></TD><TD><<$description>></TD>'

	if ShoStyle = 2:
		$RESULT += '<TD>Yes</TD>'
	else
		$RESULT += '<TD>No</TD>'
	end

	if ShoStyle = 1:
		$RESULT += '<TD>Yes</TD>'
	else
		$RESULT += '<TD>No</TD>'
	end
	
	$RESULT += '<TD><a href="exec:gt ''shoes'', ''view_shoe_item'', ''<<$ARGS[0]>>'', ''<<$ARGS[1]>>'', <<ARGS[2]>>">View</a></TD>'

	$RESULT += '<TD>'
	if sloc ! 1:$RESULT += ' <a href="exec:gt ''shoes'', ''dest1'', ''<<$ARGS[0]>>'', ''<<$ARGS[1]>>'', <<ARGS[2]>>">Storage</a><BR>'
	if sloc ! 0:$RESULT += ' <a href="exec:gt ''shoes'', ''dest0'', ''<<$ARGS[0]>>'', ''<<$ARGS[1]>>'', <<ARGS[2]>>">Wardrobe</a><BR>'
	if sloc ! 2:$RESULT += ' <a href="exec:gt ''shoes'', ''dest2'', ''<<$ARGS[0]>>'', ''<<$ARGS[1]>>'', <<ARGS[2]>>">Unwanted</a>'
	$RESULT += '</TD>'
}

if $ARGS[0] = 'dest0':
	*clr
	dynamic 's<<$ARGS[2]>>S[<<ARGS[3]>>] = 0'
	gt 'shoes', 'view_shoe_list', $ARGS[1]
end

if $ARGS[0] = 'dest1':
	*clr
	dynamic 's<<$ARGS[2]>>S[<<ARGS[3]>>] = 1'
	gt 'shoes', 'view_shoe_list', $ARGS[1]
end

if $ARGS[0] = 'dest2':
	*clr
	dynamic 's<<$ARGS[2]>>S[<<ARGS[3]>>] = 2'
	gt 'shoes', 'view_shoe_list', $ARGS[1]
end

if $ARGS[0] = 'view_shoe_list':
	!! ARGS 0 - view_shoe_list
	!! ARGS 1 - action type (wardrobe, storage, unwanted, sell)
	!! ARGS 2 - brothel flag (brothel)

	$regularwornshoetype = ''

	act 'Return':gt 'wardrobe', 'start'

	if $ARGS[1] ! 'sell' :'<center><table border=1><TH><a href="exec:gt ''shoes'', ''view_shoe_list'', ''wardrobe''">Wardrobe </a></TH><TH><a href="exec:gt ''shoes'', ''view_shoe_list'', ''store''">Storage</a></TH><TH><a href="exec:gt ''shoes'', ''view_shoe_list'', ''unwanted''">Unwanted</a></TH></table></center>'

	if $ARGS[1] ! 'sell' :
		'<center><b>Select shoes to wear</b></center>'
		if $shoeworntype ! 'none':
			act 'Remove shoes':
				gs 'shoes', 'strip'
				gt 'shoes', 'view_shoe_list', $ARGS[1]
			end
		end
	end
	
	if $ARGS[1] = 'wardrobe':
		sloc = 0
	end
	
	if $ARGS[1] = 'store':
		sloc = 1
	end
	
	if $ARGS[1] = 'unwanted':
		sloc = 2
	end

	$bgcolor='#f3f4ee'

	'<center><table border=0 cellspacing=0 cellpadding=5><TH></TH><TH>Shoe</TH><TH>Description</TH><TH>Bimbo</TH><TH>Stripper</TH><TH>Actions</TH>'

	i = 1
	:loopsboutique
		if sboutique[i] = 1 and sboutiqueS[i] = sloc:*P dyneval($shoe_list_line, $ARGS[1], 'boutique', i)
		i += 1
	if i <= 37:jump 'loopsboutique'

	i = 1
	:loopspussy
		if spussy[i] = 1 and spussyS[i] = sloc:*P dyneval($shoe_list_line, $ARGS[1], 'pussy', i)
		i += 1
	if i <= 38:jump 'loopspussy'

	i = 1
	:loopsdolls
		if sdolls[i] = 1 and sdollsS[i] = sloc:*P dyneval($shoe_list_line, $ARGS[1], 'dolls', i)
		i += 1
	if i <= 27:jump 'loopsdolls'

	i = 1
	:loopsfetish
		if sfetish[i] = 1 and sfetishS[i] = sloc:*P dyneval($shoe_list_line, $ARGS[1], 'fetish', i)
		i += 1
	if i <= 27:jump 'loopsfetish'

	i = 1
	:loopsgandm
		if sgandm[i] = 1 and sgandmS[i] = sloc:*P dyneval($shoe_list_line, $ARGS[1], 'gandm', i)
		i += 1
	if i <= 51:jump 'loopsgandm'

	i = 1
	:loopstrainers
		if strainers[i] = 1 and strainersS[i] = sloc:*P dyneval($shoe_list_line, $ARGS[1], 'trainers', i)
		i += 1
	if i <= 29:jump 'loopstrainers'

	'</table></center>' 
end

if $ARGS[0] = 'gym':
	if $regularwornshoetype = '':
		$regularwornshoetype = $shoeworntype
		regularwornshoenumber = shoewornnumber
	end

	'<center><img src="images/locations/city/citycenter/mall/sports.png"></center>'

	i = 1
	:loopstrainers2
		if strainers[i] = 1 and strainersS[i] = 0:'<a href="exec:gt ''shoes'', ''view_shoe_item'', ''wardrobe'', ''trainers'', <<i>>"><img src="images/pc/shoes/trainers/<<i>>.jpg" 	height="250" /></a>'
		i += 1
	if i <= 29:jump 'loopstrainers2'

	act 'Return': gt $loc, $metka

	if $shoeworntype ! $regularwornshoetype: gs 'shoes', 'gym2'
end

if $ARGS[0] = 'gym2':
	act 'Put your regular shoes back on':
		$shoeworntype = $regularwornshoetype
		shoewornnumber = regularwornshoenumber
		killvar '$regularwornshoetype'
		killvar 'regularwornshoenumber'
		gt 'shoes', 'gym'
	end
end

if $ARGS[0] = 'view_shoe_item':
	!! ARGS 0 - view_shoe_item
	!! ARGS 1 = action type (wardrobe, shop, sell)
	!! ARGS 2 - shoes type
	!! ARGS 3 - shoes index
	!! ARGS 4 - price for shop

	cla
	'<center><img src="<<FUNC(''$shoe_image'', $ARGS[2], ARGS[3])>>"></center>'
	gs 'shoe_attributes', $ARGS[2], ARGS[3]
	FUNC('$shoe_description', $ARGS[2]) + '<<ARGS[3]>>'
	*nl
	gs 'shoe_description2', $ARGS[2]

	'<<$description>>'
	*nl
	if ShoStyle = 1:'This shoe is considered stripper style and most suitable for stripping or whoring.'
	if ShoStyle = 2:'This shoe is considered bimbo style and makes your mind drift toward sexual thoughts.'
	if ShoStyle = 3:'This shoe is considered alternative style and makes you feel more assertive and aggressive.'

	if $ARGS[1] = 'shop':
		if dyneval('RESULT = s<<$ARGS[2]>>[<<ARGS[3]>>]') = 1:
			'You already own these shoes.'
			act 'Leave': gt $loc, $metka
		else
			price = ('<<ARGS[4]>>' * ((5 * ShoQuality) + 100) / 100) * 1000 / (1250 - Clothingstock[ARGS[3]]) * 3 / 2
			price = price / 50 * 50
			'Price: <<price>> <b>â‚½</b>'
			act 'Leave': gt $loc, $metka
			if money >= Price:
				act 'Buy':
					h = 0
					if ShoStyle = 2:h = 10
					if ShoStyle = 5:h = 20
					h += (3 * ShoQuality)
					money -= price
					dynamic 's<<$ARGS[2]>>[<<ARGS[3]>>] = 1'
					gt $loc, $metka
				end
			else
				'You cannot afford these shoes.'
			end
		end
	elseif $ARGS[1] = 'wardrobe':
		if $regularwornshoetype = '':
			act 'Leave in closet':gt 'shoes', 'view_shoe_list', $ARGS[1]
		else
			act 'Return': gt 'shoes', 'gym'
		end

		act 'Wear':
			gs 'shoes', 'wear', $ARGS[2], ARGS[3]

			if $regularwornshoetype = '':
				gt 'wardrobe', 'start'
			else
				gt 'shoes', 'gym'
			end
		end
	end
end

if $ARGS[0] = 'strip':
	if $shoesworntype = 'none': exit

	$shoelastworntype = $shoeworntype
	shoelastwornnumber = shoewornnumber
	$shoeworntype = 'none'
	shoewornnumber = 0
	ShoQuality = 0
	ShoHeels = 0
	ShoCut = 0
	ShoStyle = 0
	PShoQuality = 0
	PShoHeels = 0
	PShoCut = 0
	PShoStyle = 0
end

if $ARGS[0] = 'setup':
end
	
if $ARGS[0] = 'wear':
	!! ARGS 0 - action
	!! ARGS 1 - shoe type name
	!! ARGS 2 - shoe index

	if $ARGS[0] = 'last_worn':
		$shoeworntype = $shoelastworntype
		shoewornnumber = shoelastwornnumber
	else
		$shoeworntype = $ARGS[1]
		shoewornnumber = ARGS[2]
	end

	if $shoeworntype = 'none':
		PShoQuaility = 0
		PShoCut = 0
		PShoHeels = 0
		PShoStyle = 0
	exit
	end

	gs 'shoe_attributes', $ARGS[1], ARGS[2]

	dynamic '<<$ARGS[1]>>S[<<ARGS[2]>>] = 0'

	PShoQuality = ShoQuality
	PShoHeels = ShoHeels
	PShoCut = ShoCut
	PShoStyle = ShoStyle

!Calculations for shoes beauty, multiplied by natural beauty
	if PShoHeels > 0:
		if PShoHeels = 1:
			PXShoHeels = 25
		elseif PShoHeels = 2:
			PXShoHeels = 50
		elseif PShoHeels = 3:
			PXShoHeels = 100
		elseif PShoHeels = 4:
			PXShoHeels = 150
		elseif PShoHeels = 5:
			PXShoHeels = 200
		elseif PShoHeels = 6:
			PXShoHeels = 300
		elseif PShoHeels >= 7:
			PXShoHeels = 400
		end
	end
end

--- shoes ---------------------------------

