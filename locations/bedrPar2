# bedrPar2

if $ARGS[0] = '':
	CLOSE ALL
	act 'Return to the hallway':gt 'korrPar'

	$metkaM = $ARGS[0]
	$locM = $CURLOC
	$metka = $ARGS[0]
	$loc = $CURLOC
    $location_type = 'private'
	clr
	minut += 1
	gs 'stat'
	if night_mode = 1:
		fcolor = rgb(255, 255, 255)
		bcolor = rgb(0, 0, 0)
		lcolor = rgb(106, 90, 205)
	else
		fcolor = rgb(0, 0, 0)
		bcolor = rgb(255, 255, 255)
		lcolor = rgb(106, 90, 205)
	end
	'<center><h1><font color="maroon">Master bedroom</font></h1></center>'
	'<center><img <<$set_imgh>> src="images/locations/pavlovsk/resident/apartment/home/bedrpar2.jpg"></center>'
	*nl
	'The room in which your parents live. Their large bed, <a href="exec: gt ''bedrPar2'',''parents_wardrobe''">wardrobe</a> and other closets take up most of the space.'
	gs 'family'

	if hour > 6 and hour < 21:
        if kamasutra_day ! daystart:
            'On the bed you see a <a href="exec: gt ''bedrPar2'',''kamasutra''">book</a>.'
        end
        if prezikday ! daystart:
            if prezikProver >= 3:
                prezikday = daystart - 4
                act 'Take condoms from your parents'' stash':
                    cls
                    cla
                    gs 'stat'
                    minut+=rand(5,7)
                    prezik+=rand(1,3)
                    prezikday = daystart
                    prezikProver = 0
                    '<center><img <<$set_imgh>> src="images/shared/accessories/birthcontrol/condoms_steal.jpg"></center>'
                    'After you make sure you''re not seen, you quickly search your parents'' room for their condom stash and take a few. Now you have <<prezik>> pcs.'
                    act 'Exit the room':gt 'korrPar'
                end
            else
                'You have already taken some condoms out of your parents'' stash recently. You should wait a while, otherwise they might get suspicious.'
            end
        end
	end

	if hour = 21 and parsexrand = 5 and parentsexday ! daystart:gt 'seeparentsex'
end

! book
if $ARGS[0] = 'kamasutra':
	cla
	*clr
	minut += 1
	'<center><img <<$set_imgh>> src="images/locations/pavlovsk/resident/apartment/home/book.jpg"></center>'
	'The cover says "Kamasutra"'
	act 'Read': kamasutra_page = 0 & kamasutra_day = daystart & gt 'bedrPar2', 'read_book'	
	act 'Leave': gt $loc, $metka	
end

! read book
if $args = 'read_book':
	*clr & cla
	pcs_horny += 2
	minut += 3
	gs 'stat'
	'<center><img <<$set_imgh>> src="images/shared/accessories/books/kamasutra/ik' + kamasutra_page + '.jpg"></td></tr><tr><td align=center>'+iif(kamasutra_page < 1, '', '<a href="exec: kamasutra_page-=1 & gt''bedrPar2'',''read_book''"><br>Previous page</a>')+'</td><td align=center>'+iif(kamasutra_page > 45, '', '<a href="exec: kamasutra_page+=1 & gt''bedrPar2'',''read_book''"><br>Next page</a>')+'</center>'
	if pcs_horny >= 60 and week = 6 and family_trip = 1:
		act 'Masturbate': gt 'selfplay', 'start'
	end
	act 'Close': gt $loc, $metka
end

! parent''s wardrobe
if $ARGS[0] = 'parents_wardrobe':
	cla
	*clr
	'<center><img <<$set_imgh>> src="images/locations/pavlovsk/resident/apartment/home/wardrobe.jpg"></center>'
	'The old Soviet wardrobe'
	act 'Rummage': gt 'bedrPar2', 'wardrobe_search'
	act 'Leave ': gt $loc, $metka
end

if $ARGS[0] = 'wardrobe_search':
	cla
	*clr
	if (hour >= 21 or hour < 6) and indorf = 0:
		'What are you thinking? You can''t go through the wardrobe while your parents are sleeping right there!' 
		act 'Leave ': gt $loc, $metka
		exit
	end
	if rand(1,100) > 75:
		'<center><img <<$set_imgh>> src="images/locations/pavlovsk/resident/apartment/home/wardrobesearch'+rand(1,4)+'.jpg"></center>'
		$failwords[0]='You cannot find anything interesting'
		$failwords[1]='You find some clothe and underwear'
		$failwords[2]='You digging through mother''s closet, but find nothing except underwear.'
		*pl $failwords[rand(0,2)]
		killvar '$failwords'
	else 
		'<center><img <<$set_imgh>> src="images/locations/pavlovsk/resident/apartment/home/wardrobetoy.jpg"></center>'
		$findmomtoy[0]='Digging in the closet you find a <a href="exec: gt ''bedrPar2'',''momtoy_play''">Dildo</a>'
		$findmomtoy[1]='In mother''s lingerie is a small <a href="exec: gt ''bedrPar2'',''momtoy_play''">Dildo</a>'
		$findmomtoy[2]='You find your mother''s <a href="exec: gt ''bedrPar2'',''momtoy_play''">toy</a>'
		*pl $findmomtoy[rand(0,2)]
		killvar '$findmomtoy'
	end
	act 'Close the Cabinet': gt $loc, $metka
end

if $ARGS[0] = 'momtoy_play':
	cla
	*clr
	dildo = 1
	dildohand = 10
	selfmomtoyplay = 1
	'<center><img <<$set_imgh>> src="images/shared/sex/mast/start.jpg"></center>'
	if pcs_horny >= 70: 
		act 'Play with it': gt 'selfplay', 'start'
	else
		act 'Put it back': gt $loc, $metka
	end
end

--- bedrPar2 ---------------------------------

