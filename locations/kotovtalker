# kotovtalker
!!!!!!!!!!!!!!!!!
!!!Vitek Cats!!!
!!!!!!!!!!!!!!!!!
if numnpc = 9 and pav_slut < 25 and kotovLoveQW >= 0 and (npc_rel[$static_num] >= 80 or hotcat >= 5):
	if $loc = 'gdkin':
		if alko < 2:
			'<<$npc_firstname[$static_num]>> "<<$pcs_nickname>> Hey, let''s get drunk and then do something dumb."'

			act 'Drink beer':
				cls
				minut += 5
				npc_rel[$static_num] += 5
				alko += 2
				cumspclnt = 2
				gs 'cum_cleanup'
				pcs_breath = 0
				gs 'stat'
				'Vitek poured you a beer and you began drinking with him. Vitek stared into your eyes and tells you. "You''re a cool girl, you know that."'

				act 'Move away':gt $loc, $metka
			end

			if NikoPayback = 2 and kotovloveqw > 0 and VitekVsNiko = 0:act 'Tell him about Niko':gt 'NikoPayback', 'Vitek Help 2'

			act 'Refuse':npc_rel[$static_num] -= 5 & gt $loc, $metka
		elseif alko >= 2:
			'<<$npc_firstname[$static_num]>> "<<$pcs_nickname>> Hey, let''s dance."'

			act 'Refuse':npc_rel[$static_num] -= 5 & gt $loc, $metka

			act 'Dance':
				cls
				minut += 10
				npc_rel[$static_num] += 2
				danc_exp += rand(1, 3)
				pcs_horny += 10
				kotovQW += 1
				gs 'stat'
				'You go to the dance floor and started dancing with Vitek, he pawed at your ass with his strong hands.'

				if kotovLoveQW = 0:
					if kotovQW < 5 or npc_rel[$static_num] = 0:
						act 'Move away':gt $loc, $metka
					elseif kotovQW >= 5 and npc_rel[$static_num] ! 0:
						'The dance ended and Vitek took you by the hand, steering you to the wall. "Hey <<$pcs_nickname>>, wanna go out with me, be my chick? I''ll wring any guy''s neck for you."'

						act 'Refuse':
							cls
							npc_rel[$static_num] = 0
							gs 'stat'
							'<center><b><font color="maroon"><<$npc_firstname[$static_num]>> <<$npc_lastname[$static_num]>></font></b></center>'
							'<center><img <<$set_imgh>> src="images/characters/pavlovsk/school/head/<<numnpc>>.jpg"></center>'
							'You refused and Vitek looks completely confused, but he was immediately angry. "Well, then fuck you, you cock sucking whore!"'

							act 'Move away':gt $loc, $metka
						end

						act 'Agree':
							cls
							kotovLoveQW = 1
							gs 'stat'
							'<center><b><font color="maroon"><<$npc_firstname[$static_num]>> <<$npc_lastname[$static_num]>></font></b></center>'
							'<center><img <<$set_imgh>> src="images/characters/pavlovsk/school/head/<<numnpc>>.jpg"></center>'
							'Vitek was delighted "Sweet. I almost can''t believe that you agreed to be my chick."'

							act 'Move away':gt $loc, $metka
						end
					end
				elseif kotovLoveQW > 0:
					'The dance ends and Vitek hugged you "<<$pcs_nickname>>, lets go to my house."'

					act 'Move away':gt $loc, $metka

					act 'Go with Vitek.':
						cls
						minut += 15
						gs 'stat'
						'<center><img <<$set_imgh>> src="images/characters/pavlovsk/school/head/9.jpg"></center>'
						'You left the disco and went in the direction Vitek''s house, while he was holding your arm. Soon Vitek and you arrived at the dilapidated house, he opened the door and led you into the poor, unkept apartment. In the room clothes where lying in crumpled heaps everywhere on the floor and empty bottles littered the whole place.'

						act 'Continue':gt 'kotovSex'
					end
				end
			end
		end
	else
		'<<$npc_firstname[$static_num]>> "Hey <<$pcs_nickname>>, I''m busy. I''ll catch up with you later." He says as he turns back to talk to his friends.'
		act 'Continue': gt 'gschool_lessons', 'short_break'
	end
elseif kotovLoveQW > 0 and pav_slut >= 25:
	'Vitek scowled and tells you "Well <<$pcs_nickname>>, whats this garbage I hear about what you and <<$gnikname>> did? The whole town already knows, the guys have been telling me all about it. You fucking bitch! "'

	act 'Accuse him of sleeping around':
		cls
		npc_rel[9] = 0
		kotovLoveQW = -1
		gs 'stat'
		'You started yelling at Vitek that he has fucked other girls. Vitek started yelling at you and suddenly, there is a ringing in your head, your vision is blurry. You find you''re on the floor, with Vitek standing over you. Your cheek burns with pain, it takes you a few moments to understand what happened, it seems he slapped you, so hard it knocked you off your feet. "Stay away from me you fucking whore, it''s over between us!"'

		if $loc = 'gschool_lunch':gt 'gschool_lessons', 'short_break'
		if $loc = 'gdkin':act 'Move away':gt $loc, $metka
	end

	if pcs_dom >= 20:
		act 'Argue that it''s not true':
			cls
			sub += 20
			if pav_sex > 25: pav_sex = 25
			if pav_prostitute > 25: pav_prostitute = 25
			gs 'stat'
			'It takes a bit but you convince Vitek that all this was just gossip and none of it was true. Vitek frowned, "I''ll knock their teeth out if they whisper one more word about you then. Don''t worry, baby I will shut their yapping mouths."'

			if $loc = 'gschool_lunch':gt 'gschool_lessons', 'short_break'
			if $loc = 'gdkin':act 'Move away':gt $loc, $metka
		end
	end
elseif $loc = 'gdkin':
	if pav_slut < 50:
		'<<$npc_firstname[$static_num]>> "<<$pcs_nickname>> Hey, let''s get drunk and then do something dumb."'

		act 'Drink beer':
			cls
			minut += 5
			npc_rel[$static_num] += 5
			alko += 1
			cumspclnt = 2
			gs 'cum_cleanup'
			pcs_breath = 0
			gs 'stat'
			'You drink the beer with his group of friends.'

			act 'Move away':gt $loc, $metka
		end

		act 'Refuse':npc_rel[$static_num] -= 5 & gt $loc, $metka
	elseif pav_slut >= 50:
		'<<$npc_firstname[$static_num]>> "Then what the fuck are you doing here cumwhore? Came to suck some dick?"'

		act 'Leave':npc_rel[$static_num] -= 5 & gt $loc, $metka
	end
end

--- kotovtalker ---------------------------------

