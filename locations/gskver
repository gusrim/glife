# gskver
CLOSE ALL
if sound = 0:
	if month >=11 and month <= 12 or month >=1 and month <=3:
		if hour >= 8 and hour <= 23: PLAY 'sound/street2.mp3',30 & PLAY 'sound/zima.mp3',30
		if hour >= 0 and hour <= 7: PLAY 'sound/street_night.mp3',30 & PLAY 'sound/zima.mp3',30
	else
		if hour >= 8 and hour <= 23: PLAY 'sound/street2.mp3',30 & PLAY 'sound/street_leto.mp3',30
		if hour >= 0 and hour <= 7:PLAY 'sound/street_night.mp3',30
	end
end
$metka = $ARGS[0]
$loc = $CURLOC
cls
gs 'stat'
gs 'outdoors'

if $clothingworntype = 'nude':willpower -= 15 & manna -= 15 & 'You notice passers-by in the park pointing at you as you walk, and overhear one older lady telling her friend: "Look at her... it''s that <<$surname>> girl. Youth these days, they have no shame at all!"'

if month=3 or month=4 or month=5:
	if hour>=6 and hour<=17:
		'<center><img src="images/nigma/gor/lake/skver.spring.jpg"></center>'
		'The main square of Pavlovsk Park. Spring has come, melting the worst of the snow. The trees are sprouting new leaves.'
		'There are puddles on the ground everywhere, all remains from the melted snow. A lot of people in the park are having fun with them, trying to jump over them and mocking those that fail. They''re not particularly deep though, and plenty of others just walk through without giving them a second thought.'
		'The park is not particularly crowded, this time of year. Many people think it''s still too cold to spend much time outdoors.'
		''
	else
		'<center><img src="images/nigma/gor/lake/skver.spring.night.jpg"></center>'
		'The main square of Pavlovsk Park. Spring has come, melting the worst of the snow. The trees are sprouting new leaves.'
		'There is no particular reason to visit the park during the night, and as a result it''s largely deserted. In the distance you see one person running through the park, probably using it as a shortcut to go home faster.'
		''
	end
end
if month=6 or month=7 or month=8:
	if hour>=6 and hour<=19:
		'<center><img src="images/gevent/skver.jpg"></center>'
		'The main square of Pavlovsk Park. At this time of year, it''s one of the primary places in town that locals and tourists enjoy spending their spare time in. It''s always busy: people are resting, walking, playing and dating to their heart''s desires, enjoying the nice surroundings.'
		''
	else
		'<center><img src="images/nigma/gor/lake/skver.summer.night.jpg"></center>'
		'The main square of Pavlovsk Park. At this time of year, it''s one of the places where locals and tourists enjoy going in their spare time.'
		'However, there is no particular reason to visit the park during the night, and as a result it''s largely deserted. In the distance you see one person running through the park, probably using it as a shortcut to go home faster.'
		''
	end
end
if month=9 or month=10 or month=11:
	if hour>=6 and hour<=18:
		'<center><img src="images/nigma/gor/lake/skver.autumn.jpg"></center>'
		'The main square of Pavlovsk Park. Autumn has come, and some of the trees are already shedding their leaves. The leaves that are still on the trees have changed colour, and are a beautiful mix of red and brown.'
		'Everywhere in the park, people are enjoying nature. Some are taking photos of the fallen leaves, kids are playing everywhere, and couples are walking hand-in-hand, simply enjoying each other''s company.'
		''
	else
		'<center><img src="images/nigma/gor/lake/skver.autumn.night.jpg"></center>'
		'The main square of Pavlovsk Park. Autumn has come, and some of the trees are already shedding their leaves.'
		'It gets quite cold at night, and you try to stay active to keep yourself warm. The lone soul you see running across the park is probably trying to get home faster, to warm up with some nice cocoa.'
		''
	end
end
if month=1 or month=2 or month=12:
	if hour>=7 and hour<=16:
		'<center><img src="images/nigma/gor/lake/skver.winter.jpg"></center>'
		'The main square of Pavlovsk Park. It''s winter, and the park is quite busy with locals and tourists alike.'
		'There''s snow everywhere, and kids from the whole town have gathered here to make snowmen and enjoy snowball fights. Some of the adults are joining them, happy to play like kids again for just a moment.'
		''
	else
		'<center><img src="images/nigma/gor/lake/skver.winter.night.jpg"></center>'
		'The main square of Pavlovsk Park. It''s winter, and the nights in Pavlovsk are very cold.'
		'As you expected, the park is mostly deserted at this time of day. You occasionally see a wild animal, scrounging for food.'
		''
	end
end

*nl

if hour > 8 and hour < 19:'There''s a small <a href="exec:minut += 5 & gt ''gskino''">movie theater</a> in the park. It only has a single screen.'

if GvstreBoy = 1:'You see <a href="exec:numnpc = SkverBoy & gt ''Gnpc''"><<$nameBoyfrend[SkverBoy]>></a> walking across the main square of Pavlovsk Park. He doesn''t look like he''s going anywhere in particular, and seems to just be enjoying his walk.'
!!if kotovLoveQW > 0 and hour >= 18 and hour < 20 and week < 6 or week >= 6 and kotovLoveQW > 0 and hour >= 9 and hour < 20:'You see <a href="exec:minut += 3 & gt ''kotovEv''">Vitek Kotov</a> sitting on a bench in the park, drinking beer by himself. Judging from the empty bottles near the bench, he''s already had a few.'
if StoryLine = 1 and SchoolAtestat = 0:
	if hour >= 18 and hour < 20 and week < 6 or week >= 6 and hour >= 9 and hour < 20:
		'You see your classmates <a href="exec:minut += 3 & gt ''gopskver''">Vitek, Dan and Vasily</a> sitting on a park bench, drinking beer.'
	end
end

if rexPark = 1 and rexParkDay = daystart and hour >= 16 and hour < 18:
	'You see Rex walking in the park.'
	act 'Go and see Rex':gt 'reks', 'skver'
end

act 'Sit down on the bench': ParkBench = 0 & gt'placer_sex','sleeping_parc_alco'

act 'Go for a walk (0:15)':
	cla
	*clr
	minut += 15
	place = 1
	Frend_num = 0
		if MagicDostup ! 1:
			if rand (1, 4) <= magik and (hour <=6 or hour >=19):
				gt 'MagEncounterFairy'
			end
		end
	gt 'placer'
end

if hour >= 8 and hour < 22:
	act 'Distribute flyers to earn money (50 <b>₽</b>) (1:00)':
		cla
		*clr
		minut += 60
		money += 50
		manna -= 5
		gs 'stat'
		gs 'outdoors'
		'<center><img src="images/etogame/flaer.jpg"></center>'
		'You spend an hour handing out flyers to park visitors, advertising something or another. You get paid 50 <b>₽</b> for your efforts.'

		act 'Continue':gt 'gskver'
	end
end

if $clothingworntype = 'exercise' and hour > 5 and hour < 22 and daybegskver ! daystart:
	act 'Go for a run in the park (1:00)':
		cls
		minut += 60
		if salo > 3:salo -= rand(1, 3)
		fat -= rand(2, 7)
		beg += rand(0, 2)
		sweat += 2
		daybegskver = daystart
		gs 'stat'
		'<center><img src="images/etogame/parkbeg.jpg"></center>'
		'You prepare yourself for a nice run in the beautiful park of Pavlovsk, and pace yourself so you last for a full hour. You feel great after your run.'

		act 'Continue':place = 1 & Frend_num = 0 & gt 'placer'
	end
end

act 'Go to the residential area':minut += 5 & gt 'pavResidential'
act 'Go to the high street':minut += 5 & gt 'pavCommercial'

--- gskver ---------------------------------

