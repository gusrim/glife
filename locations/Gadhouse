# Gadhouse

$metkaM = ''
$locM = 'Gadhouse'
$metka = ''
$loc = 'Gadhouse'

frost = 0
CLOSE ALL

$grandma = {
	cls
	minut += 2
	if grandmatalkday ! daystart:grandmatalk = 0
	gs 'stat'
	
	'<center><img src="images/qwest/gadukino/grandma.jpg"></center>'
	'Your grandma is ancient, and almost never leaves her house.  Most of the time she just sits on her bed watching TV, or is slowly cleaning her house.'
	
	if hour >= grandmatalk + 3:
		act 'Chat with your grandmother':
			cla
			*clr
			grandmatalk = hour
			grandmatalkday = daystart
			minut += 10
			manna += mannamax/RAND(10,15)
			gs 'stat'
			
			'<center><img src="images/qwest/gadukino/grandma.jpg"></center>'
			'You spend some time talking with your grandmother. Sometimes, she''s difficult to understand.  Her nearly toothless mouth makes the pronunciation of words difficult for her. Most of what she says boils down to the same thing, life is much more fun when you''re young!'
			
			act 'Continue':gt 'Gadhouse'
		end
	end
	if grandmastory ! daystart:
		act 'Ask her to tell you a story':
			cla
			*clr
			grandmastory = daystart
			minut += 20
			manna += mannamax/RAND(5,10)
			temp = RAND(0,3)
			gs 'stat'
			
			'<center><img src="images/qwest/gadukino/babushka.jpg"></center>'
			'When you ask your grandmother to tell you a story, she mumbles a little.  Nevertheless, after a brief second or two, she decides on a story to share with you today, slowly she begins to talk.'
			
			if temp = 0:
				'"One time, my parents allowed me to stay at my friend''s home for the night. During the night, a lot of snow fell... and we heard a soft knock on the wall!" she says in an ominous voice.'
				'"We listened more closely, but heard only the wind whistling through the trees... so we tried to go back to sleep. Then we heard the knocking again!"'
				'"We both ran to the window, but couldn''t see anything other than snow. The knocking became more insistent, and the voice of a small girl begged us to open the door! My friend and I moved to the middle of the room, tightly hugging one another... we were terrified!"'
				'"The knocking continued into the morning, even when the blizzard stopped. It sounded like someone was panicking, but when we told her parents in the morning, they didn''t believe us. When we went outside... there was no tracks of the snow! As if it never happened at all!"'
				'"We never heard the voice or the knocking ever again after that, but I still feel bad for not going downstairs to answer the door. What if some girl was actually there, begging for our help? I hope she''s okay..."'
			elseif temp = 1:
				'"I had a friend back when I was younger.", she says, in an ominous voice. "Her husband died at her house, he was a drunken batard. He got drunk one day and died of alcohol poisoning in her very house."'
				'"One day she came home from work, and the floor boards were creaking! And through the glass panes in the door in the hallway, she could see the silhouette of a man. It looked remarkably like her late husband, she said!"'
				'"She was terrified! She quickly left her house, and only went back when I promised to go with her... but of course, we found nothing. Do you believe in ghosts, <<$nickname>>? Because ever since that moment, I do..."'
			elseif temp = 2:
				'"I wasn''t born in Gadyukino, I grew up in a different village", your grandmother begins. "This is a true story, by the way!"'
				'"When I was still a little girl, one of our neighbors died. Then, less than two weeks later, one of his brothers died too! And as the legend goes, death comes in threes..."'
				'"His one surviving sister was terrified. She was a friend of mine, but her parents wouldn''t let her leave the house."'
				'"It was well into the winter season then, and people could only go to other towns by sleigh. For forty days, they waited... just when they were about to accept that the legend might not be true, they found one of her relatives. Dead. Muzhik I believe his name was, he was found frozen solid, basically a block of ice! He''d tried to attend his cousin''s funeral, but it cost him his life!"'
				'"Death is not to be trifled with, <<$nickname>>... her parents were smart to hide her, otherwise she might''ve died instead!"'
			elseif temp = 3:
				'"This really happened!" your grandmother reassures you. "When I was young, basically your age... it was just after the war. No one believed in superstition or anything like that. They were tough times, all anyone cared about is how to get their next meal on the table for their family."'
				'"We all went to bed, but our cat kept meowing at me! It just wouldn''t let me sleep, for hours and hours!"'
				'"When I finally caught some sleep and woke up the next morning, my father was next to my bed. He said when he cleaned out the furnace of ashes, he found the remains of our dead cat in there!"'
				'You see her eyes tear up. "No one knew how she got there... no one noticed anything! Except for me. To this day I believe she was trying to warn me, asking for my help..."'
				'Your grandmother shrugs, wiping a tear from her eye. You put your hand on her knee reassuringly, but don''t really know what to say to comfort her.'
			end
			
			act 'You thank your grandmother for the story.':gt 'Gadhouse'
		end
	
	end

	act 'Leave her alone':gt 'Gadhouse'
}

$grandpa = {
	cls
	minut += 2
	if grandpatalkday ! daystart:grandpatalk = 0
	gs 'stat'
	
	'<center><img src="images/qwest/gadukino/grandpa.jpg"></center>'
	'Your grandfather is grumpy old man, who is always complaining about the village elders and how they have allowed the village to become run down and barely habitable.  He can walk, but requires a cane.  He doesn''t really leave the house any more, unless he really has to.'
	
	if hour >= grandpatalk + 3:
		act 'Chat with your grandfather':
			cla
			*clr
			grandpatalk = hour
			grandpatalkday = daystart
			minut += 10
			manna += mannamax/RAND(10,15)
			gs 'stat'
			
			'<center><img src="images/qwest/gadukino/grandpa.jpg"></center>'
			'Your grandfather stop compaining and smiles at you, he loves telling stories.  He has lived a rich and eventful life, and you enjoy listening to his stories and talking to him. He mostly reminisces about his past, and reflects on his earlier life.'
			
			act 'Continue':gt 'Gadhouse'
		end
	end

	act 'Leave him alone':gt 'Gadhouse'
}

$villagecat = {
	cls
	minut += 2
	gs 'stat'

	'<center><img src="images/qwest/gadukino/villcat.jpg"></center>'
	'Your grandparents'' fat tomcat, Boniface, is aware of you approaching him, even though his eyes are closed.  He tracks you with his ears and simply ignores you.'
	
	act 'Pet Boniface':
		cla
		*clr
		minut += 5
		manna += mannamax/RAND(30,50)
		gs 'stat'
		
		'<center><img src="images/qwest/gadukino/villcat1.jpg"></center>'
		'You gently pet Boniface, giving him a good scratch behind the ears.  You hear him start to purr.  As soon as you stop scratching him, the purring stops too. That lazy bastard... he''s living the easy life!'
		
		act 'Continue':minut += 2 & gt 'Gadhouse'
	end
	
	act 'Leave Boniface alone':minut += 2 & gt 'Gadhouse'
}

'<center><H4>Your grandparents'' cottage</H4></center>'
'<center><img src="images/qwest/gadukino/izba.jpg"></center>'
'A fairly simple home in the village of Gadyukino, but your grandparents call it home.  Inside the main room is a  huge wardrobe (where you can <a href="exec:gt ''clothing'', ''view_clothing_list'', ''wardrobe''">organize your clothes</a> or <a href="exec:gt ''clothing_QV'', ''list''"> pick something to wear</a>) and a <a href="exec:gad = 1 & gt ''bed2''">bed</a> where you can sleep. It''s more comfortable than it looks.'
'In the living room they have an old black and white <a href="exec:gt ''tvPar''">television</a>. It''s positioned next to a body length <a href="exec:gt ''mirror'', ''start''">mirror</a>.'
if catday = daystart:
	'On the table by the window <a href="exec: dynamic $villagecat ">Boniface</a> lies lazily, soaking in the heat from the rays of sunshine coming through the window.'
else
	if hour < 8:
		temp = RAND(0,9)
		if temp = 0:catday = daystart & 'On the table by the window <a href="exec: dynamic $villagecat ">Boniface</a> lies lazily, soaking up some rays of sunshine.'
	elseif hour >= 8 and hour < 12:
		temp = RAND(0,2)
		if temp = 1:catday = daystart & 'On the table by the window <a href="exec: dynamic $villagecat ">Boniface</a> is busy cleaning himself with his tongue.'
	elseif hour >= 12 and hour < 18:
		temp = RAND(0,5)
		if temp = 3:catday = daystart & 'Next to the table by the window <a href="exec: dynamic $villagecat ">Boniface</a> stretches and meows loudly, it''s time for dinner.'
	elseif hour >= 18 and hour < 22:
		temp = RAND(0,3)
		if temp = 0:catday = daystart & '<a href="exec: dynamic $villagecat ">Boniface</a> is stalking around the main floor, looking for a nice juicy mouse to play with.'
	elseif hour >= 22:
		temp = RAND(0,2)
		if temp = 1:catday = daystart & 'On the table by the window <a href="exec: dynamic $villagecat ">Boniface</a> lies lazily, soaking up some rays of sunshine.'
	end	
end

if hour >= 6 and hour < 22:
	'Your <a href="exec: dynamic $grandma ">grandmother</a> is sitting on her bed. She looks a bit confused, as if she''s not sure where to go.'
	'Your <a href="exec: dynamic $grandpa ">grandpa</a> slowly shuffles across the room, using his cane for support.'
else
	'Your grandparents sleep in separate beds.  But talk lovely to each other continously.'
end

clr
gs 'stat'
fcolor = rgb(0, 0, 0)
bcolor = rgb(255, 255, 255)
lcolor = rgb(106, 90, 205)

act 'Go outside':
	if $clothingworntype ! 'nude':
		minut += 5 & gt 'Gaddvor'
	else
		msg'<b><font color = red>You need to get dressed first.</font></b>'
		gt $curloc
	end
end

act 'Eat a full meal (0:30)':gs 'food', 'm_meal'

act 'Eat a light meal (0:20)':gs 'food', 's_meal'

act 'Have a snack (0:15)':gs 'food', 'snack'

act 'Have a cup of tea (0:05)':
	cls
	minut += 5
	if water >= 100:
		'You don''t need any more tea.'
	else
		water += 80
		cumspclnt = 2
		gs 'cum_cleanup'
		fbreath = 0
		'You have a cup of tea, quenching your thirst.'
	end

	gs 'stat'

	act 'Get up from the table':gt 'Gadhouse'
end

if pranik > 0:
	'You brought your <<pranik>> cookies with you.'

	act 'Drink tea with cookies (0:05)':
		cla
		*clr
		minut += 5
		pranik -= 1
		health += 30
		manna += 100
		energy += 20
		water += 20
		cumspclnt = 2
		gs 'cum_cleanup'
		fbreath = 0
		fat += 4
		frost = 0
		gs 'stat'
		'<center><img src="images/pics/food.jpg"></center>'
		'You enjoy a cup of tea while you eat some cookies. You know they''re too sweet and can''t be good for your figure, but they taste so good!'

		act 'Get up from the table':gt 'Gadhouse'
	end
end

if edaD > 0:
	act 'Eat diet food (0:30)':
		cla
		*clr
		frost = 0
		minut += 30
		health += 10
		manna += 20
		edaD -= 1
		'<center><img src="images/pics/food.jpg"></center>'
		if energy >= 100:
			'You prepare a diet meal and try to eat it, but you''re really not hungry at all. You can''t finish the meal.'
		elseif energy >= 80:
			energy += 40
			'You try to enjoy the diet meal, but half-way in you realize you weren''t all that hungry yet. You manage to finish the meal, but feel like it was kind of a waste to prepare it.'
		else
			energy += 80
			'The diet meal is tasty, and you enjoy eating it.'
		end

		if water >= 80:
			'You don''t need any more tea.'
		else
			water = 100
			cumspclnt = 2
			gs 'cum_cleanup'
			fbreath = 0
			'You have a cup of tea, quenching your thirst.'
		end

		act 'Get up from the table':gt 'Gadhouse'
	end
end

dynamic $fatdel

if lekarstvo > 0:
	'You have <<lekarstvo>> tablet(s).'

	if sick > 0 and lekarday ! daystart:
		act 'Take a pill (0:05)':
			cla
			*clr
			minut += 5
			lekarday = daystart
			lekarstvo -= 1
			sick -= sick * 20 / 100
			if water < 50 : water += 30 else water +=15
			if water > 100: water = 100

			'<center><img src="images/inbed/lekr<<picrand>>.jpg"></center>'
			'You swallow the pill, and shortly after you feel a bit better.'

			act 'Continue':gt 'Gadhouse'
		end
	end
end

if vitamin > 0:
	'You have <<vitamin>> vitamin pills.'

	if vitaminday ! daystart:
		act 'Take vitamins (0:05)':
			cla
			*clr

			if fat > salo:fat = salo

			minut += 5
			vitaminday = daystart
			vitamin -= 1
			frost = 0
			if water < 50 : water += 30 else water +=15
			if water > 100: water = 100

			if KandidozOnce = 1:Kandidoz -= 2
			if GerpesOnce = 1:Gerpes -= 2
			if SifacOnce = 1 and Sifilis >= 10:Sifilis -= rand(0, 1)

			skinvan += 1

			'You take some vitamins to stay healthy.'

			act 'Continue':gt 'Gadhouse'
		end
	end
end

gs 'events', 'read'

if indorf = 1:
	'Your parents are relaxing.'

	if week = 7:
		if hour >= 20:gt 'farmhomeride'
	end
end

if monthbabkapay ! month:
	monthbabkapay = month
	money += 2000
	gs 'stat'
	'Out of nowhere, your grandmother gives you 2,000 <b>â‚½</b>. Wow!'
end

if rand (0,2) ! 0:gs 'din_bad' & dynamic $mobile_check

--- Gadhouse ---------------------------------

