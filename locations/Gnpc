# Gnpc
!!Guys
GvstreBoyAfter[numnpc] = 0
GvstreBoy = 0
boyvstre[numnpc] += 1
gs 'stat'
'<center><h1><font color="maroon"><<$nameBoyfrend[numnpc]>></font></h1></center>'
!!'<center><img src="images/qwest/alter/npc<<numnpc>>.jpg"></center>'

$bfTitle[numnpc] = 'date'
if boytimes[numnpc] = 0:
	$bfRelationship[numnpc] = 'He made a pretty good impression the first time you met so you gave him your phone number. This is your first date with him, you wonder how you''ll get along.'
	'You spot a guy in the park. He looks like he''s waiting for someone. "Ah, yes, this must be <<$nameBoyfrend[numnpc]>>!", you think. <<$bfRelationship[numnpc]>>'
elseif boytimes[numnpc] <= 4:
	$bfRelationship[numnpc] = 'You recently started dating him'

	if otnBoyFrend[numnpc] < 20:
    	$bfRelationship[numnpc] += ', but you are already off to a bad start. You don''t get along at all.'
    elseif otnBoyFrend[numnpc] < 40:
    	$bfRelationship[numnpc] += ', but there are already some tensions between you.'
    elseif otnBoyFrend[numnpc] < 60 and gnpcSex[numnpc] = 0:
    	$bfRelationship[numnpc] += ', but you don''t know him that well yet.'
    elseif otnBoyFrend[numnpc] < 60 and gnpcSex[numnpc] > 0:
    	$bfRelationship[numnpc] += ' and you don''t know him that well yet. But you like to occasionally blow off a little steam with him, no strings attached.'
    elseif otnBoyFrend[numnpc] < 80 and gnpcSex[numnpc] = 0:
    	$bfRelationship[numnpc] += ' and you don''t know him that well yet, but you think you''re off to a good start.'
    elseif otnBoyFrend[numnpc] < 80 and gnpcSex[numnpc] > 0:
    	$bfRelationship[numnpc] += '. You don''t know him that well yet, but you enjoy each others company and your sexual adventures.'
    elseif otnBoyFrend[numnpc] < 100 and gnpcSex[numnpc] = 0:
    	$bfRelationship[numnpc] += ' and even though you don''t know him that long you think you found a wonderful friend in him.'
    elseif otnBoyFrend[numnpc] < 100 and gnpcSex[numnpc] > 0:
    	$bfRelationship[numnpc] += ' and even though you don''t know him that long you have already strong feelings for him.'
    elseif otnBoyFrend[numnpc] >= 100 and gnpcSex[numnpc] = 0:
    	$bfRelationship[numnpc] += ' and even though you don''t know him that long it feels like he''s already one of your best friends.'
    elseif otnBoyFrend[numnpc] >= 100 and gnpcSex[numnpc] > 0:
    	$bfRelationship[numnpc] += ' and even though you don''t know him that long it feels like you found your soulmate. You are totally in love with him!'
    end

	'You see <<$nameBoyfrend[numnpc]>> in the park, apparently waiting for you. <<$bfRelationship[numnpc]>>'
elseif boytimes[numnpc] <= 16:
	$bfRelationship[numnpc] = 'You''ve been dating him for a while now'

	if otnBoyFrend[numnpc] < 20:
    	$bfRelationship[numnpc] += ' and you don''t know why you''re still wasting your time with him. You don''t get along at all.'
    elseif otnBoyFrend[numnpc] < 40:
    	$bfRelationship[numnpc] += ', but there are some tensions in your relationship.'
    elseif otnBoyFrend[numnpc] < 60 and gnpcSex[numnpc] = 0:
    	$bfRelationship[numnpc] += ' and you get along pretty well.'
    	$bfTitle[numnpc] = 'friend'
    elseif otnBoyFrend[numnpc] < 60 and gnpcSex[numnpc] > 0:
    	$bfRelationship[numnpc] += 'and you like to occasionally blow off a little steam with him, no strings attached.'
    	$bfTitle[numnpc] = 'lover'
    elseif otnBoyFrend[numnpc] < 80 and gnpcSex[numnpc] = 0:
    	$bfRelationship[numnpc] += '. He''s a fun guy to hang around with.'
    	$bfTitle[numnpc] = 'friend'
    elseif otnBoyFrend[numnpc] < 80 and gnpcSex[numnpc] > 0:
    	$bfRelationship[numnpc] += '. You are friends with benefits.'
    	$bfTitle[numnpc] = 'lover'
    elseif otnBoyFrend[numnpc] < 120 and gnpcSex[numnpc] = 0:
    	$bfRelationship[numnpc] += '. He''s a good friend and you feel very comfortable around him.'
    	$bfTitle[numnpc] = 'friend'
    elseif otnBoyFrend[numnpc] < 120 and gnpcSex[numnpc] > 0:
    	$bfRelationship[numnpc] += ' and you consider him your boyfriend. You really like him a lot.'
    	$bfTitle[numnpc] = 'boyfriend'
    elseif otnBoyFrend[numnpc] >= 120 and gnpcSex[numnpc] = 0:
    	$bfRelationship[numnpc] += ' and he''s become one of your best friends by now.'
    	$bfTitle[numnpc] = 'best friend'
    elseif otnBoyFrend[numnpc] >= 120 and gnpcSex[numnpc] > 0:
    	$bfRelationship[numnpc] += ' and you enjoy every minute with your lovely boyfriend. You love him deeply.'
    	$bfTitle[numnpc] = 'boyfriend'
    end

	'You see <<$nameBoyfrend[numnpc]>> in the park, apparently waiting for you. <<$bfRelationship[numnpc]>>'
else
	$bfRelationship[numnpc] = ''

	if otnBoyFrend[numnpc] < 20:
    	$bfRelationship[numnpc] = 'For as long as it lasted, your relationship is now ruined. You can''t stand each other.'
    elseif otnBoyFrend[numnpc] < 40:
    	$bfRelationship[numnpc] = 'You''ve been dating him pretty long, but there are tensions in your relationship.'
    elseif otnBoyFrend[numnpc] < 60 and gnpcSex[numnpc] = 0:
    	$bfRelationship[numnpc] = 'You like to occasionally hang out with him.'
    	$bfTitle[numnpc] = 'friend'
    elseif otnBoyFrend[numnpc] < 60 and gnpcSex[numnpc] > 0:
    	$bfRelationship[numnpc] = 'You like to occasionally blow off some steam with him, no strings attached.'
    	$bfTitle[numnpc] = 'lover'
    elseif otnBoyFrend[numnpc] < 80 and gnpcSex[numnpc] = 0:
    	$bfRelationship[numnpc] = 'He''s been a good friend to you and you have fun hanging out with him.'
    	$bfTitle[numnpc] = 'friend'
    elseif otnBoyFrend[numnpc] < 80 and gnpcSex[numnpc] > 0:
    	$bfRelationship[numnpc] = 'One could consider you a couple for as long as you''ve been together now, but you both see yourselves more as friends with benefits.'
    	$bfTitle[numnpc] = 'lover'
    elseif otnBoyFrend[numnpc] < 120 and gnpcSex[numnpc] = 0:
    	$bfRelationship[numnpc] = 'He''s always been a very good friend and you love spending your time with him.'
    	$bfTitle[numnpc] = 'friend'
    elseif otnBoyFrend[numnpc] < 120 and gnpcSex[numnpc] > 0:
    	$bfRelationship[numnpc] = 'He''s your lovely boyfriend.'
    	$bfTitle[numnpc] = 'boyfriend'
    elseif otnBoyFrend[numnpc] < 160 and gnpcSex[numnpc] = 0:
    	$bfRelationship[numnpc] = 'He''s one of your best friends, you could talk with him about anything.'
    	$bfTitle[numnpc] = 'best friend'
    elseif otnBoyFrend[numnpc] < 160 and gnpcSex[numnpc] > 0:
    	$bfRelationship[numnpc] = 'You still feel lucky that you can call him your boyfriend. You love him deeply.'
    	$bfTitle[numnpc] = 'boyfriend'
    elseif otnBoyFrend[numnpc] >= 160 and gnpcSex[numnpc] = 0:
    	$bfRelationship[numnpc] = 'He''s your best friend, you would trust him with your life.'
    	$bfTitle[numnpc] = 'best friend'
    elseif otnBoyFrend[numnpc] >= 160 and gnpcSex[numnpc] > 0:
    	$bfRelationship[numnpc] = 'He''s your lovely boyfriend and the guy you want to marry someday. You absolutely adore him.'
    	$bfTitle[numnpc] = 'boyfriend'
    end

	'You see the familiar face of <<$nameBoyfrend[numnpc]>> in the park, obviously waiting for you. <<$bfRelationship[numnpc]>>'
end

*nl

$bfGreeting = ''
if boytimes[numnpc] > 0 and otnBoyFrend[numnpc] > 40:
	if (gnpcSex[numnpc] = 0 and otnBoyFrend[numnpc] > 80) or gnpcSex[numnpc] > 0:$bfGreeting += ' with a hug'
	if (gnpcSex[numnpc] = 0 and otnBoyFrend[numnpc] > 120) or (gnpcSex[numnpc] > 0 and otnBoyFrend[numnpc] < 80):
		$bfGreeting += ' and a kiss on the cheek'
	elseif gnpcSex[numnpc] > 0 and otnBoyFrend[numnpc] > 80:
		$bfGreeting += ' and a kiss on the lips'
		$orgasm_or = 'no'
		gs 'arousal', 'kiss', 1
		gs 'arousal', 'end'
	end
end
'You approach <<$nameBoyfrend[numnpc]>> and greet him<<$bfGreeting>>.'

act 'Apologize and leave':otnBoyFrend[numnpc] -= 5 & gt $loc, $metka

act 'Take a walk through the park':
	cla

	if hapri = 0:otnBoyFrend[numnpc] -= 5
	if hapri = 1:otnBoyFrend[numnpc] += 1
	if mop = 2:otnBoyFrend[numnpc] += 1
	if mop = 3:otnBoyFrend[numnpc] += 2
	if mop = 4:otnBoyFrend[numnpc] -= 5
	if sweat > 19:
		otnBoyFrend[numnpc] -= 5
	elseif sweat < 10:
		otnBoyFrend[numnpc] += 1
	end
	if PCloSkirt > 2:otnBoyFrend[numnpc] += 3
	if PCloPants > 3:otnBoyFrend[numnpc] += 2

	minut += 15
	boytimes[numnpc] += 1
	manna += 10
	gs 'stat'

	$bfWalk = ''
	if otnBoyFrend[numnpc] > 80 and gnpcSex[numnpc] > 0:$bfWalk = ' hand in hand'
	'You walk<<$bfWalk>> through the park while talking and having fun being together.'

	if kotovLoveQW > 0:
		otnBoyFrend[numnpc] = 0
		manna -= 10
		*nl
		'You hear footsteps behind you and when you turn to look you see Witek Kotov heading straight your way. <i>"Oh boy, he looks pissed!"</i>, you think to yourself just as he shouts, "Hey, dipshit!"'
		'When <<$nameBoyfrend[numnpc]>> turns around to look who''s shouting Witek slaps him so hard across the face that <<$nameBoyfrend[numnpc]>> immediately falls to the ground.'
		'Witek towers over him, his eyes glaring with rage, "Listen, you little fucker, this is MY girl! If I ever see you near her again I will bury you in the woods alive, do you understand?!"'
		'<<$nameBoyfrend[numnpc]>> stammers something and nods his head fearfully while crawling backwards. Then he jumps back on his feet und runs as fast as he can.'
		'You doubt that you will ever see him again...'

		act 'Leave':gt $loc, $metka

		exit
	end

	gboyrand = rand(0, 1)

	if gboyrand = 0:
		'<<$nameBoyfrend[numnpc]>> offers to go to the movies.'

		act 'Apologize and leave':otnBoyFrend[numnpc] -= 5 & gt $loc, $metka

		act 'Go to the cinema':
			cls
			minut += 15
			gs 'stat'
			'You go with <<$nameBoyfrend[numnpc]>> to the movie theater near the park. <<$nameBoyfrend[numnpc]>> buys the tickets and together you enter a small cinema hall and find your seats.'

			act 'Watch the movie':
				cls
				manna += 100
				minut += 100
				otnBoyFrend[numnpc] += 5
				gs 'stat'
				'You got a guy on the chair placed in the back row. Soon the lights dimmed and the movie started on the screen.'

				if otnBoyFrend[numnpc] < 80:
					'You watched the movie a couple of hours sitting in a chair next to the guy.'

					act 'Go to the exit':gt 'Gnpc2'
				elseif otnBoyFrend[numnpc] >= 80 and gnpcSex[numnpc] = 0:
					'You have been watching a movie and suddenly felt a hand on my knee Man.'

					act 'Remove his hands':
						cls
						dom += 1
						otnBoyFrend[numnpc] -= 20
						gs 'stat'
						'You have removed palm guy with his knees and continued to watch the movie. Movie finally ended. In the hall lights went on and the screen went titles.'

						act 'Go to the exit':gt 'Gnpc2'
					end

					act 'Ignore':
						cls
						otnBoyFrend[numnpc] += 5
						gs 'stat'
						'You pretended that nothing happens and the guy started more actively stroking your leg. Movie finally ended. In the hall lights went on and the screen went titles.'

						act 'Go to the exit':gt 'Gnpc2'
					end
				elseif otnBoyFrend[numnpc] >= 80 and gnpcSex[numnpc] > 0:
					'You have been watching the movies, and <<$nameBoyfrend[numnpc]>> put his hand on your foot and began to stroke your knee. Movie finally ended. In the hall lights went on and the screen went titles.'

					act 'Go to the exit':gt 'Gnpc2'
				end
			end
		end
	elseif gboyrand = 1:
		'<<$nameBoyfrend[numnpc]>> suggests to drink some beers in the park.'

		act 'Apologize and leave':otnBoyFrend[numnpc] -= 5 & gt $loc, $metka

		act 'Go for a beer':
			cla
			minut += 15
			gs 'stat'
			'You head with <<$nameBoyfrend[numnpc]>> to the nearest kiosk. He buys a few a beers and some snacks and then you settle down on a secluded bench in the far corner of the park.'

			act 'Drink':
				cla
				manna += 100
				minut += 30
				alko += 2
				cumspclnt = 2
				gs 'cum_cleanup'
				fbreath = 0
				otnBoyFrend[numnpc] += 5
				gs 'stat'

				$bfDrinkSuffix = '.'
				if otnBoyFrend[numnpc] < 40:$bfDrinkSuffix = ', briefly forgetting the tensions in your relationship for the moment.'
				'<<$nameBoyfrend[numnpc]>> pours some beer into glasses and you both start drinking. He tells you many funny stories and you both laugh a lot while enjoying your drinks<<$bfDrinkSuffix>>'

				if boytimes[numnpc] <= 4:'You think you''ve got to know him a lot better now especially in a loose atmosphere like this.'

				act 'Continue':gt 'Gnpc2'
			end
		end
	end
end

if preg = 1 and pregtime <= 80 and pregTalk[numnpc] = 0 and gnpcSex[numnpc] > 0 and knowpreg = 1:
	if $father ! $nameBoyfrend[numnpc]:
		$pregLie = '(Lie) '
	else
		$pregLie = ''
	end
	act '<<$pregLie>>Tell him that he knocked you up':
		cla
		manna -= 50
		gs'stat'
		'You tell <<$nameBoyfrend[numnpc]>> that he got you pregnant. His eyes nearly pop out of their sockets.'

		if $father ! $nameBoyfrend[numnpc]: pregLieRand = rand(1,5)
		if $father = $nameBoyfrend[numnpc] or ($father ! $nameBoyfrend[numnpc] and pregLieRand >= 4):
			'He looks at his feet, unable to look you in the eyes. "Listen, I''m not ready to be a father or even to marry you." He hands you five thousand â‚½. "Here, take this money and let us part."'

			act 'Dump him and take the money':
				cla
				dom += 2
				pregTalk[numnpc] = 1
				otnBoyFrend[numnpc] = 0
				money += 5000
				gs 'stat'
				'You furiously scream at him that you never want to see him again. Then you snatch the bills from his hand and leave.'

				act 'Leave':gt $loc, $metka
			end

			act 'Dump him and throw the money in his face':
				cla
				dom += 5
				pregTalk[numnpc] = 1
				otnBoyFrend[numnpc] = 0
				gs 'stat'
				'You furiously scream at him that you never want to see him again and throw the money in his face before storming off.'

				act 'Leave':gt $loc, $metka
			end

		else
			'He snaps at you, "Yeah sure, you slut! Do you think I''m that stupid? You fuck around behind my back and now you want to pin the consequences on me? Go and find some other sorry ass who you can exploit!"'

			act 'Dump him':
				cla
				dom += 5
				pregTalk[numnpc] = 1
				otnBoyFrend[numnpc] = 0
				gs 'stat'
				'You furiously scream at him that you never want to see him again and storm off.'

				act 'Leave':gt $loc, $metka
			end
		end
	end
end

if GboyBalabol[numnpc] = 1:
	act 'Blame him for your bad reputation':
		cla
		gs 'stat'
		'You accuse <<$nameBoyfrend[numnpc]>> that he blabbed about your sexual relationship and now and everyone thinks that you''re a <<$gnikname>>.'
		reaktrand = rand(0, 2)

		if reaktrand = 0:
			'<<$nameBoyfrend[numnpc]>> begins to apologize, he''s been drinking with a friend and accidentally told him about your sexual exploits together. He vows that this will never happen again.'
		elseif reaktrand = 1:
			'<<$nameBoyfrend[numnpc]>> tries to comfort you, "<<$nickname>>, I would never do such a thing. Just ignore what those idiots say. They will soon find someone else to gossip about."'
		elseif reaktrand = 2:
			'<<$nameBoyfrend[numnpc]>> grins, "Relax, <<$nickname>>. It''s a fact that you''re a <<$gnikname>>. So what?"'
		end

		act 'Break up with him':
			cla
			dom += 5
			GboyBalabol[numnpc] = 2
			otnBoyFrend[numnpc] = 0
			gs 'stat'
			'You furiously scream at him that you never want to see him again and storm off.'

			act 'Leave':gt $loc, $metka
		end

		act 'Forgive him':
			cla
			sub += 5
			GboyBalabol[numnpc] = 2
			gs 'stat'
			'You think a about what he said. Because of the rumours, a lot of people have already a bad opinion about you and you don''t want to lose your <<$bfTitle[numnpc]>>, too. So you reluctantly decide to forgive him.'

			act 'Leave':gt $loc, $metka
		end
	end
end

if sub < 20 and ((boytimes[numnpc] <= 4 and otnBoyFrend[numnpc] < 100) or (boytimes[numnpc] > 4 and otnBoyFrend[numnpc] < 120)):
	act 'End the relationship':
		cla
		*nl
		'You think about how you could approach your <<$bfTitle[numnpc]>> with this delicate matter...'
		*nl

		if otnBoyFrend[numnpc] < 40 or dom > 10:
			act 'Break up with him':
				cla
				dom += 5
				otnBoyFrend[numnpc] = 0
				gs 'stat'
				'You tell <<$nameBoyfrend[numnpc]>> that it would be better if you don''t see each other again and that he should''t call you anymore. He just stares at you, so you decide to quickly leave before he overcomes his state of shock.'

				act 'Leave':gt $loc, $metka
			end
		end

		if  otnBoyFrend[numnpc] >= 40 and gnpcSex[numnpc] > 0:
			act 'Just be friends':
				cla
				temprand = rand(0, 4)
				sub -= 5
				'You nervously approach <<$nameBoyfrend[numnpc]>>, "Listen, <<$nameBoyfrend[numnpc]>>, I really like you, but I''m not ready for an intimate relationship right now. I think we should forget what happened between us and just be friends, okay?"'

				if boytimes[numnpc] > 4 and otnBoyFrend[numnpc] >= 80 and temprand = 0:
					otnBoyFrend[numnpc] -= 40
					gnpcSex[numnpc] = 0
					gs 'stat'

					'<<$nameBoyfrend[numnpc]>> looks totally dejected as you tell him the bad news. You really feel pity for him. When finish your speech there''s a moment of painful silence.'
					'Then <<$nameBoyfrend[numnpc]>> takes a deep breath, looks you in the eyes and says, "That''s really hard on me, <<$name>>. But I respect your feelings. I like you, too... very much in fact! That''s why I''m willing to be your friend, even if it means that we can''t be a couple."'
					'Wow, you didn''t expect that! Still, you feel touched by his devotion and give him a bear hug, smiling from ear to ear. "Thank you, <<$nameBoyfrend[numnpc]>>! I''m so glad that you can understand me. I think we both need some time now to digest all that. Call me when you''re feeling better."'
				else
					otnBoyFrend[numnpc] = 0
					gs 'stat'

					'<<$nameBoyfrend[numnpc]>> narrows his eyes, "Just friends, huh? That''s a pretty lame excuse, <<$name>>, you know that? If you want to break up with me then why don''t you say so!"'
					'You try to explain to him, "No, that''s not what I mean, <<$nameBoyfrend[numnpc]>>! I really like you..."'
					'"You know what? You can stuff your friendship! I''m outta here!", with these words he storms off.'
				end

				act 'Leave':gt $loc, $metka
			end
		end

		act 'Back down':gt 'Gnpc'
	end
end

--- Gnpc ---------------------------------

