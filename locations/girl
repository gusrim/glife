# girl

!!'<<$girldesc>> <<$girlbody>> <<$girlbod>> <<$girlface>>. <<$girldesc>> <<$girlClo>>'
if $ARGS[0] = '':
	!randomly age
	girlage = rand(18,35)

	!! {The NPC generator is now the primary system of creating new characters.}
	if girlage <= 25:
		gs 'npcgeneratec', 1, 'A Girl', girlage
	else
		gs 'npcgeneratec', 1, 'A Woman', girlage
	gs 'boyStat', $npclastgenerated
	gs 'girl', 'details'
end

if $ARGS[0] = 'details':
	!Select Photos

:girldetailsloop
	!! girls 1-30, girlfriends 31-60
	picrand =  RAND(1,30) + 30
	if picrand = lover_picture[0] or picrand = lover_picture[1] or picrand = lover_picture[2]: jump 'girldetailsloop'

	silaVag = npc_sexskill[$npclastgenerated]
	$npc_usedname[$npclastgenerated] = $npc_firstname[$npclastgenerated]
	$girldesc = $npc_usedname[$npclastgenerated]

	!!appearance
	girlbodyrand = rand(1, 3)

	if girlbodyrand = 1: $girlbody = 'short'
	if girlbodyrand = 2: $girlbody = 'average'
	if girlbodyrand = 3: $girlbody = 'tall'

	girlbodrand = rand(1, 4)

	if girlbodrand = 1 and girlbodyrand = 1: $girlbod = 'petite'
	if girlbodrand = 1 and girlbodyrand > 1: $girlbod = 'slender'
	if girlbodrand = 2: $girlbod = 'toned'
	if girlbodrand = 3: $girlbod = 'chubby'
	if girlbodrand = 4: $girlbod = 'fat'

	girlfacerand = rand(1, 3)

	if girlfacerand = 1: $girlface = 'black'
	if girlfacerand = 2: $girlface = 'brunette'
	if girlfacerand = 3: $girlface = 'blond'

	!!variable is responsible for guy
	girlonce = 0

	!!Variables responsible for any features like
	!!security, is responsible for the money and requirements
	harakgirl = rand(0, 2)
	finance = rand(0, 2)

	if finance = 0:
		!!Gopnik beggar or a hard worker
		!!check in appearance Primary
		if harakgirl = 0: npc_app[$npclastgenerated] = rand(0, 8)
		if harakgirl = 1: npc_app[$npclastgenerated] = rand(0, 16)
		if harakgirl = 2: npc_app[$npclastgenerated] = rand(0, 32)

		$girlClo = 'wearing a tracksuit.'
	elseif finance = 1:
		!!middle_peasant
		!!check in appearance
		if harakgirl = 0: npc_app[$npclastgenerated] = rand(0, 16)
		if harakgirl = 1: npc_app[$npclastgenerated] = rand(0, 24)
		if harakgirl = 2: npc_app[$npclastgenerated] = rand(0, 32)

		$girlClo = 'wearing jeans and a blouse.'
	elseif finance = 2:
		!!middle_peasant
		!!check in appearance
		if harakgirl = 0: npc_app[$npclastgenerated] = rand(16, 32)
		if harakgirl = 1: npc_app[$npclastgenerated] = rand(24, 32)
		if harakgirl = 2: npc_app[$npclastgenerated] = rand(32, 40)

		$girlClo = 'wearing an expensive dress.'
	end

	!!requirements of the figure, chest size, hair color.
	!!0 - ass less 80, 1 back from 80 prior_to 100, 2 ass over 100
	figurgirl = rand(0, 2)
	!!0 - chest less 3, 1 breast 3-4, 2 breasts bigger 4
	npc_bust[$npclastgenerated] = rand(0, 2)
	!!0 - black, 1 - brown, 2 - Redhead, 3 - blonde
	npc_haircol[$npclastgenerated] = rand(0, 3)
	!!character 0 - soft, 1 -norm 2 - aggressor
	randizvrat = rand(0, 100)

	if randizvrat < 90:izvrat = 0
	if randizvrat >= 90:izvrat = 1



	hairgirl = npc_haircol[$npclastgenerated]
	titgirl = npc_bust[$npclastgenerated]
	vneshgirl = npc_app[$npclastgenerated]


	!! trying to make the preference section 
	!! Variables here are clotTypePrefTmp, clotQualPrefTmp, clotTopPrefTmp, bottShorPrefTmp, clotThinPrefTmp, bimbPrefTmp, pierPrefTmp, tattPrefTmp, lipsPrefTmp, bodyPrefTmp, makePrefTmp, IQPrefTmp.


	!!This is for clothing type preference
	!! If you notice how rare it is to get a preference that is intentional
	!!0 - none, 1 - cheap, 2 - average, 3 - formal, 4 - fetish.
	clotTypePrefTmp = 0
	if rand(0, 4) = 4:
		if finance = 0:
			clotTypePrefTmp = rand(1,2)
			if rand(0, 1) = 1 and izvrat = 1:clotTypePrefTmp = 4
		elseif finance = 1:
			clotTypePrefTmp = rand(1,3)
			if rand(0, 2) = 2 and izvrat = 1:clotTypePrefTmp = 4
		elseif finance = 2:
			clotTypePrefTmp = rand(2,3)
			if rand(0, 1) = 1 and izvrat = 1:clotTypePrefTmp = 4
		end
	end


	!!This is for the quality of clothing
	!! 0 - no preference, 1 - 1 to 4 units cheap to average (Locked if formal chosen), 2 - 5 to 7 units expensive to ballroom quality (Locked if fetish is chosen)
	clotQualPrefTmp = 0
	if rand(0, 5) = 5:
		if finance = 0:
			clotQualPrefTmp = 1
		elseif finance = 1:
			clotQualPrefTmp = rand(1, 2)
			randchan = rand(0, 1)
			if randchan = 0 and clotTypePrefTmp ! 3:
				clotQualPrefTmp = 1
			elseif randchan = 1 and clotTypePrefTmp ! 1:
				clotQualPrefTmp = 2
			end
		elseif finance = 2:
			randchan = rand(0, 2)
			if randchan = 2 and clotTypePrefTmp ! 1:
				clotQualPrefTmp = 2
			end
		end
	end

	!!This is for the clothing top cut for the clothing


	!!Need to add modifiers based on breast size, where larger breasts end up forcing the player to show more cleavage than those with smaller breasts.
	!! 0 - no preference, 1 - 1 to 2 or neckline to moderate, 2 - 3 to 4 or heavy to severe, 3 - 5 to 7 or buldging to outpouring 
	!! The breast size must be adjusted either when the action is being calculeted or we can base it on their ideal breast size :titgirl + 0/3 depending on the players breast size


	!!0 - no preference, 1 - 1 neckline , 2 - 2 or moderate, 3 - 3 to 4 or heavy to severe
	!! It doesn''t matter if your poor or rich, everyone loves tits.

	slutConstant = 0
	if rand(0, 2) = 2 and izvrat = 1:slutConstant = 1


	clotTopPrefTmp = 0
	if rand(0, 2) = 2:
		randchan = rand(0, 5)
		if randchan < 3 and clotTypePrefTmp ! 4:
			clotTopPrefTmp = 1 + slutConstant
		elseif randchan < 5 and clotTypePrefTmp ! 4:
			clotTopPrefTmp = 2 + slutConstant
		elseif randchan = 5 and clotTypePrefTmp ! 3:
			clotTopPrefTmp = 3
		end
		if clotTopPrefTmp ! 0 and titgirl = 2:
			if clotPref ! 3:
				clotTopPrefTmp += 1
			end
		end
	end

	!! This is for pants and skirt shortness and will be grouped together as such.
	!! 0 - no preference, 1 - 1 to 2 units or ankle to calf length, 2 - 3 to 4 units or knee to miniskirt, 3 - 5 units to 6 or slut to hoe.

	bottShorPrefTmp = 0
	if rand(0, 2) = 2:
		randchan = rand(0, 5)
		if randchan < 3 and clotTypePrefTmp ! 4:
			bottShorPrefTmp = 1 
		elseif randchan < 5:
			bottShorPrefTmp = 2 
		elseif randchan = 5:
			bottShorPrefTmp = 3
		end
		randchan = rand(0, 1)
		if slutConstant = 1 and randchan = 1 and clotTypePrefTmp = 4:
			bottShorPrefTmp = 3
		end
	end

	!! This is for clothes thinness.
	!! 0 - no preference, 1 - 0 to 2 (Conservative), 2 - 3 to 4 (Moderate), 3 - 4 to 6 (really thin)
	!! I see the moderate to really thin overlapping. Its intentional.

	clotThinPrefTmp = 0
	if rand(0, 2) = 2:
		randchan = rand(0, 5)
		if randchan < 3:
			clotThinPrefTmp = 1 + slutConstant
		elseif randchan < 5:
			clotThinPrefTmp = 2 + slutConstant
		elseif randchan = 5 and clotTypePrefTmp ! 3:
			clotThinPrefTmp = 3
		end
	end


	!!This is to find out if they want you to wear bimbo clothing
	!! 0 - no preference, 1 - Prefers bimbo clothing.

	bimbPrefTmp = 0
	if rand(0, 10) = 0 and clotTypePrefTmp ! 3:bimbPrefTmp = 1

	!!This is to find out if they want you to wear piercings.
	!! 0 - no preference, 1 - 0 to 2 piercings, 2 - 3 to 5 piercings, 3 - 6 or more piercings.

	pierPrefTmp = 0
	if rand(0, 3) = 3:
		randchan = rand(0, 2)
		if randchan = 0:
			pierPrefTmp = 1 + slutConstant
		elseif randchan = 1:
			pierPrefTmp = 2 + slutConstant
		elseif randchan = 2:
			pierPrefTmp = 3
		end
	end


	!!This is to find out if they want you to have tattoos.
	!! 0 - no preference, 1 - 0 to 2 tattoos, 2 - 3 to 5 tattoos, 3 - 6 or more tattoos.

	tattPrefTmp = 0
	if rand(0, 3) = 3:tattPrefTmp =  rand(1, 3)

	!!This is to find out how big they want your lips.
	!! 0 - no preference, 1 - 2 to 3 units (average) , 2 - 4 units (THIC lips)

	lipsPrefTmp = 0
	if rand(0, 3) = 3:
		randchan = rand(0, 2)
		if randchan = 0:
			lipsPrefTmp = 1 + slutConstant
		elseif randchan = 2:
			lipsPrefTmp = 2
		end
	end

	!!This is to find out how big they want YOU.
	!! 0 - no preference, 1 - 1 to 3 units (Skinny) , 2 - 3 to 6 units (Average), 3 - 6 + units (THIC)
	!! Notice how this one overlaps. I believe that it should because changing ones weight is very hard.

	bodyPrefTmp = 0
	if rand(0, 3) = 3:bodyPrefTmp = rand(1, 3) 

	!!This is to find out how much makeup they want you to wear.
	!! 0 - no preference, 1 - 1 (light makeup) , 2 - 2 (moderate makeup), 3 - 3(heavy makeup)
	!! Plan to make a way for you to set what you makeup you want to wear at your bedside and giving into their wishes would automatically change the automated setting.

	makePrefTmp = 0
	if rand(0, 1) = 1:
		randchan = rand(0, 5)
		if randchan < 3:
			makePrefTmp = 1 + slutConstant 
		elseif randchan < 5:
			makePrefTmp = 2 + slutConstant
		elseif randchan = 5 and clotTypePrefTmp ! 3:
			makePrefTmp = 3 
		end
	end


	!! This is to see if they like a ditz, average, or a smartypants
	!!0 - no preference, 1 - 1 to 30 intelligence, 2 - 30 to 85 intelligence, 3 - 85 + intelligence. 

	IQPrefTmp = 0
	if rand(0, 4) = 4:IQPrefTmp = rand(1, 2) + rand(1, 2)

end

--- girl ---------------------------------

