# pavCommercial

!{Change log
25 March 2016
Fixed diplaced period after church link
Fixed visting links to look the same
Added start of Andrei kidnapping
}
$location_type = 'public_outdoors'

$metka = ''
$loc = 'pavCommercial'
$metkaStreet = ''
$locStreet = 'pavCommercial'
CLOSE ALL
if sound = 0:
	if month >=11 and month <= 12 or month >=1 and month <=3:
		if hour >= 8 and hour <= 23: PLAY 'sound/street2.mp3',30 & PLAY 'sound/zima.mp3',30
		if hour >= 0 and hour <= 7: PLAY 'sound/street_night.mp3',30 & PLAY 'sound/zima.mp3',30
	else
		if hour >= 8 and hour <= 23: PLAY 'sound/street2.mp3',30 & PLAY 'sound/street_leto.mp3',30
		if hour >= 0 and hour <= 7:PLAY 'sound/street_night.mp3',30
	end
end

cla
*clr
litltown = 1
alkoblock = 0
boynumBlock = 0

!call random events from [street_events], and display them before show location.
if streetevent_hour + 2 < hour:gt'street_event','pavCommercial', 'main'

'<center><h2>Pavlovsk</h2></center>'
if Enable_towntable = 1 : $_str = '<center><table><tr><td>' else $_str = ''
if month >= 11 or month <= 3:
	if hour >= 8 and hour <= 21:
		$_str += '<center><img <<$set_imgh>> src="images/locations/pavlovsk/gorodokw.jpg"></center>'
	else
		$_str += '<center><img <<$set_imgh>> src="images/locations/pavlovsk/pavbuswn.jpg"></center>'
	end
else
	if hour >= 8 and hour <= 21:
		$_str += '<center><img <<$set_imgh>> src="images/locations/pavlovsk/gorodok.jpg"></center>'
	else
		$_str += '<center><img <<$set_imgh>> src="images/locations/pavlovsk/pavbusn.jpg"></center>'
	end
end

if Enable_towntable = 1 : $_str += '</td><td>'
$_str += '<center>The high street of a provincial town in a rural area of Russia.  Population: about 15000 inhabitants.</center>'
$_str += '<center>Apart from the old <a href="exec:view''images/locations/pavlovsk/palace/Pav_Palace.jpg''">Imperial palace</a> with its surrounding gardens and forest, built by Catherine the Great, and the recently rebuilt <a href="exec:view''images/locations/pavlovsk/fortress/BIP_hotel.png''">Bip fortress</a> (which is now a Luxury hotel and restaurant), it looks much like all the other towns in the region.</a></center> '
$_str += '<center>The tallest building in town is the <a href="exec:gt ''ParChurch'',''start''">church of St Nikolas.</a></center>'
if Enable_towntable = 1 : $_str += '</td></tr><table></center>'
*nl $_str
killvar '$_str'
''

gs'street_event','pavCommercial', 'extras'

if AlbFather >= 1:
	if AlbFaterBlackTime = 0:AlbFaterBlackTime = totminut + 2880

	'You can see election posters on the walls all over town. They say: "Vote for Boris Barlovsky!" Wait a minute... isn''t that Albina''s father?'
end

if AlbFaterBlackTime > 0 and AlbFaterBlackTime <= totminut and hour >= 14 and AlbFather = 1:gt 'AlbFatherQW'
if hour >= 14 and hour <= 22 and AlbFather = 2 and AlbZaDay + 30 < daystart and year > 2015:
	gt 'AlbStarsEnd'
end

if StoryLine > 0 and hour < 4 and motherWorry = 0 and SchoolAtestat = 0 and age < 18:
	motherWorry = 1
end

if rand(1, 5) >= 4 and pav_slut >= 50:
	if pav_slut < 75:
		'You catch people staring at you intently, as if they''re supposed to know who you are but can''t quite remember why. You sincerely hope they don''t realize your promiscuous adventures are the reason why. The occasional person chuckling and pointing at you crushes those hopes, and you quickly keep walking before they get a chance to say anything.'
	elseif pav_slut < 100:
		'People look at you with lewd grins on their faces, some of them even making vulgar gestures. Looks like they know what you''ve been up to.'
	else
		'Everywhere you go, people recognize you as a whore. Some even come over and slap you on the ass, claiming they know you like it when they do, or claiming they want to sample your goods. An elderly woman sitting on a bench calls out to you, loudly calling you a slut and a whore. You hide your face and run away as fast as you can, before she can draw too much attention to you.'
	end
end

gs 'stat'

if car > 0 and cardrive = 24:
	cardrive = 24
	'Your <a href="exec:gs ''carF'', ''start''">car<<$car>></a> is parked in the street.'
end

if hour >= 8 and hour <= 20:
	$Pharmname = ', and the <a href="exec: minut += 15 & gt ''gaptek'', ''start''">pharmacy</a> is right across the street.'
else
	$Pharmname = '.'
end

'You are on the high street, at one end is the <a href="exec:minut += 15 & gt ''vokzalG''">train station</a> and access to the highway. Near the highway you can also find a <a href="exec:gt ''zaprF'', ''start''">gas station</a>.'
'The nearby residential area is next to the lake and the town surrounds the old palace grounds which are now a large <a href="exec:gt ''gskver'', ''park''">public park</a>.'
'There is a small <a href="exec:minut += 15 & gt ''gpoli''">clinic</a><<$Pharmname>>'

if leonidSlave = 1:
	'Near the town hall there is an old <a href="exec:minut += 15 & gt ''pavCommercialOffices''">office building</a>.'
end


if hour >= 8 and hour <= 17:'The local <a href="exec: minut += 3 & gt ''barbershop'', ''start''">barbershop</a> is within walking distance, merely a few minutes away.'
if hour >= 7 and hour <= 18:'Pavlovsk has a <a href="exec:gt ''etoexhib'', ''pos20''">vocational school</a>, where young adults can learn a trade.'
if hour >= 8 and hour <= 20:'There is a big chain <a href="exec:gt ''shop'', ''start''">supermarket</a> nearby.'
if week < 6 and hour >= 9 and hour <= 18:'You can see the most important <a href="exec:bankloc = 0 & gt ''bank'', ''start''">Bank</a> in the city from here.'
if (week < 6 and hour >= 9 and hour <= 16) or (week = 6 and hour >= 9 and hour <= 13):'The local <a href="exec:bankloc = 0 & gt ''postoffice'', ''start''">post office</a> branch is open.'

'A <a href="exec:minut += 15 & gt ''Gshveyfab'', ''start''">garment factory</a> named Paris Commune is situated in the industrial area on the outskirts of town, close to the highway.'

''

if workPTU > 2:act '<b>Go to the hostel</b>':gt 'etoexhib', 'pos40'

act 'Go to the residential area':minut += 5 & gt 'pavResidential'

act 'Wait':
	cla
	waiting = input ("How long would you like to wait? (Not more than 120 minutes)")

	if waiting <= 0 or waiting > 120:
		minut += 15
	elseif waiting > 0 and waiting <= 120:
		minut += waiting
	end

	act 'Check the time':gt $curloc
end

if rand (0,2) ! 0:gs 'din_bad' & dynamic $mobile_check

if npc_QW['A113'] = 1 and belgangday<daystart:
	'As you walk through Pavlovsk, a shabby Mitsubishi Pajero stops near you. Vadim Bely steps out, along with several of his henchmen. He gives you a smile that you know is 100% fake and behaves amicably while he says: "Long time no see, <<$pcs_nickname>>! How are you? I hope you bear good news?"'
		if money>=belgangPay:
			act'Give him the money':
			!!img = handing over money
			cls
			gs'stat'
			'You have the money with you, handing it over to Vadim while his henchmen keep a look out. Once again, he gives you an awkward smile, very pleased how things have turned out. "It''s all there, right? I would hate to have to come back for the rest."'
			'You nod at him, reassuring him. "It''s all there, mister Bely. Count them if you don''t believe me."'
	        'He takes a moment to count the money and nods approvingly. "Very good, it''s the full amount. Pleasure doing business with you, <<$pcs_nickname>>. Take care of yourself."'
			'He signals his crew to get back in the jeep. You barely manage to dodge the spray of pebbles and dirt the spinning tires spew your way, as they drive off as fast as they can, probably on their way to another ''business appointment''.'
			money-=belgangPay
			gs'stat'
			act 'Continue':gt 'pavCommercial'
			end
	else
		act'You don''t have the money':
			cls
			gs'stat'
			'You''re tearing up as you look down at your shoes, not daring to look him in the eye. "I''m... I''m sorry, mister Bely. I simply don''t have that kind of money. If you give me some more time, I wi-"'
			'He frowns, abruptlingly cutting you off. You can see the disapproval in his eyes when you glance up. "That is very unfortunate <<$pcs_nickname>>. I had such high hopes for you," he says. "I guess we will have to find an alternative way for you to pay off your debts."'
			'He then directs his attention to two of his henchmen: "She''s coming with us."'
			'Too scared to resist, you simply let the two men grab you by your arms and pull you into the Mitsubishi. Vadim gets back behind the wheel and within minutes you''ve left Pavlovsk. You notice you''re being taken to the city, and you wonder what they''re planning for you there.'
			act'In the city...':
				cls
				gs'stat'
				minut+=60
				workDolg=belgangPay
				workDolg -= money
				money = 0
				belgangPay=0
				'The car pulls over in front of the sauna. After one of the criminals makes sure the coast is clear, two of the men pull you out of the car and escort you to a room inside the building. Vadim follows them and asks to be left alone with you for a minute. After the other guys leave the room, he explains: "Until you pay off your debts, we own you. You will live here and work here. Any expenses you incur while you are here will be added to your debt. Understood?"'
				'Silence fills the room as he waits for you to acknowledge what he just said. Mortified about what might happen if you don''t, you nod. He decides to paint the picture anyway: "If you try to escape or refuse to work, we''ll put a bullet in your head, chop you up and bury you in the woods in pieces. And that would be a waste, with such a pretty face as yours." He pinches your cheeks and then slaps your face; not hard enough to be painful, but it certainly drives the message home. You can tell he''s dead serious.'
				'After looking you in the eyes for a few more seconds to make sure you get the message, he turns around and leaves. You are all alone now.'
				act 'Look around':gt'saunaroom'
			end	
		end
	end
end

--- pavCommercial ---------------------------------

