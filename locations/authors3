# authors3
clr
minut += 10

gs 'stat'
betAmount = 10
slotsJackpot = betAmount * 20000

'<center><img src="images/casino/avt.jpg"></center>'
'Rules of the game are very simple: feed a <<betAmount>> <b>₽</b> coin into the machine and pull the lever. There are multiple winning combinations, but everyone here has their eyes on the <<slotsJackpot>> <b>₽</b> jackpot.'

act 'Pull the lever':
	if money >= betAmount:
		clr
		money -= betAmount

		slotsRand = rand(1, 100000)

		if slotsRand > 99990:
			winnings = slotsJackpot
			msg 'Jackpot! What are the odds!?'
		elseif slotsRand >= 99900:
			winnings = betAmount * 1500
		elseif slotsRand >= 99000:
			winnings = betAmount * 100
		elseif slotsRand >= 90000:
			winnings = betAmount * 5
		elseif slotsRand >= 66667:
			winnings = betAmount * 2
		else
			winnings = 0
		end

		if winnings > 0:
			money += winnings
			msg 'You have won <<winnings>> <b>₽</b>.'
		else
			msg 'Alas, you lose. Try again?'
		end

		gt 'authors3'
	else
		msg 'You don''t have enough money to play. Turns out there''s no such thing as a free lunch, and you need money for a chance to win more money. Life is so unfair.'
		gt 'authors3'
	end

	killvar 'slotsRand'
	killvar 'winnings'
end

act 'Go back to the main hall':gt 'igravt'

--- authors3 ---------------------------------

