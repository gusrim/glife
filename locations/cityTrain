# cityTrain

if $ARGS[0] = 'start':
	$metka = 'start'
	$loc = 'cityTrain'
	$location_type = 'public_outdoors'
	cls
	gs'stat'
	'<center><B>City center railway station</B></center>'
	if hour < 9 or hour > 19:
		'<center><img <<$set_imgh>> src="images/locations/city/shared/trainstation/central_station_night.jpg"></center>'
	else
		'<center><img <<$set_imgh>> src="images/locations/city/shared/trainstation/central_station_day.jpg"></center>'
	end
	'The main station for the city, it is always busy offering services to many destinations and the best place to park your car or hire a taxi.'

	if car > 0 and cardrive = 2:
		'In the parking lot is <a href="exec:GS ''carF'',''start''">your <<$car>></a>.'
	end

	act 'Enter the station building': gt 'cityTrain', 'inside'
	act 'Go to the city center':minut += 5 & gt 'down'
end

if $ARGS[0] = 'inside':
	$metka = 'inside'
	$loc = 'cityTrain'
	$location_type = 'public_indoors'
	cls
	gs'stat'
	'<center><B>City center railway station</B></center>'
	'<center><img <<$set_imgh>> src="images/locations/city/shared/trainstation/central_station_inside.jpg"></center>'
	'The grand old station is even more impressive on the inside and has a number of shops and bars as well as all the platforms and trains. '
	*nl
	if Trainpass_day > daystart:
		'You have <<Trainpass_day - daystart>> days left on your train pass.'
		act 'Go to platforms': gt 'cityTrain', 'platform'
	else
		act 'Buy a ticket': gt 'cityTrain', 'tickets'
	end
	act 'Leave the station': gt 'cityTrain', 'start'
end

if $ARGS[0] = 'tickets':
	cls
	gs 'stat'
	'<center><h4>Ticket office</h4></center>'
	'<center><img <<$set_imgh>> src="images/locations/shared/train/kassa.jpg" ></center>'

	act 'Buy a ticket':
		cla
		gs 'cityTrain', 'buy_ticket'
	end

	if money >= 1500 and Trainpass_day <= daystart:
		act 'Buy a weekly pass  (7 days - 1500 <b>₽</b>) ':
			cla
			money -= 1500
			Trainpass_day = daystart + 7
			act 'Return': gt 'cityTrain', 'inside'
		end
	end

	if money >= 5500 and Trainpass_day <= daystart:
		act 'Buy a monthly pass (30day -  5500 <b>₽</b>)':
			cla
			money -= 5500
			Trainpass_day = daystart + 30
			act 'Return': gt 'cityTrain', 'inside'
		end
	end

	act 'Leave the ticket office':minut += 1 & gt 'cityTrain', 'inside'
end

if $ARGS[0] = 'buy_ticket':
	act 'Buy a ticket to the city industrial area (50 <b>₽</b>)':
		if money >= 50:
			money -= 50
			gt 'cityTrain', 'ind'
		else
			'You don''t have enough money for this ticket.'
		end
	end

	
	act 'Buy a ticket to the old platform by the communal village (75 <b>₽</b>)':
		if money >= 75:
			money -= 75
			gt 'cityTrain', 'communal'
		else
			'You don''t have enough money for this ticket.'
		end	
	end

	act 'Buy a ticket to the village of Gadyukino (100 <b>₽</b>)':
		if money >= 100:
			money -= 100
			gt 'cityTrain', 'village'
		else
			'You don''t have enough money for this ticket.'	
		end
	end

	
	act 'Buy a ticket to the old town (125 <b>₽</b>)':
		if money >= 125:
			money -= 125
			gt 'cityTrain', 'oldtown'
		else
			'You don''t have enough money for this ticket.'	
		end
	end

	act 'Buy a ticket to Pavlovsk (150 <b>₽</b>)':
		if money >= 150:
			money -= 150
			gt 'cityTrain', 'pavlovsk'
		else
			'You don''t have enough money for this ticket.'		
		end
	end

	act 'Leave the ticket office':minut += 1 & gt 'cityTrain', 'inside'
end

if $ARGS[0] = 'platform':
	$location_type = 'public_outdoors'
	gs 'stat'
	'<center><img <<$set_imgh>> src="images/locations/pavlovsk/trainstation/vokzalper.jpg" ></center>'
	'When you arrive at the platform, you can''t help but notice it bustling with activity. Lots of passengers are coming and going, all carrying shopping bags. It feels like it is always busy here, no matter at what time of the day it is.'

	act 'Take a train to the city industrial area': gt 'cityTrain', 'ind'

	act 'Take a train to the old platform by the communal village': gt 'cityTrain', 'communal'

	act 'Take a train to the village of Gadyukino': gt 'cityTrain', 'village'

	act 'Take a train to the old town': gt 'cityTrain', 'oldtown'

	act 'Take a train to Pavlovsk': gt 'cityTrain', 'pavlovsk'

	act 'Return to the station hall':gt 'cityTrain', 'inside'
end

if $ARGS[0] = 'ind':
	$loc = 'vokzalholle'
	$metka = ''
	minut += 5
	gs 'stat'
	if rand(1, 100) < 15: gt 'train', 'private'
	if rand(1, 100) <= 10: gt 'train_incidental', 'events'
	if rand(1, 100) <= 20: gt 'train', 'events'
	'<center><img <<$set_imgh>> src="images/locations/shared/train/indusplat.jpg" ></center>'
	'After 5 minutes, the train stops at the St. Petersburg city industrial area station.'
	act 'Get off the train':
		if train_event = 0 and rand(1, 100) <= 5: gt 'train_incidental', 'end'
		gt 'vokzalholle'
	end
end

if $ARGS[0] = 'communal':
	$loc = 'train'
	$metka = 'communal'
	minut += 15
	if rand(1, 100) <= 10: gt 'train_incidental', 'events'
	if rand(1, 100) <= 20: gt 'train', 'events'
	gs 'stat'
	'<center><img <<$set_imgh>> src="images/locations/shared/train/oldplat.jpg" ></center>'
	'After 15 minutes you arrive at an old railway platform near the communal village.'
	act 'Get off the train':
		if train_event = 0 and rand(1, 100) <= 5: gt 'train_incidental', 'end'
		gt 'train', 'communal'
	end
end

if $ARGS[0] = 'village':
	$loc = 'train'
	$metka = 'village'
	minut += 25
	if rand(1, 100) <= 10: gt 'train_incidental', 'events'
	if rand(1, 100) < 20: gt 'train', 'events'
	gs 'stat'
	'<center><img <<$set_imgh>> src="images/locations/shared/train/gadplat.jpg" ></center>'
	'After 25 minutes, the train stops at the small station near the village of Gadyukino.'
	act 'Get off the train':
		if train_event = 0 and rand(1, 100) <= 5: gt 'train_incidental', 'end'
		gt 'train', 'village'
	end
end

if $ARGS[0] = 'oldtown':
	$loc = 'train'
	$metka = 'oldtown'
	minut += 35
	if rand(1, 100) < 15: gt 'train', 'private'
	if rand(1, 100) < 30: gt 'train', 'Gopnik'
	if rand(1, 100) <= 10: gt 'train_incidental', 'events'
	if rand(1, 100) < 20: gt 'train', 'events'
	gs 'stat'
	'<center><img <<$set_imgh>> src="images/locations/shared/train/pushplat.jpg" ></center>'
	'After 35 minutes, the train stops at old platform by the old town of Pushkin. Famous for its extensive park and old historic buildings, the locals are known to be eccentric but that adds to the charm.'
	act 'Get off the train':
		if train_event = 0 and rand(1, 100) <= 5: gt 'train_incidental', 'end'
		gt 'train', 'oldtown'
	end
end

if $ARGS[0] = 'pavlovsk':
	$loc = 'vokzalperG'
	$metka = ''
	minut += 40
	if rand(1, 100) < 15: gt 'train', 'private'
	if rand(1, 100) < 30: gt 'train', 'Gopnik'
	if rand(1, 100) <= 10: gt 'train_incidental', 'events'
	if rand(1, 100) <= 20: gt 'train', 'events'
	gs 'stat'
	if train_event = 0 and rand(1, 100) < 20: gt 'train', 'events'
	'<center><img <<$set_imgh>> src="images/locations/shared/train/pavplat.jpg" ></center>'
	'After 40 minutes, you see the town of Pavlovsk approaching. Pavlovsk Castle is an impressive sight, even from a long distance. The train stops at its final destination, a platform in Pavlovsk station.'
	act 'Get off the train':
		if train_event = 0 and rand(1, 100) <= 5: gt 'train_incidental', 'end'
		gt 'vokzalperG'
	end
end

--- cityTrain ---------------------------------

