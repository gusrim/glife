# exercise


!{This will centralize calls for exercise. For calls where you wish to allow multiple types of exercises, such as a bedroom or yard, use gs 'exercise', 'start' 
For direct calls use this format: gs 'exercise', $ARGS[0], ARG[1], $ARGS[2], $ARGS[3], $ARGS[4]
$ARGS[0] is the workout tier and the higher the tier, the greater the workout intensity.
	Values: 'tier1' 'tier2' 'tier3' 'tier4'
ARG[1] is the time override. By default the tiers are in 15 minute segments if being called from an event with predetermined time set this to 1
	Values: 0 (default of 15 minutes) 1 (time set to 0)
$ARGS[2] and on are the stats to be affected by the workout NOTE: use the _exp variant, not the pcs_!!!! 
	Values: 'stren_exp' 'vital_exp' 'agil_exp' 'sprt_exp' 'react_exp' 
	Other stats can be added if desired - for a dance-a-thon you might use gs 'exercise', 'tier1', 0, 'agil_exp', 'danc_exp'

}


if $ARGS[0] = 'start':
	if $loc = 'Meadow':
		if sunWeather = 1:
			if pcs_stam >=15 or pcs_stam >= 10 and bookYog + obruch > 0:
				if $clothingworntype ! 'exercise' and pcs_exhibition < 35:
					'<center>You could do some exercises here if you were wearing workout clothes.</center>'
				elseif $clothingworntype ! 'exercise' and pcs_exhibition >= 35 and Miraloc ! 2 and temper >= 10:
					'<center>Your meadow is a perfect place to do some exercises. There is no one around, so you could do it <a href="exec: gt ''exercise'', ''workout'', ''nude'', ''Meadow''"> in the nude</a>.</center>'
				else
					'<center>Your meadow is a perfect place to do some <a href="exec: gt ''exercise'', ''workout''">exercises</a>.</center>'
				end
			else
				'<center>Your meadow is a perfect place to do some exercises, but you don''t have the energy right now.</center>'
			end
		end
	elseif (pcs_stam >=15 or pcs_stam >= 10 and bookYog + obruch > 0) and $loc ! 'Meadow': 
		'<center>There is enough space in the room to <a href="exec: gt ''exercise'', ''workout''">exercise</a>.</center>'
	else
		'<center>There is enough space in the room for a variety of exercises, but you don''t have the energy to work out now.</center>'
	end
end

if $ARGS[0] = 'workout':
	cla
	if $loc = 'Meadow':
		$sportloc = 'Meadow'
		gs 'clothing', 'strip', $sportloc
	else
		$sportloc = ''
	end
	gs 'stat'
	if $ARGS[1] = 'nude': 
		gs 'arousal', 'foreplay', 15, 'exhibitionism'
		gs 'arousal', 'end'
		clothesAtLocation = FUNC('lost_clothes_here',$sportloc)
		minut -= 15
		gs 'stat'
		if exhibitionQW < 3: 'Your cheeks feel hot with embarrassment as you strip off your clothes. You giggle nervously at the thought of getting caught in the nude - the thought terrifies you but it also excites you.'
	end
	*nl
	'<a href="exec:gt ''exercise'', ''setup''">Click here</a> to set up your automated exercise options.'
	if clothesAtLocation = 1 and $clothingworntype = 'nude':
		act 'End workout and get dressed':
			cla
			minut += 5
			gs 'clothing', 'recover_lost_clothes', $sportloc, 1
			gt $loc, $metka
		end
	else	
		act 'Leave': gt $loc, $metka
	end
	act 'Manual': gt 'exercise', 'manual'
	if exer_stam[1] ! 0 and pcs_stam > exer_stam[1]: act '<<$excer_name[1]>> - (<<exer_stam[1]>> stamina)': x = 1 & gt 'exercise', 'auto'
	if exer_stam[2] ! 0 and pcs_stam > exer_stam[2]: act '<<$excer_name[2]>> - (<<exer_stam[2]>> stamina)': x = 2 & gt 'exercise', 'auto'
	if exer_stam[3] ! 0 and pcs_stam > exer_stam[3]: act '<<$excer_name[3]>> - (<<exer_stam[3]>> stamina)': x = 3 & gt 'exercise', 'auto'
	if exer_stam[4] ! 0 and pcs_stam > exer_stam[4]: act '<<$excer_name[4]>> - (<<exer_stam[4]>> stamina)': x = 4 & gt 'exercise', 'auto'
end

if $ARGS[0] = 'auto':
	exer_auto = 1
	if exer_rout0[x] > 0:
		timemult = exer_rout0[x] & gs 'exercise', 'timestring' & gt 'exercise', 'push', 'auto'
	elseif exer_rout1[x] > 0:
		timemult = exer_rout1[x] & gs 'exercise', 'timestring' & gt 'exercise', 'press', 'auto'
	elseif exer_rout2[x] > 0:
		timemult = exer_rout2[x] & gs 'exercise', 'timestring' & gt 'exercise', 'rope', 'auto'
	elseif exer_rout3[x] > 0:
		timemult = exer_rout3[x] & gs 'exercise', 'timestring' & gt 'exercise', 'yoga', 'auto'
	elseif exer_rout4[x] > 0:
		timemult = exer_rout4[x] & gs 'exercise', 'timestring' & gt 'exercise', 'hula', 'auto'
	end
end

if $ARGS[0] = 'auto1':
	if exer_rout1[x] > 0:
		timemult = exer_rout1[x] & gs 'exercise', 'timestring' & gt 'exercise', 'press', 'auto'
	elseif exer_rout2[x] > 0:
		timemult = exer_rout2[x] & gs 'exercise', 'timestring' & gt 'exercise', 'rope', 'auto'
	elseif exer_rout3[x] > 0:
		timemult = exer_rout3[x] & gs 'exercise', 'timestring' & gt 'exercise', 'yoga', 'auto'
	elseif exer_rout4[x] > 0:
		timemult = exer_rout4[x] & gs 'exercise', 'timestring' & gt 'exercise', 'hula', 'auto'
	end
end

if $ARGS[0] = 'auto2':
	if exer_rout2[x] > 0:
		timemult = exer_rout2[x] & gs 'exercise', 'timestring' & gt 'exercise', 'rope', 'auto'
	elseif exer_rout3[x] > 0:
		timemult = exer_rout3[x] & gs 'exercise', 'timestring' & gt 'exercise', 'yoga', 'auto'
	elseif exer_rout4[x] > 0:
		timemult = exer_rout4[x] & gs 'exercise', 'timestring' & gt 'exercise', 'hula', 'auto'
	else
		gt 'exercise', 'auto_end'
	end
end

if $ARGS[0] = 'auto3':
	if exer_rout3[x] > 0:
		timemult = exer_rout3[x] & gs 'exercise', 'timestring' & gt 'exercise', 'yoga', 'auto'
	elseif exer_rout4[x] > 0:
		timemult = exer_rout4[x] & gs 'exercise', 'timestring' & gt 'exercise', 'hula', 'auto'
	else
		gt 'exercise', 'auto_end'
	end
end

if $ARGS[0] = 'auto4':
	if exer_rout4[x] > 0:
		timemult = exer_rout4[x] & gs 'exercise', 'timestring' & gt 'exercise', 'hula', 'auto'
	else
		gt 'exercise', 'auto_end'
	end
end

if $ARGS[0] = 'auto_end':
	exer_auto = 0
	if clothesAtLocation = 1 and $clothingworntype = 'nude':
		cla
		minut += 5
		gs 'clothing', 'recover_lost_clothes', $sportloc, 1
		gt $loc, $metka
	else	
		gt $loc, $metka
	end
end

if $ARGS[0] = 'manual':
	cla
	if pcs_stam >= 15:
		Act 'Do pushups for 15 minutes': timemult = 1 & gs 'exercise', 'timestring' & gt 'exercise', 'push', 'manual'
		Act 'Do crunches for 15 minutes': timemult = 1 & gs 'exercise', 'timestring' & gt 'exercise', 'press', 'manual'
		if skak > 0 and $loc ! 'meadow':
			Act 'Jump rope for 15 minutes': timemult = 1 & gs 'exercise', 'timestring' & gt 'exercise', 'rope', 'manual'
		end
	end
	if pcs_stam >= 10:
		if bookYog > 0:
			Act 'Do yoga for 15 minutes': timemult = 1 & gs 'exercise', 'timestring' & gt 'exercise', 'yoga', 'manual'
		end
		if obruch > 0 and $loc ! 'meadow': 
			Act 'Use your hula hoop for 15 minutes': timemult = 1 & gs 'exercise', 'timestring' & gt 'exercise', 'hula', 'manual'
		end
	end
	if pcs_stam >= 30:
		Act 'Do pushups for 30 minutes': timemult = 2 & gs 'exercise', 'timestring' & gt  'exercise', 'push', 'manual'
		Act 'Do crunches for 30 minutes': timemult = 2 & gs 'exercise', 'timestring' & gt  'exercise', 'press', 'manual'
		if skak > 0 and $loc ! 'meadow':
			Act 'Jump rope for 30 minutes': timemult = 2 & gs 'exercise', 'timestring' & gt  'exercise', 'rope', 'manual'
		end
	end
	if pcs_stam >= 20:
		if bookYog > 0:
			Act 'Do yoga for 30 minutes': timemult = 2 & gs 'exercise', 'timestring' & gt  'exercise', 'yoga', 'manual'
		end
		if obruch > 0 and $loc ! 'meadow': 
			Act 'Use your hula hoop for 30 minutes': timemult = 2 & gs 'exercise', 'timestring' & gt  'exercise', 'hula', 'manual'
		end
	end
	if pcs_stam >= 45:
		Act 'Do pushups for 45 minutes': timemult = 3 & gs 'exercise', 'timestring' & gt  'exercise', 'push', 'manual'
		Act 'Do crunches for 45 minutes': timemult = 3 & gs 'exercise', 'timestring' & gt  'exercise', 'press', 'manual'
		if skak > 0 and $loc ! 'meadow':
			Act 'Jump rope for 45 minutes': timemult = 3 & gs 'exercise', 'timestring' & gt  'exercise', 'rope', 'manual'
		end
	end
	if pcs_stam >= 30:
		if bookYog > 0:
			Act 'Do yoga for 45 minutes': timemult = 3 & gs 'exercise', 'timestring' & gt  'exercise', 'yoga', 'manual'
		end
		if obruch > 0 and $loc ! 'meadow': 
			Act 'Use your hula hoop for 45 minutes': timemult = 3 & gs 'exercise', 'timestring' & gt  'exercise', 'hula', 'manual'
		end
	end
	if pcs_stam >= 60:
		Act 'Do pushups for 60 minutes': timemult = 4 & gs 'exercise', 'timestring' & gt  'exercise', 'push', 'manual'
		Act 'Do crunches for 60 minutes': timemult = 4 & gs 'exercise', 'timestring' & gt  'exercise', 'press', 'manual'
		if skak > 0 and $loc ! 'meadow':
			Act 'Jump rope for 60 minutes': timemult = 4 & gs 'exercise', 'timestring' & gt  'exercise', 'rope', 'manual'
		end
	end
	if pcs_stam >= 40:
		if bookYog > 0:
			Act 'Do yoga for 60 minutes': timemult = 4 & gs 'exercise', 'timestring' & gt  'exercise', 'yoga', 'manual'
		end
		if obruch > 0 and $loc ! 'meadow': 
			Act 'Use your hula hoop for 60 minutes': timemult = 4 & gs 'exercise', 'timestring' & gt  'exercise', 'hula', 'manual'
		end
	end
	if pcs_stam < 10 or pcs_stam < 15 and bookYog + obruch = 0:
		'You don''t have the energy to work out anymore now.'
	end
	if clothesAtLocation = 1 and $clothingworntype = 'nude':
		Act 'End workout and get dressed':
			cla
			minut += 5
			gs 'clothing', 'recover_lost_clothes', $sportloc, 1
			gt $loc, $metka
		end
	else	
		Act 'Leave': gt $loc, $metka
	end
end

if $ARGS[0] = 'setup':
	*clr
	cla
	'<center><b>Define up to 4 automated exercise routines.</b></center>'
	*nl
	*nl
	*nl
	*nl
	'<center><table><TH></TH><TH></TH><TH>Push ups</TH><TH>Crunches</TH><TH>Jump rope</TH><TH>Yoga</TH><TH>Hula hoop</TH>'
	'<TR><TD><<$excer_name[1]>></TD><TD><a href="exec:$excer_name[1] = input (''Enter name for exercise routine 1'')">rename</a></TD><TD><<exer_rout0[1]*15>> minutes</TD><TD><<exer_rout1[1]*15>> minutes</TD><TD><<exer_rout2[1]*15>> minutes</TD><TD><<exer_rout3[1]*15>> minutes</TD><TD><<exer_rout4[1]*15>> minutes</TD>'
	'<TR><TD><<$excer_name[2]>></TD><TD><a href="exec:$excer_name[2] = input (''Enter name for exercise routine 2'')">rename</a></TD><TD><<exer_rout0[2]*15>> minutes</TD><TD><<exer_rout1[2]*15>> minutes</TD><TD><<exer_rout2[2]*15>> minutes</TD><TD><<exer_rout3[2]*15>> minutes</TD><TD><<exer_rout4[2]*15>> minutes</TD>'
	'<TR><TD><<$excer_name[3]>></TD><TD><a href="exec:$excer_name[3] = input (''Enter name for exercise routine 3'')">rename</a></TD><TD><<exer_rout0[3]*15>> minutes</TD><TD><<exer_rout1[3]*15>> minutes</TD><TD><<exer_rout2[3]*15>> minutes</TD><TD><<exer_rout3[3]*15>> minutes</TD><TD><<exer_rout4[3]*15>> minutes</TD>'
	'<TR><TD><<$excer_name[4]>></TD><TD><a href="exec:$excer_name[4] = input (''Enter name for exercise routine 4'')">rename</a></TD><TD><<exer_rout0[4]*15>> minutes</TD><TD><<exer_rout1[4]*15>> minutes</TD><TD><<exer_rout2[4]*15>> minutes</TD><TD><<exer_rout3[4]*15>> minutes</TD><TD><<exer_rout4[4]*15>> minutes</TD>'
	'</table></center>'

	act 'Return': gt 'exercise', 'workout'
	act 'Define <<$excer_name[1]>>':i = 1 & gt 'exercise', 'matrix'
	act 'Define <<$excer_name[2]>>':i = 2 &  gt 'exercise', 'matrix'
	act 'Define <<$excer_name[3]>>':i = 3 &  gt 'exercise', 'matrix'
	act 'Define <<$excer_name[4]>>':i = 4 &  gt 'exercise', 'matrix'
!!exer_rout1 = default
end

if $ARGS[0] = 'matrix':
	*clr
	cla
	exer_stam[i] = exer_stam0[i] + exer_stam1[i] + exer_stam2[i] + exer_stam3[i] + exer_stam4[i]
	act'Confirm':
		gt 'exercise', 'setup'
	end

	'Choose your exercise options from the following table:'
	'<center><table border=0 cellspacing=0 cellpadding=10 width=1000><TH>Exercise</TH><TH>None</TH><TH>15 mins</TH><TH>30 mins</TH><TH>45 mins</TH><TH>60 mins</TH>'
	gs 'exercise', 'matrixdata', 0, 'Push ups'
	gs 'exercise', 'matrixdata', 1, 'Crunches'
	if skak > 0 and $loc ! 'meadow': gs 'exercise', 'matrixdata', 2, 'Jump rope'
	if bookYog > 0: gs 'exercise', 'matrixdata', 3, 'Yoga'
	if obruch > 0 and $loc ! 'meadow': gs 'exercise', 'matrixdata', 4, 'Hula hoop'

	'</center></table>'
	*nl
	'Total stamina required - <<exer_stam[i]>>'
end

if $ARGS[0] = 'matrixdata':
	'<TR>'
	*p '<TD><<$ARGS[2]>></TD>'

	if dyneval('RESULT = exer_rout<<ARGS[1]>>[<<i>>]') = 0:
		dynamic 'exer_stam<<ARGS[1]>>[<<i>>] = 0'
		*p '<TD>Selected</TD>'
	else
		*p '<TD><a href="exec:dynamic ''exer_rout<<ARGS[1]>>[i] = 0'' & gt ''exercise'', ''matrix''">Select</a></TD>'
	end

	if dyneval('RESULT = exer_rout<<ARGS[1]>>[<<i>>]') = 1:
		if ARGS[1] >= 3:
			dynamic 'exer_stam<<ARGS[1]>>[<<i>>] = 10'
		else
			dynamic 'exer_stam<<ARGS[1]>>[<<i>>] = 15'
		end
		*p '<TD>Selected</TD>'
	else
		*p '<TD><a href="exec:dynamic ''exer_rout<<ARGS[1]>>[i] = 1'' & gt ''exercise'', ''matrix''">Select</a></TD>'
	end

	if dyneval('RESULT = exer_rout<<ARGS[1]>>[<<i>>]') = 2:
		if ARGS[1] >= 3:
			dynamic 'exer_stam<<ARGS[1]>>[<<i>>] = 20'
		else
			dynamic 'exer_stam<<ARGS[1]>>[<<i>>] = 30'
		end
		*p '<TD>Selected</TD>'
	else
		*p '<TD><a href="exec:dynamic ''exer_rout<<ARGS[1]>>[i] = 2'' & gt ''exercise'', ''matrix''">Select</a></TD>'
	end

	if dyneval('RESULT = exer_rout<<ARGS[1]>>[<<i>>]') = 3:
		if ARGS[1] >= 3:
			dynamic 'exer_stam<<ARGS[1]>>[<<i>>] = 30'
		else
			dynamic 'exer_stam<<ARGS[1]>>[<<i>>] = 45'
		end
		*p '<TD>Selected</TD>'
	else
		*p '<TD><a href="exec:dynamic ''exer_rout<<ARGS[1]>>[i] = 3'' & gt ''exercise'', ''matrix''">Select</a></TD>'
	end

	if dyneval('RESULT = exer_rout<<ARGS[1]>>[<<i>>]') = 4:
		if ARGS[1] >= 3:
			dynamic 'exer_stam<<ARGS[1]>>[<<i>>] = 40'
		else
			dynamic 'exer_stam<<ARGS[1]>>[<<i>>] = 60'
		end
		*p '<TD>Selected</TD>'
	else
		*p '<TD><a href="exec:dynamic ''exer_rout<<ARGS[1]>>[i] = 4'' & gt ''exercise'', ''matrix''">Select</a></TD>'
	end

	exer_stam[i] = exer_stam0[i] + exer_stam1[i] + exer_stam2[i] + exer_stam3[i] + exer_stam4[i]
end

if $ARGS[0] = 'hula':
	cla
	*clr
	loopcount = 1
	:timeloophula
	gs 'exercise', 'tier2', 0, 'agil_exp', 'react_exp'
	if loopcount < timemult: loopcount += 1 & jump 'timeloophula'
	loopcount = 0
	timemult = 0
	if $clothingworntype ! 'nude':
		if $location_type = 'secluded':
			if month >=5 and month <= 9:
				'<center><img <<$set_imgh>> src="images/pc/activities/hulaoutdoors.jpg"></center>'
			else
				'<center><img <<$set_imgh>> src="images/pc/activities/hulawinter.gif"></center>'
			end
		else
			'<center><img <<$set_imgh>> src="images/pc/activities/huladressed.gif"></center>'
		end
	else
		'<center><img <<$set_imgh>> src="images/pc/activities/obruch.gif"></center>'
	end
	'You develop your dexterity by using your hula hoop for <<$timestring>> minutes.'

	if exer_auto = 1:
		act 'Continue': gt 'exercise', 'auto_end'
	else
		act 'Continue': gt 'exercise', 'manual'
	end
end

if $ARGS[0] = 'yoga':
	cla
	*clr
	loopcount = 1
	:timeloopyoga
	gs 'exercise', 'tier2', 0, 'sprt_exp', 'agil_exp', 'pcs_willpwr'
	if loopcount < timemult: loopcount += 1 & jump 'timeloopyoga'
	loopcount = 0
	timemult = 0

	if $clothingworntype ! 'nude':
		'<center><img <<$set_imgh>> src="images/pc/activities/yoga_dressed.jpg"></center>'
	elseif tanga = 1:
		'<center><img <<$set_imgh>> src="images/pc/activities/yoga_underwear.jpg"></center>'
	else
		'<center><img <<$set_imgh>> src="images/pc/activities/yoga_naked.png"></center>'
	end

	'You meditate for <<$timestring>> minutes, developing your will.'

	if exer_auto = 1:
		act 'Continue': gt 'exercise', 'auto4'
	else
		act 'Continue': gt 'exercise', 'manual'
	end
end

if $ARGS[0] = 'rope':
	cla
	*clr
	loopcount = 1
	:timelooprope
	gs 'exercise', 'tier3', 0, 'agil_exp', 'react_exp'
	if loopcount < timemult: loopcount += 1 & jump 'timelooprope'
	loopcount = 0
	timemult = 0
	
	if $clothingworntype ! 'nude':
		'<center><img <<$set_imgh>> src="images/pc/activities/rope_dressed.jpg"></center>'
	elseif tanga = 1:
		'<center><img <<$set_imgh>> src="images/pc/activities/rope_underwear.jpg"></center>'
	else
		'<center><img <<$set_imgh>> src="images/pc/activities/rope_naked.jpg"></center>'
	end

	'You jump rope for <<$timestring>> minutes, developing your speed.'

	if exer_auto = 1:
		act 'Continue': gt 'exercise', 'auto3'
	else
		act 'Continue': gt 'exercise', 'manual'
	end
end

if $ARGS[0] = 'press':
	cla
	*clr
	loopcount = 1
	:timelooppress
	gs 'exercise', 'tier3', 0, 'vital_exp'
	if loopcount < timemult: loopcount += 1 & jump 'timelooppress'
	loopcount = 0
	timemult = 0
	
	if $clothingworntype ! 'nude':
		'<center><img <<$set_imgh>> src="images/pc/activities/abdominal_dressed.jpg"></center>'
	elseif tanga = 1:
		'<center><img <<$set_imgh>> src="images/pc/activities/abdominal_underwear.jpg"></center>'
	else
		'<center><img <<$set_imgh>> src="images/pc/activities/abdominal_naked.jpg"></center>'
	end

	'You do a series of abdominal exercises for <<$timestring>> minutes, developing your endurance.'

	if exer_auto = 1:
		act 'Continue': gt 'exercise', 'auto2'
	else
		act 'Continue': gt 'exercise', 'manual'
	end
end

if $ARGS[0] = 'push':
	cla
	*clr
	loopcount = 1
	:timelooppush
	gs 'exercise', 'tier3', 0, 'stren_exp'
	if loopcount < timemult: loopcount += 1 & jump 'timelooppush'
	loopcount = 0
	timemult = 0

	if $clothingworntype ! 'nude':
		'<center><img <<$set_imgh>> src="images/pc/activities/push_dressed.jpg"></center>'
	elseif tanga = 1:
		'<center><img <<$set_imgh>> src="images/pc/activities/push_underwear.jpg"></center>'
	else
		'<center><img <<$set_imgh>> src="images/pc/activities/push_naked.jpg"></center>'
	end
	'You do push-ups for <<$timestring>> minutes, developing your strength.'
	pushnum = pcs_stren * pcs_sleep * rand(1,3) / 200
	'<br>You managed to do <<pushnum>> push-ups. Your previous record is <<pushrecord>>.'

	if pushrecord < pushnum:pushrecord = pushnum & 'This is a new record!'

	if exer_auto = 1:
		act 'Continue': gt 'exercise', 'auto1'
	else
		act 'Continue': gt 'exercise', 'manual'
	end
end

if $ARGS[0] = 'tier1':
	if ARGS[1] = 0: minut += 15
	pcs_stam -= 5
	pcs_sweat += 3
	fat -= 1
	pcs_mood += 3
	pcs_willpwr += 3
	if $ARGS[3] = '':
		dynamic '<<$ARGS[2]>> += 1'
		if $ARGS[2] = 'vital_exp' or $ARGS[2] = 'stren_exp':
			dynamic '<<$ARGS[2]>> += rand(0,steroid_dose)'
			if steroid_dose > 0 and rand(1,5) <= steroid_dose:clit_size += rand(0,1) 
		end
	else
		i = 2
		:exploop
		if $ARGS[i] ! '': 
			dynamic '<<$ARGS[i]>> += rand(0,1)'
			if $ARGS[i] = 'vital_exp' or $ARGS[i] = 'stren_exp': dynamic '<<$ARGS[i]>> += rand(0,steroid_dose) & steroidcheck = 1'
			i += 1
			jump 'exploop'
		else
			if steroidcheck = 1 and steroid_dose > 0 and rand(1,5) <= steroid_dose:clit_size += rand(0,1)
			killvar 'steroidcheck'
			killvar 'i'
			exit
		end
	end
end
	
if $ARGS[0] = 'tier2':
	if ARGS[1] = 0: minut += 15
	pcs_stam -= 10
	pcs_sweat += 10
	fat -= 2
	pcs_mood += 5
	pcs_willpwr += 5
	if $ARGS[3] = '':
		dynamic '<<$ARGS[2]>> += rand(2,3)'
		if $ARGS[2] = 'vital_exp' or $ARGS[2] = 'stren_exp':
			dynamic '<<$ARGS[2]>> += rand(0,steroid_dose)'
			if steroid_dose > 0 and rand(1,5) <= steroid_dose:clit_size += rand(0,1) 
		end
	else
		i = 2
		:exploop2
		if $ARGS[i] ! '': 
			dynamic '<<$ARGS[i]>> += rand(1,2)'
			if $ARGS[i] = 'vital_exp' or $ARGS[i] = 'stren_exp': dynamic '<<$ARGS[i]>> += rand(0,steroid_dose) & steroidcheck = 1'
			i += 1
			jump 'exploop2'
		else
			if steroidcheck = 1 and steroid_dose > 0 and rand(1,5) <= steroid_dose:clit_size += rand(0,1)
			killvar 'steroidcheck'
			killvar 'i'
		end
	end
end

if $ARGS[0] = 'tier3':
	if ARGS[1] = 0: minut += 15
	pcs_stam -= 15
	pcs_sweat += 15
	fat -= 3
	pcs_mood += 5
	pcs_willpwr += 5
	if $ARGS[3] = '':
		dynamic '<<$ARGS[2]>> += rand(3,5)'
		if $ARGS[2] = 'vital_exp' or $ARGS[2] = 'stren_exp':
			dynamic '<<$ARGS[2]>> += rand(0,steroid_dose)'
			if steroid_dose > 0 and rand(1,5) <= steroid_dose:clit_size += rand(0,1) 
		end
	else
		i = 2
		:exploop3
		if $ARGS[i] ! '': 
			dynamic '<<$ARGS[i]>> += rand(2,3)'
			if $ARGS[i] = 'vital_exp' or $ARGS[i] = 'stren_exp': dynamic '<<$ARGS[i]>> += rand(0,steroid_dose) & steroidcheck = 1'
			i += 1
			jump 'exploop3'
		else
			if steroidcheck = 1 and steroid_dose > 0 and rand(1,5) <= steroid_dose:clit_size += rand(0,1)
			killvar 'steroidcheck'
			killvar 'i'
		end
	end
end

if $ARGS[0] = 'tier4':
	if ARGS[1] = 0: minut += 15
	pcs_stam -= 25
	pcs_sweat += 25
	fat -= 4
	pcs_mood += 5
	pcs_willpwr += 5
	if $ARGS[3] = '':
		dynamic '<<$ARGS[2]>> += rand(7,10)'
		if $ARGS[2] = 'vital_exp' or $ARGS[2] = 'stren_exp':
			dynamic '<<$ARGS[2]>> += rand(0,steroid_dose)'
			if steroid_dose > 0 and rand(1,5) <= steroid_dose:clit_size += rand(0,1) 
		end
	else
		i = 2
		:exploop4
		if $ARGS[i] ! '': 
			dynamic '<<$ARGS[i]>> += rand(3,5)'
			if $ARGS[i] = 'vital_exp' or $ARGS[i] = 'stren_exp': dynamic '<<$ARGS[i]>> += rand(0,steroid_dose) & steroidcheck = 1'
			i += 1
			jump 'exploop4'
		else
			if steroidcheck = 1 and steroid_dose > 0 and rand(1,5) <= steroid_dose:clit_size += rand(0,1)
			killvar 'steroidcheck'
			killvar 'i'
		end
	end
end

if $ARGS[0] = 'timestring':
	if timemult = 1: $timestring = 'fifteen' 
	if timemult = 2: $timestring = 'thirty'
	if timemult = 3: $timestring = 'forty-five'
	if timemult = 4: $timestring = 'sixty'
end

--- exercise ---------------------------------

