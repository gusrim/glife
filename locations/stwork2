# stwork2

if $ARGS[0] = 'strip':
	clr
	gs 'stat'
	'<center><b><font color="maroon">Stage</font></b></center>'
	'<center><img <<$set_imgh>> src="images/locations/city/citycenter/stripclub/stripclub1.jpg"></center>'

	if publichot < 10:$pubhot = 'booing you, you hear someone shout, "Get this stupid cow off the stage."'
	if publichot >= 10 and publichot < 20:$pubhot = 'clearly showing disconsent with your dancing. They want you off the stage as quickly as possible.'
	if publichot >= 20 and publichot < 30:$pubhot = 'clearly looking bored, drinking and talking among themselves, ignoring you.'
	if publichot >= 30 and publichot < 60:$pubhot = 'sometime looking at you, smiling and enjoy the performence.'
	if publichot >= 60 and publichot < 70:$pubhot = 'watching you with genuine interest while conversing with each other over their beers.'
	if publichot >= 70 and publichot < 80:$pubhot = 'having their attention focused on you.'
	if publichot >= 80 and publichot < 90:$pubhot = 'with strained faces, sweat beading on their foreheads, their arousal clearly showing.'
	if publichot >= 90:$pubhot = 'completely indulgded in you. They''re clapping and waving money at you trying to get you attention.'

	'There are men sitting in front of the stage <<$pubhot>>.'

	if hour >= 15 or hour <= 2:
		if stpredmet > 0:
			act 'Striptease 5 minutes (<<stpredmet>> items of clothing)':
				cls
				minut += 5
				stpredmet -= 1
				pcs_sweat += 5
				fat -= 3
				pcs_mood -= rand(2, 6)
				dancero_exp += rand(2, 4)
				if pcs_exhibition < 50:exhibition_exp += rand(1,3)
				city_stripping += rand(1,2)

				if stpredmet = 1:pcs_sweat += 5
				if stpredmet = 0:pcs_hairbsh = 0 & stripdancesum += 1

				publichot += rand(pcs_dancero/5, pcs_dancero/3)

				if publichotstart = publichot: publichot -= rand(5, 10) & publichotstart = publichot

				gs 'stat'
				'<center><img <<$set_imgh>> src="images/locations/city/citycenter/stripclub/stripclub2.jpg"></center>'
				'You walk up on the stage and hear the music start. The effect is immediate, you start moving seductively on stage, teasingly undressing on stage...'

				if pcs_weight > pcs_hgt - 90:
					vesrand = rand(0, 100)

					if vesrand >= 75:
						publichot -= rand(5, 10)
						'You overhear some of the customers right next to the stage, "Look at her fat hanging out. Her gut and ass shaking like jelly."'
					end
				end

				gs 'stwork2', 'tipcalculator'

				act 'Continue': gt 'stwork2', 'strip'
			end

			act 'Dance around the pole 5 minutes (<<stpredmet>> garments)':
				cls
				minut += 5
				stpredmet -= 1
				pcs_sweat += 5
				fat -= 5
				pcs_mood -= rand(4, 8)
				dancpol_exp += rand(2, 4)
				if pcs_exhibition < 65:exhibition_exp += rand(1,3)
				city_stripping += rand(1,2)
				
				if stpredmet = 1:pcs_hairbsh = 0
				if stpredmet = 0:pcs_makeup = 0 & stripdancesum += 1

				!pcs_hairbsh = 0
				publichot += rand(pcs_dancero / 10, pcs_dancero / 5)+ rand(pcs_dancpol / 5, pcs_dancpol / 2)

				if publichotstart = publichot:publichot -= rand(5, 10) & publichotstart = publichot

				gs 'stat'
				'<center><img <<$set_imgh>> src="images/locations/city/citycenter/stripclub/strippole.jpg"></center>'
				'You step up on the stage and the music starts. You grab hold of the pole and acrobatically slide around it while you teasingly undress.'

				if pcs_weight > pcs_hgt - 90:
					vesrand = rand(0, 100)

					if vesrand >= 75:
						'Performing another trick on the pole you can not support your weight and fall flat to the floor. The crowd laughs, "Hey, hippopotamus, stomp back to the swamp where you came from."'
						publichot -= 15
						fingalrand = rand(0, 100)
						if fingalrand >= 75:
							fingal += rand(2, 5)
							gs 'pain', 4, 'cheeks', 'Slam'
							gs 'stat'
							'As you fall, you hit the floor really hard and you can feel a bruise forming on your face.'
						end
					end
					if vesrand >= 50:
						publichot -= rand(5, 10)
						'You overhear some of the customers right next to the stage, "Look at her fat hanging out. Her gut and ass shaking like jelly."'
					end
				end

				gs 'stwork2', 'tipcalculator'

				act 'Continue': gt 'stwork2', 'strip'
			end
		end

		if stpredmet = 0:
			if publichot >= 70:
				privatrand = rand(0, 100)
				if privatrand >= 70:
					'After you finish dance, the security guard approaches you and tells you that you have a customer wanting a private show.'
					gs 'stwork2', 'privatdance'
					exit
				end
			end
		end
	else
		'Your shift is over for tonight. Time to head home.'
	end

	act 'Leave the stage':gt 'stwork', 'start'
end

if $ARGS[0] = 'tipcalculator':
	chance = 0
	paymin = 0
	paymax = 0
	
	if publichot < 30:
		'Nobody even noticed you or your efforts on the stage.'
	elseif publichot < 60:
		'A couple of people looked at you but nothing more than that, you didn''t recieve a tip.'
	elseif publichot < 70:
		paymax = 1
		if payrand = 0: 'A couple of people looked at you but nothing more than that, you didn''t recieve a tip.'
	elseif publichot < 80:
		chance = 5
		paymin = 3
		paymax = 10
		'The customers are pleased with your performence. They give you a small tip.'
	elseif publichot < 90:
		chance = 10
		paymin = 10
		paymax = 50
		'The customers are following you move between their conversations. As you finish they give you a decent tip.'
	else
		chance = 15
		paymin = 10
		paymax = 100
		'The customers are paying their full attention to your dancing and are tipping quite nicely.'
	end
	
	payrand = rand(0, chance)
	if payrand = 0:
		'No one tipped you.'
	else
		paymoneyrand = rand(paymin, paymax)
		money += paymoneyrand * 10
		pcs_mood += rand(1,2)
		'Some guy shoved <<paymoneyrand>>0 <b>₽</b> in your panties and slapped you on the ass.'
	
	end
end

if $ARGS[0] = 'privatdance':
	act 'Go and perform a lap dance':
		cls
		minut += 30
		money += 800
		if pcs_sweat < 30 : pcs_sweat += 10
		pcs_hairbsh = 0
		pcs_makeup = 0
		fat -= 5
		pcs_mood += rand(2,6)
		dancero_exp += rand(2, 4)
		stripdancesum += 1

		gs 'stat'
		'<center><img <<$set_imgh>> src="images/locations/city/citycenter/stripclub/privatstrip.jpg"></center>'
		'You walk over to a smaller private room where you put on some music before the customer walks in. As the customer walks in you tell him to sit down and enjoy the show. You spend half an hour showing off your dancing skills, and every time you grind against him you can feel his rock hard cock.'

		stprrand = rand(0,5)
		if stprrand = 0:gt 'stwork2', 'pr1'
		if stprrand = 1:gt 'stwork2', 'pr2'
		if stprrand = 2:gt 'stwork2', 'pr3'

		act 'Return to the locker room':gt 'stwork', 'start'
	end
end

if $ARGS[0] = 'pr1':
	cla
	*clr
	'<center><img <<$set_imgh>> src="images/locations/city/citycenter/stripclub/pr1.jpg"></center>'
	'Just as you start to dance, you hear a knock on the door. His friends are waiting outside wanting to join in on the dance. Feeling trapped you agree and start dancing, after a while they tell you to stop and ask if you''re willing to go all the way for 5000 <b>₽</b>.'

	act 'Reject them':gt 'stwork', 'start'

	act 'Agree':
		gang += 1
		guy += 4
		killvar '$boy'
		i=rand(18,40)
		gs 'npcgeneratec', 0, 'Private dance Client', i
		gs 'boyStat', $npclastgenerated
		gs 'npcgeneratec', 0, 'Friend of Client', i
		gs 'boyStat', $npclastgenerated,'1'
		gs 'npcgeneratec', 0, 'Friend of Client', i
		gs 'boyStat', $npclastgenerated,'2'
		gs 'npcgeneratec', 0, 'Friend of Client', i
		gs 'boyStat', $npclastgenerated,'3'
		picrand = 12
		money += 5000
		gt 'paysex', 'var'
	end
end

if $ARGS[0] = 'pr2':
	cla
	*clr
	'<center><img <<$set_imgh>> src="images/locations/city/citycenter/stripclub/pr2.jpg"></center>'
	'The man you''re dancing is mesmerized by you and can''t stop touching himself, after a while he asks you if he can fuck you for 3000 <b>₽</b>.'

	act 'Reject him':gt 'stwork', 'start'

	act 'Agree':
		guy += 1
		picrand = 39
		money += 3000
		xgt 'sex', 'var'
	end
end

if $ARGS[0] = 'pr3':
	cla
	*clr
	'<center><img <<$set_imgh>> src="images/locations/city/citycenter/stripclub/pr3.jpg"></center>'
	'It''s a bit different dancing for a woman, it''s not often you have the pleasure doing that so you''re feeling a bit more frisky than usual. As you finish the dance she gives you an kiss and you start making out. She stops and offers you 2000 <b>₽</b> to continue on.'

	act 'Reject her':gt 'stwork', 'start'

	act 'Agree':
		girl += 1
		lesbian += 1
		picrand = 18
		money += 2000
		$sexloc = 'stwork'
		xgt 'lezbsex', 'var'
	end
end

--- stwork2 ---------------------------------

